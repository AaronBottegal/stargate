	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E  >>>>>
	ORG	LMAORG
	JMP	LNDST$
	JMP	SCZST$
	JMP	ASTST$
	JMP	LEDTR$
	JMP	SHOOT
	JMP	SHT00
	JMP	NITRUS
	JMP	BOZOV$
	FDB	ASTRO	FIXED STORED ADDRESSES
	FDB	AFALL
	FDB	ASTKIL
	FDB	LNDP1
	JMP	PHST	PHONY LANDER START
*
*START ASTROS
*A=#
*B=BIT 6,7 OF XADDR
ASTST$	PSHS	D
	STA	XTEMP+1
ASTST1	OBI	ASTP1,ASTKIL,$6666
	JSR	RAND
	LDD	HSEED
	ANDA	#$1F
	ADDA	1,S
	STD	OX16,X
	LSRB
	BCC	ASTS2
	LDD	#ASTP3	OPPOSITE DIRECTION
	STD	OPICT,X
ASTS2	LDA	#$E0
	STA	OY16,X
	LDA	#$10
	STA	OTYP,X	NON COLLIDE
	CLRD
	STD	OYV,X
	STD	OXV,X
	STD	OBJID,X
	STX	OPTR
	STX	,Y++
	DEC	XTEMP+1
	BNE	ASTST1
	PULS	D,PC
*
*KILL ASTRO
*X=OBJECT
ASTKIL	LDA	PCFLG	PLAYER?
	BNE	NOKILL
ASTK1	BSR	ASTCLR
	JSR	KILOFF
	LDD	#ASXP1
	STD	OPICT,X
	LDD	OX16,X
	SUBD	#$40
	STD	OX16,X
	JSR	XSVCT
	LDD	#AHSND
	JMP	SNDLD
AKIL1	LDU	OBJID,X
	BEQ	ASTKIL	NOT FALLING
	LDD	PADDR,U	1ST TIME
	CMPD	#AFALL2
	BEQ	NOKILL
	LDA	PCFLG
	BEQ	AKIL10	SHOT
	LDD	#ACSND
	JSR	SNDLD
	PSHS	X
	MAKP	PPAC	DO THE INCREASING CATCH STUFF.
	LEAY	,X
	PULS	X
	STX	PD,Y	ASTRO OBJ-500 ROUTINE
AKIL2	LDD	#AFALL2
	STD	PADDR,U
NOKILL	CLRA
	PULS	D,PC
AKIL10	BSR	ASTK1
	JMP	KILLOP
*
*CLEAR ASTRO FROM LIST
*
ASTCLR	LEAY	,X
ASTC0	PSHS	X,U,A
	TST	STATUS	NO ATTRACT KILL
	BMI	ASTCX
	LDU	#TLIST
	LDA	#(TLEND-TLIST)/2
ASTC1	CMPY	,U++
	BEQ	ASTC2
	DECA
	BNE	ASTC1
	JSR	ERROR
ASTC2	CLRD
	STD	-2,U
	DEC	ASTCNT
	BNE	ASTCCX
	MAKP	TERBLO	BLOW UP TERRAIN
	PULS	X,U,PC,A
ASTCCX	LDB	ASTCNT	LOOK AT COUNT
	CMPB	#3	FEW??
	BHI	ASTCX	NOPE..BYE
	LDX	#ASFMES	FEW AST MESSAGE
	JSR	MESLDV	TRY AND SEND IT.
ASTCX	PULS	X,U,PC,A
*
ASFMES	FCB	$E0,CLEAR,$C0,FEWM
*
*TERRAIN BLOW PROCESS
*
TERGL1	LDB	STATUS
	BITB	#$74
	BEQ	TERGL2
TERGL3	NAP	$4,TERGL1
TERGL2	NAP	$4,TERBLO

TERBLO	LDB	STATUS
	BITB	#$74	HYPER OR STAR GATE OR SOMETHING
	BNE	TERGL3	YEP...STOP RIGHT THERE...
	ORB	#$06	LET SAM CLEAN UP OR GET OUT OR WHATEVER.
	STB	STATUS
TBL2	LDX	SPTR	TRASH BULLETS.
	BEQ	TBL3
	JSR	KILSHL
	BRA	TBL2
TBL3	LDB	#$C0	TIME TO SLEEP
	BSR	NITRUS	PUT THE BOYS TO SLEEP NOW
	NAP	1,TBL4	LET THE SAM ROUTINE CLEANUP
TBL4	LDX	#IPTR	FIND END OF ILIST
TBBL3	LDY	,X	GET NEXT.
	BEQ	TBBL4	FOUND IT
	LEAX	,Y	MOVE BACK TO X
	BRA	TBBL3
TBBL4	LDY	OPTR	GET ON SCREEN LIST
	STY	,X	APPEND TWO LISTS.
	LDD	#0
	STD	OPTR	AND CLEAR THE ACTIVE ONE.
	JSR	SCRCLR	CLEAR THE ENTIRE SCREEN
	INC	SAMEXC	NOW LOCK HIM OUT
	JSR	BLOWV	DO THE EFFECT PLEASE
	JSR	SCLEAN	CLEAN UP SAMRAM
	JSR	SCRCLR	CLEAR THE STUFF AWAY.
	JSR	TDISPV	PUT THE TOP STUFF BACK
	CLR	PD,U	PARAMETER TO INDICATE WHICH LIST
	LDX	#IPTR	MAKE SURE INACTIVE GUYS FAR AWAY
	BSR	HYPEM
	INC	PD,U
	LDX	#OPTR
	BSR	HYPEM
	JSR	OSCAN	REFORM LIST
	LDA	STATUS
	ANDA	#$FB
	STA	STATUS	REMOVE SAM INHIBITION
	JMP	SUCIDE

NITRUS	LDX	ACTIVE
TBL5	LDA	PTYPE,X	GET TYPE
	CMPA	#CTYPE	COIN??
	BEQ	TBL6
	CMPA	#DTYPE	DEATH??
	BEQ	TBL6	THEN NO DELAY
	CMPA	#LTYPE	LASER??
	BEQ	TBL6
	CMPA	#SBTYPE
	BEQ	TBL6
	STB	PTIME,X	MAKE MAX TIME FOR NON COINS
TBL6	LDX	,X
	BNE	TBL5
	RTS

HYPEM	LDX	,X	GET THE	NEXT MAN.
	BEQ	HYPEMX	BYE
	LDD	OX16,X	CHECK POSITION
	SUBD	BGL	ON SCREEN???
	ADDD	#100*32	OR EVEN NEARBY??
	CMPD	#500*32
	BHS	HYPEM	NOPE....MOVE TO NEXT
	LDD	OPICT,X	DON HYPER STARGATES.
	CMPD	#SGPIC
	BEQ	HYPEM	GOOD BOY ROVER
	LDD	OX16,X
	ADDD	#$8000	TRIP TO THE SPACE WARS
	STD	OX16,X
	TST	PD,U	WHICH LIST??
	BEQ	HYPEM	INACTIVE.....LEAVE ALONE..
	LDD	OBJX,X	GET COORDINATE
	BEQ	HYPEM	NOT THERE
	LDY	OPICT,X	GET PICTURE
	JSR	[OBJDEL,Y] AND CAN IT.
	BRA	HYPEM	AND LOOP
HYPEMX	RTS
*
*ASTRONAUT PROCESS
*WALK ON TERRAIN
*PD =CURRENT INDEX TO TLIST
*
ASTRO	LDX	PD,U
	LEAX	2,X
	CMPX	#TLEND
	BLO	ASTRO1
	LDX	#TLIST
ASTRO1	STX	PD,U
	LDX	,X
	BEQ	ASTX	NOONE THERE
	LDD	OBJX,X	ON SCREEN?
	BEQ	ASTX	NOPE
	LDD	OCVECT,X
	CMPD	#ASTKIL
	BNE	ASTX	CAPTURED
	LDD	OPICT,X
	CMPD	#ASTP2
	BHI	ASTR	WALKING TO RIGHT
*WALKING TO LEFT
	LDA	SEED
	CMPA	#8
	BLS	ASTRX1	TURN AROUND
	JSR	GETALT
	ADDA	#4
	CMPA	#$E8
	BLS	ASTL1
	LDA	#$E8
ASTL1	LDB	#1
	CMPA	OY16,X
	BEQ	ASTL3
	BHI	ASTL2
	NEGB
ASTL2	ADDB	OY16,X
	STB	OY16,X	UP/DOWN
ASTL3	LDU	OPICT,X	TOGGLE IMAGE
	LEAU	OBJL,U
	CMPU	#ASTP2
	BLS	ASTLX2
ASTLX1	LDU	#ASTP1
ASTLX2	LDB	#-$20	MOVE LEFT
	BRA	ASTX1
*WALKING TO RIGHT
ASTR	LDA	SEED
	CMPA	#$8
	BLS	ASTLX1	TURN AROUND
	JSR	GETALT
	ADDA	#15
	CMPA	#$E8
	BLS	ASTR1
	LDA	#$E8
ASTR1	LDB	#1
	CMPA	OY16,X
	BEQ	ASTR3
	BHI	ASTR2
	NEGB
ASTR2	ADDB	OY16,X
	STB	OY16,X
ASTR3	LDU	OPICT,X	IMAGING
	LEAU	OBJL,U
	CMPU	#ASTP4
	BLS	ASTRX2
ASTRX1	LDU	#ASTP3
ASTRX2	LDB	#$20
ASTX1	STU	OPICT,X
	SEX
	ADDD	OX16,X
	STD	OX16,X
ASTX	NAP	2,ASTRO
*
*250 POINT PROCESS
*PD=ASTRO PROCESS
*
P250	LDD	#$0125
	JSR	SCORE	SCORE IT
	LDA	OFREE	ANY OBJECTS??
	BEQ	SUCVV	NOPE...JUST DROP DEAD
	OBI	C25P1,NOKILL,$0000
	LDY	PD,U	GET THE MAN OBJECT
	LDD	OBJX,Y	IS HE ON SCREEN??
	LBNE	P5001	YEP....HANDLE LIKE A SET DOWN
	LDD	OX16,Y	GET THE MANS WORLD X
	SUBD	PLABX	COMPARE TO US
	BMI	P2501	HE OFF TO THE LEFT (HE SMALLER THAN US)
	LDD	BGL	HE TO THE RIGHT MAKE HIM GO NEGATIVE
	ADDD	#312*32	PUT HIM JUST RIGHT OF THE SCREEN
	STD	OX16,X	GOOD POSITION
	LDD	PLAXV	GET THE PLAYERS VELOCITY
	SUBD	#2*32	TWO PIXELS PER..LESS THAN PLAYER
	BRA	P2502
P2501	LDD	BGL
	STD	OX16,X
	LDD	PLAXV
	ADDD	#2*32	TWO PIXELS FASTER THAN THE PLAYER.
P2502	STD	OXV,X	STORE THE VELOCITY
	JMP	P2503	JUMP INTO THE 500 ROUTINE TO PUT THE Y
* AND OTHER STUFF IN

PPAC	LDA	MANCTR	GET THE COUNTER
	ASLA		X 4
	ASLA
	CMPA	#12	MORE THAN 3 MEN?
	BLS	PPAC1	NOPE
	LDA	#12	3 MEN +1 IS MAX BONUS
PPAC1	LDY	#PACTAB
	LEAY	A,Y	POINTER OBTAINED
	INC	MANCTR	ADD THE NEW FELLOW ON THE BANDWAGON.
	TST	OFREE	ANY OBJECTS??
	BEQ	SUCVV
	JSR	GETOB	WE NEED AN OBJECT FOR THE NUMBER
	LDD	2,Y	GET THE PICTURE FROM THE TABLE
	STD	OPICT,X
	STU	OBJID,X
	LDD	#NOKILL
	STD	OCVECT,X
	LDD	#0
	STD	OBJCOL,X NO BLIP
	LDD	,Y	GET SCORE
	BRA	P5000
*
*500 POINT PROCESS
*X=EARTHLING OBJECT
*
P500	JSR	WVCHKV	CHECK FOR PRO-CATCH-END OF WAVE??
	BNE	P5103	NOPE.
	LDY	AFALLP	CHECK FOR FALLING MEN PROCESSES
	JSR	PCHEK
	BNE	PROCAT	NONE IN THE AIR...GOLLY..IT MUST BE A PRO CATCH
P5103	LDA	OFREE	OBJECTS??
SUCVV	BEQ	SUCV
	OBI	C5P1,NOKILL,$0000
P5100	LDD	#$0150
P5000	JSR	SCORE
	LDY	PD,U
P5001	JSR	RAND	CYCLE THE GENERATOR IN CASE OF MANY AT ONCE
	LDB	SEED
	ASRB		MUCH SMALLER
	ASRB
	ASRB
	SEX		BIG NUMBER
	ADDD	PLAXV	ADD SKEW TO VELOCITY
	STD	OXV,X
	LDD	OX16,Y
	STD	OX16,X
P2503	LDB	HSEED	Y SKEW......MAKE IT UPWARD
	LDA	#$FF
	STD	OYV,X
	LDA	#$11
	STA	OTYP,X
	LDD	OY16,Y
	BMI	P501
	ADDD	#$1800
	BRA	P502
P501	SUBD	#$2000
P502	STD	OY16,X
	STX	OPTR
	STX	PD,U	OBJECT INDEX
	NAP	100,P503
P503	LDX	PD,U
	JSR	KILOFF
SUCV	JMP	SUCIDE
*
* PRO CATCH
*
PROCAT	JSR	GNCIDE	MAMA...OOOO....I DONT WANT TO DIE
	LDD	#$0220	2000 POINTS
	JSR	SCORE	AND SCORE IT
	CLR	PCRAM+5	MAKE SURE IT IS BLACK
	LDA	#$76
	STA	STATUS
	NAP	1,PROCT0
PROCT0	LDY	PD,U
	LDD	OBJX,Y	GET COORD OF MAN.
	BNE	PROCT1	OK
	LDD	#$50B0	USE DEFAULT
PROCT1	SUBB	#$15	RAISE Y
	SUBA	#$8	CENTER
	TFR	D,X	MAKE IT THE CURSOR
	JSR	PRINV	INITIALIZE STUFF
	LDA	#PROM	PRO MESSAGE
	JSR	WRD7V	AND PRINT THE MESSAGE
*
	LDD	#PROSND
	JSR	SNDLD	DO THE SOUND
*
	LDA	#$18
	STA	PD,U
	LDD	#$800	FIRST DELAY
	STD	PD+1,U
PROCT3	JSR	RAND
	ANDA	#$1F
	LDX	#COLTAB
	LDA	A,X
	STA	PCRAM
	NAP	2,PROCT4
PROCT4	CLR	PCRAM
	DEC	PD,U	DONE??
	LBEQ	ENDWVV	YEP...BACK INTO GAME EXEC.
	LDD	PD+1,U
	CMPD	#$160
	BHI	PROCT6
	LDD	#$160
PROCT6	SUBD	#$60
	STD	PD+1,U
	LDX	#PROCT3
	JMP	SLEEP


PROSND	FCB	$FF,1,$30,$D,0
*
*SHOOT AT PLAYER
*X=OBJECT FIRING
*
SHOOT	PSHS	X
	SHL	FBOUT,BMBP1,BKIL
SHT00	BEQ	SHOOTX	NONE LEFT
	LDB	SEED
	ANDB	#$1F
	ADDB	#-$10
	ADDB	PLAXC
	SUBB	OBJX,X
	SEX
	ASLB
	ROLA
	ASLB
	ROLA
	STD	OXV,X
	LDB	SEED	REL?
	CMPB	#120
	BLS	SHOOT0	NO
	LDD	PLAXV
	ASLB
	ROLA
	ASLB
	ROLA
	ADDD	OXV,X
	STD	OXV,X
SHOOT0	LDB	LSEED
	ANDB	#$1F
	ADDB	#-$10
	ADDB	PLAYC
	SUBB	OBJY,X
	SEX
	ASLB
	ROLA
	ASLB
	ROLA
	STD	OYV,X
	LDA	#1	RETURN NE
SHOOTX	PULS	X,PC
*
*LANDER SHOOT
*
LSHOT	DEC	PD6,U
	BNE	LSHTX
	LDA	LDSTIM
	JSR	RMAX
	STA	PD6,U
	BSR	SHOOT
	BEQ	LSHTX
	LDD	#LSHSND
	JSR	SNDLD
LSHTX	RTS
*
*START SCHITZOS
*A=NUMBER
*PD=X
*PD2=SHOT TIMER
*
SCZST$	PSHS	A
	STA	XTEMP
SCZS0	MKPROB	SCZ0,SCZP1,SCZKIL,$CC33
	BEQ	SCZSX	NOBODY LEFT
	JSR	RAND
	LDD	BGL
	SUBD	#300*32
	STD	XTEMP2
	LDD	HSEED
	SUBD	XTEMP2
	CMPD	#600*32
	BHS	SCZS1
	ADDD	#$8000
SCZS1	ADDD	XTEMP2	AVOID PLAYER
	STD	OX16,X
	LDA	SEED
	LSRA
	ADDA	#YMIN
	STA	OY16,X
	JSR	APVCT
	INC	SCZCNT
	DEC	XTEMP
	BNE	SCZS0
SCZSX	PULS	A,PC
*KILL	SHITZO
SCZKIL	DEC	SCZCNT
	KILP	0115,SCHSND
	RTS
*
*GET TARGET FROM LIST
*RET EQ IF NONE,D=TRG OBJECT
*U=PROCESS
GTARG	PSHS	X
	LDA	ASTCNT
	BEQ	GTX	NOBODY LEFT
	LDX	TPTR
GT1	LEAX	2,X
	CMPX	#TLEND
	BLO	GT2
	LDX	#TLIST
GT2	LDD	,X
	BNE	GT3
	CMPX	TPTR	AROUND ONCE?
	BNE	GT1
	PULS	X,PC
GT3	STX	TPTR	FOUND ONE
	STD	PD2,U
	STX	PD4,U
GTX	PULS	X,PC
*
*START LANDERS
*A=NUMBER
*
LNDST$	PSHS	A
	STA	XTEMP
LNDST0	TST	ASTCNT	NO MORE TARGETS
	BNE	LNDST1
	JMP	SCZS0
LNDST1	MKPROB	LANDS0,LNDP1,LKILL,$4433
	BEQ	LNDSTX	NO MO
	JSR	RAND
	LDD	HSEED
	STD	OX16,X
	LDA	#YMIN+2
	STA	OY16,X
	LDD	LNDYV
	STD	OYV,X
	LDA	LDSTIM	SHOOTING TIME
	JSR	RMAX
	STA	PD6,U
	LDA	LNDXV
	JSR	RMAX
	TFR	A,B
	CLRA
	BITB	#1
	BEQ	LNS2
	COMB
	COMA
LNS2	STD	OXV,X
	JSR	APVCT
	BSR	GTARG
	INC	LNDCNT
	DEC	XTEMP
	BNE	LNDST0
LNDSTX	PULS	A,PC
*
*UPDATE LANDERS OUT OF BOZO MODE
*
BOZOV$	PSHS	X,U,Y,D
	LDU	#ACTIVE
	BRA	BOZVLP
BOZOV0	LDY	PADDR,U
	CMPY	#LANDS0
	BLO	BOZVLP	NOT A LANDER
	BNE	BOZV2	NO VELOCITY
	LDX	PD,U
	LDA	LNDXV
	JSR	RMAX
	TFR	A,B
	CLRA
	TST	OXV,X
	BPL	BOZV1
	COMA
	COMB		PRESERVE SIGN
BOZV1	STD	OXV,X
BOZV2	CMPY	#LANDFX	NOT A LANDER
	BHI	BOZVLP
	LDA	LDSTIM	GET SHOT TIMER
	JSR	RMAX
	STA	PD6,U
BOZVLP	LDU	,U
	BNE	BOZOV0
	PULS	X,Y,U,D,PC
*
*LANDER KIDNAP
*PD=OBJECT,PD2=TARGET,PD4=TLIST INDEX
*PD6=SHOT TIMER,PD7=ROTATION DIVIDER
LANDS0	LDX	PD,U
	LDY	PD2,U
	LDD	[PD4,U]	TARGET ALIVE?
	BEQ	LNDSAA
	LDA	OCVECT+1,Y
	CMPA	#ASTKIL!.$FF
	BNE	LNDSAA	ORBIT
	LDA	OX16,X
	ANDA	#$FC
	STA	XTEMP
	LDA	OX16,Y
	ANDA	#$FC
	CMPA	XTEMP	CLOSE?
	BEQ	LANDG0	GET IM
	BRA	LANDSA
LNDSAA	LDA	OTYP,X	HYPERABLE
	ANDA	#$FE
	STA	OTYP,X
	JSR	GTARG	RE:TARGET
	LBEQ	SCZ00	NO, FREAK
LANDSA	JSR	GETALT
	SUBA	#50
	SUBA	OY16,X
	BHI	LANDS1
	CMPA	#-20
	BLT	LNDS1
	CLRA
	CLRB
	BRA	LANDS2
LNDS1	LDD	LNDYV
	COMA
	COMB
	BRA	LANDS2
LANDS1	LDD	LNDYV
LANDS2	STD	OYV,X	NEW Y VEL
	LDD	OPICT,X	APPEARING?
	CMPD	#NULOB
	BEQ	LANDS4
	LDA	OBJY,X
	BEQ	LANDS4	DONT DO NOTHIN OF LOST.
	JSR	LSHOT
	BSR	LPSUB	CYCLE IMAGE
	NAP	3,LNDSP1
LNDSP1	LDX	PD,U	GET PICTURE BACK
	BSR	LPSUB	DO THE PICTURE AGAIN
	NAP	3,LANDS0
*
LANDS4 NAP 6,LANDS0
*
LPSUB	LDU	OPICT,X	NEW PICT
	LEAU	OBJL,U
	CMPU	#LNDP4
	BLS	LANDS3
	LDU	#LNDP1
LANDS3	STU	OPICT,X
	RTS
*
*LETS GET HIM
*
LANDG0	CLRD
	INC	OTYP,X	DONT HYPER ME
	STD	OXV,X
	STD	OYV,X
LANDG	LDX	PD,U
	LDY	PD2,U
	LDD	[PD4,U]
	LBEQ	LNDSAA	DEAD
	LDA	OCVECT+1,Y
	CMPA	#ASTKIL!.$FF
	LBNE	LNDSAA	TAKEN
	LDD	OX16,Y
	ANDB	#$E0
	STD	XTEMP2
	LDD	OX16,X
	ANDB	#$E0
	CMPD	XTEMP2
	BEQ	LNDG2
	BLT	LNDG1
	LDB	#-$20
	BRA	LANDG1
LNDG1	LDB	#$20
LANDG1	SEX
	ADDD	OX16,X
	STD	OX16,X
LNDG2	LDA	OY16,Y
	SUBA	#12
	CMPA	OY16,X
	BEQ	LANDG3
	LDD	LNDYV
	BHS	LANDG2
	COMA
	COMB
LANDG2	ADDD	OY16,X
	STD	OY16,X
LNDG20	JSR	LSHOT
	NAP	1,LANDG
*ARE WE ON HIM?
LANDG3	LDD	OX16,X
	ADDD	#$0040
	SUBD	OX16,Y
	CMPD	#$80
	BHI	LNDG20	NOPE
	LDD	#LKIL1	CHANGE KILL VECTOR
	STD	OCVECT,X
	LDD	LNDYV
	COMB
	COMA
	STD	OYV,X	SPLIT
	STD	OYV,Y
	LDD	#LPKSND
	JSR	SNDLD
	LDD	#AKIL1
	STD	OCVECT,Y
*FLEE
LANDF	LDU	CRPROC
	LDX	PD,U
	LDA	OY16,X
	CMPA	#YMIN+8
	BLS	LANDFX	ALL DONE
	LDA	OBJY,X
	BEQ	LANDFO
	JSR	LSHOT
	JSR	LPSUB	LOOK PRETTY WHILE YOUR RIPPING OFF THAT GUY
LANDFO	NAP	3,LANDF
*PULL HIM INSIDE
LANDFX	LDD	#LSKSND
	JSR	SNDLD
LNDFXA	LDX	PD,U
	LDY	PD2,U
	LDD	[PD4,U]
	BNE	LNDFX0
	JSR	KILOFF	GIVE UP
	DEC	LNDCNT
	INC	LNDRES
	JMP	SUCIDE
LNDFX0	CLRD
	STD	OYV,X
	STD	OYV,Y
	LDA	OY16,Y
	CMPA	OY16,X
	BLS	LNDFX1
	DEC	OY16,Y
	LDA	#$12
	JSR	SNDOUT
	NAP	1,LNDFXA
LNDFX1	LEAX	,Y	KILL ASTRO
	LDD	OBJX,Y	CENTER EARTHLING BLOW
	ADDA	#1
	STD	CENTMP
	JSR	ASTK1
SCZ00	DEC	LNDCNT
	BNE	SCZ00$
	LDA	LNDRES	CHECK RESERVE
	BNE	SCZ00$	NUTS...
	LDA	STATUS
	BITA	#$2	THE MEN ARE GONE, SO WHO CARES??
	BNE	SCZ00$
	LDD	#$8FF	FAST WHITE
	STD	MESSBL
	LDX	#ALLLND	ALL LANDER MESSAGE
	JSR	MESLDV	AND SEND IT.
SCZ00$	INC	SCZCNT
	LDX	PD,U
	CLR	OTYP,X	OK TO HYPER
	LDD	#SCZP1
	STD	OPICT,X
	LDD	#$CC33
	STD	OBJCOL,X
	LDD	#SCZKIL
	STD	OCVECT,X
	LDA	SZSTIM
	STA	PD2,U
*
*I AM SCHITZO I,M FEELING FINE
*PD=OBJECT,PD2=SHOT TIME
*
SCZ0	LDX	PD,U
	LDB	SZXV
	LDY	PLABX
	CMPY	OX16,X
	BGE	SCZ1
	NEGB
SCZ1	SEX
	STD	OXV,X	SEEK X
	LDD	PLABX	X CLOSE?
	SUBD	OX16,X
	ADDD	#380
	CMPD	#$700
	BLS	SCZ6	SEEK Y
	LDA	PLAYC	AVOID Y
	SUBA	OY16,X
	BLS	SCZ2
	CMPA	#8
	BHI	SCZ3
	LDD	SZYV
	COMA
	COMB
	BRA	SCZ5
SCZ2	CMPA	#-8
	BGT	SCZ4
SCZ3	CLRD
	BRA	SCZ5
SCZ4	LDD	SZYV
SCZ5	STD	OYV,X
	BRA	SCZ10
SCZ6	LDA	PLAYC	SEEK Y
	CMPA	OY16,X
	LDD	SZYV
	BHS	SCZ7
	COMA
	COMB
SCZ7	STD	OYV,X
	LDD	OBJX,X
	BEQ	SCZ12	NOT ON SCREEN
*RANDOM	Y HOP
SCZ10	LDB	SZRY
	LDA	SEED
	BMI	SCZ11
	NEGB
SCZ11	ADDB	OY16,X
	CMPB	#YMIN
	BHS	SCZ110
	LDB	#YMAX
SCZ110	STB	OY16,X
	DEC	PD2,U	SHOT TIME
	BNE	SCZ12
	LDA	SZSTIM
	JSR	RMAX
	STA	PD2,U
	JSR	SHOOT
	BEQ	SCZ12
	LDD	#SSHSND
	JSR	SNDLD
SCZ12	NAP	3,SCZ0
*
*KILL KIDNAPPING LANDER
*
LKIL1	LDU	OBJID,X	GOT A PASSENGER?
	LDD	[PD4,U]
	BEQ	LKILL
LKIL2	LDU	OBJID,X
	PSHS	X
	MAKP	AFALL	START HIM FALLING
	LDU	PD2,U
	STU	PD,X
	LDD	#ASCSND
	JSR	SNDLD	SCREAM.....
	LDD	#0
	STD	OYV,U
	STX	OBJID,U
	PULS	X
*KILL LANDR
LKILL	DEC	LNDCNT
	BNE	LKILL$
	LDA	LNDRES	CHECK RESERVES
	BNE	LKILL$	NUTS
	LDA	STATUS
	BITA	#$2
	BNE	LKILL$	MEN ALL DEAD..SO NO NEED.
	LDD	#$8FF	FAST WHITE
	STD	MESSBL
	PSHS	X
	LDX	#ALLLND	ALL LANDER MESSAGE
	JSR	MESLDV	AND SEND IT.
	PULS	X
LKILL$	KILP	0115,LHSND
	RTS
*
ALLLND	FCB	$20,CLEAR!+BLINK,$80,LNDKLM
*
*ASTRONAUT FALL
*
AFALL	LDX	PD,U
	LDD	#8	ACCEL DOWNWARD
	ADDD	OYV,X
	CMPD	#$300
	BHS	AFALL0
	STD	OYV,X
AFALL0	JSR	GETALT
	CMPA	OY16,X	HIT GROUND?
	BHI	AFALL1	NO
	LDD	OYV,X
	CMPD	#$E0	FATAL?
	BLS	ALAND	NO
	LDD	OBJX,X
	ADDD	#$0107
	STD	CENTMP	CENTER OF EXPLOSION
	JSR	ASTK1
	JMP	SUCIDE
AFALL1	NAP	4,AFALL
AFALL2	LDX	PD,U
	LDD	#0
	STD	OYV,X
	LDA	PLAY16
	ADDA	#10
	STA	OY16,X
	LDD	PLABX
	ADDD	#$0080
	STD	OX16,X
	JSR	GETALT
	CMPA	OY16,X
	BLO	ALAND0	WERE ON THE GROUND
	NAP	1,AFALL2
ALAND	PSHS	X
	LDX	#P250
	BRA	ALND00
ALAND0	PSHS	X
	DEC	MANCTR	ONE SMALL STEP FOR MAN...
	LDX	#P500
ALND00	LDA	#STYPE
	JSR	MKPROC
	LEAY	,X
	PULS	X
	STX	PD,Y	ASTRO OBJECT TO 500 PROC
	LDD	#0
	STD	OBJID,X
	STD	OYV,X
	LDD	#ASTKIL
	STD	OCVECT,X
	LDD	#ALSND
	JSR	SNDLD
	JMP	SUCIDE
* SEE IF ANY MEN ON PLANET
* BNE FOR NO MEN ON SURFACE

LEDTR$	LDX	#TLIST
LEDTG0	LDY	,X++
	BEQ	LEDTG1
	LDD	OCVECT,Y GET THE MANS COLLISION VECTOR
	CMPD	#ASTKIL
	BEQ	LEDTG2	EXIT.....MAN ON GROUND
LEDTG1	CMPX	#TLIST+20
	BNE	LEDTG0
	LDA	#1
LEDTG2	RTS

LNDP1	FCB	5,8
	FDB	LND10,LND11
	FDB	ON58,OFF58

LNDP2	FCB	5,8
	FDB	LND20,LND21
	FDB	ON58,OFF58

LNDP3	FCB	5,8
	FDB	LND30,LND31
	FDB	ON58,OFF58

LNDP4	FCB	5,8
	FDB	LND40,LND41
	FDB	ON58,OFF58

LND10	FCB	$00,$03,$30,$30,$23,$00,$03,$30
	FCB	$23,$44,$03,$03,$33,$30,$00,$00
	FCB	$33,$44,$30,$30,$23,$30,$30,$30
	FCB	$20,$43,$03,$03,$33,$30,$03,$00
	FCB	$00,$00,$30,$30,$20,$00,$00,$30

LND11	FCB	$00,$00,$03,$03,$02,$00,$00,$03
	FCB	$02,$34,$00,$00,$33,$03,$30,$00
	FCB	$33,$44,$33,$33,$32,$03,$03,$03
	FCB	$32,$44,$00,$00,$33,$03,$00,$00
	FCB	$00,$30,$33,$33,$32,$00,$30,$03

LND20	FCB	$00,$03,$30,$20,$33,$00,$03,$30
	FCB	$32,$44,$33,$33,$32,$30,$00,$00
	FCB	$33,$44,$00,$00,$33,$30,$30,$30
	FCB	$30,$42,$33,$33,$32,$30,$03,$00
	FCB	$00,$00,$30,$30,$30,$00,$00,$30

LND21	FCB	$00,$00,$03,$02,$03,$00,$00,$03
	FCB	$03,$34,$03,$03,$33,$03,$30,$00
	FCB	$23,$44,$30,$30,$23,$03,$03,$03
	FCB	$33,$44,$03,$03,$33,$03,$00,$00
	FCB	$00,$20,$33,$33,$23,$00,$30,$03

LND30	FCB	$00,$03,$23,$33,$33,$00,$03,$30
	FCB	$33,$33,$30,$30,$23,$30,$00,$00
	FCB	$23,$33,$03,$03,$33,$30,$30,$30
	FCB	$30,$33,$30,$30,$23,$30,$03,$00
	FCB	$00,$00,$20,$30,$30,$00,$00,$30

LND31	FCB	$00,$00,$02,$03,$03,$00,$00,$03
	FCB	$03,$33,$33,$33,$32,$03,$30,$00
	FCB	$32,$33,$00,$00,$33,$03,$03,$03
	FCB	$33,$33,$33,$33,$32,$03,$00,$00
	FCB	$00,$30,$02,$03,$33,$00,$30,$03

LND40	FCB	$00,$02,$33,$33,$32,$00,$03,$30
	FCB	$33,$33,$00,$00,$33,$30,$00,$00
	FCB	$32,$33,$33,$33,$32,$30,$30,$30
	FCB	$30,$33,$00,$00,$33,$30,$03,$00
	FCB	$00,$00,$30,$20,$30,$00,$00,$30

LND41	FCB	$00,$00,$03,$03,$03,$00,$00,$03
	FCB	$03,$23,$30,$30,$23,$03,$30,$00
	FCB	$33,$33,$03,$03,$33,$03,$03,$03
	FCB	$23,$33,$30,$30,$23,$03,$00,$00
	FCB	$00,$30,$03,$02,$33,$00,$30,$03
*
*PHONY LANDER START
*
PHST	LDD	#$04C0
	LDX	#$A000	X ADDRESS
	PSHS	D,X
PHSL	MKPROB	PHLND,LNDP1,LKIL2,$4433
	TFR	X,Y
	STX	OPTR
	OBI	ASTP1,AKIL1,$6666
	STX	PD2,U
	STX	OPTR
	CLRA
	CLRB
	STD	OYV,X
	STD	OXV,X
	STD	OXV,Y
	LDD	2,S
	STD	OX16,X	X COORD
	STD	OX16,Y
	ADDD	#$1800
	STD	2,S	SPACING
	LDD	#$E000
	STD	OY16,X
	LDA	1,S
	STD	OY16,Y
	SUBA	#$28
	STA	1,S
	LDD	#$200
	STD	OYV,Y
	STD	PD4,U	FOR LANDER KILLER FOOLING
	LDA	#1
	STA	OTYP,X
	STA	OTYP,Y	DONT HYPER PLEASE
	DEC	,S
	BNE	PHSL
	LDA	#10
	STA	LNDCNT
	PULS	D,X,PC
*
*PHONY LANDER ROUTINE
*PD=LANDER,PD2=GUY
PHLND	LDX	PD,U	REV WHEN ON HIM
	LDA	OY16,X
	CMPA	#$D0
	BHS	PHL1
	NAP	1,PHLND
PHL1	LDX	PD,U
	LDD	#$D400
	STD	OY16,X
	LDD	#$FF80
	STD	OYV,X
	LDX	PD2,U
	STD	OYV,X
PHL2	NAP	60,PHL2	R.I.P.

	END
