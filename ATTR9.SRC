	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E >>>>>
	ORG	ATRORG
* ATTRACT
*PICKUP MODE
*
PICKUP	LDA	#$FF
	STA	STATUS
	LDA	#$30
	STA	ATRFLG
	CLR	.P1LAS	CLEAR SHIP COUNTS
	CLR	.P2LAS
	CLR	.P1SBC
	CLR	.P2SBC
	CLR	.P1INV
	CLR	.P2INV
	JSR	GNCDAV
	JSR	SCRCLR
	JSR	CRINIT
	LDA	#1
	LDU	CRPROC
	JSR	DIMV
	LDX	#0
	JSR	SCINIT
	LDA	#DEFMES
	JSR	WRD5V
	LDA	#1
	JSR	SGSTV
	LDD	#$3000	ADJUST COORDS
	STD	OX16,X
	LDA	#$80
	STA	OY16,X
	LDD	#KILLNO
	STD	OCVECT,X
	CLR	ATRSW2
	CLR	ATRSW3
	JSR	PHSTV	PHONY LANDER START
	LDA	#$80
	STA	STATUS
	LDA	#2
	LDU	CRPROC
	JSR	BRTV
	JSR	ONCOLV
	LDD	#$0200	NOW THRUST INTO GATE
	STD	ATRSW2
	NAP	$60,PH1	WAIT FOR COLLISION
PH1	LDA	#$FF
	STA	STATUS
	LDB	#28
	JSR	NITRV
	LDA	#2
	LDU	CRPROC
	JSR	DIMV
	JSR	SCLR1	CLEAR IT
	JSR	PLINV	REINIT PLAYER
	LDX	#$8000
	STX	BGL
	STX	BGLX
	JSR	BGINIT
	CLR	ATRSW2
	LDA	#$80
	STA	STATUS
	LDU	CRPROC
	JSR	SCNR
	LDA	#2
	LDU	CRPROC
	JSR	BRTV	ON AGAIN
	CLR	MANCTR
	LDA	#$14
	STA	LNDCNT
	LDX	#PHSTAB	GET COMMANDS
	STX	PD,U
PHS1	LDX	PD,U
PHS1A	LDD	,X++
	BEQ	PH8	ALL OVER
	CMPA	#$FF	CHECKPOINT
	BNE	PHS1B
	LDA	LNDCNT
	LDB	MANCTR
	LEAX	1,X
	CMPD	-2,X
	BNE	PH8	ERROR
	BRA	PHS1A
PHS1B	STX	PD,U
	PSHS	D
	BITB	#$20
	BEQ	PHS2
	NEWP	LFIRE,LTYPE
	LDD	0,S
PHS2	BITB	#$10
	BEQ	PHS3
	MAKP	REV
	LDD	0,S
PHS3	ANDB	#$1
	STB	ATRSW3
	PULS	D
	ANDB	#$82
	STB	ATRSW2
	LDX	#PHS1
	JMP	SLEEP
PH8	BRA	FIGHT
*
*PHONY CATCH COMMANDS
*B7=DOWN,B5=LASER,B4=REV,B1=THRUST,B0=UP
*FF=CKPNT--LNDCNT,MANCTR
PHSTAB	FCB	$30,$02
	FCB	$0C,$A2
	FCB	$FF,$13,$00
	FCB	$05,$82
	FCB	$18,$00
	FCB	$FF,$13,$01
	FCB	$20,$01
	FCB	$0C,$A2
	FCB	$FF,$12,$01
	FCB	$18,$02
	FCB	$FF,$12,$02
	FCB	$1A,$01
	FCB	$0D,$A2
	FCB	$FF,$11,$02
	FCB	$1C,$02
	FCB	$FF,$11,$03
	FCB	$1A,$01
	FCB	$07,$22
	FCB	$FF,$10,$03
	FCB	$19,$02
	FCB	$2C,$92
	FCB	$28,$02
	FCB	$1C,$12
	FCB	$FF,$10,$04
	FCB	$09,$01
	FCB	$98,$02
	FCB	$00,$00
	RMB	20
*
*FIGHTING MODE
*PD,U=TABLE PTR
*PD2,U=TIMER
FIGHT	LDX	#FITAB
	STX	PD,U
	LDA	#$FF
	STA	STATUS
	JSR	GNCDAV	KILL EVERYBODY
	LDA	#3
	JSR	DIMV	COOL THE LITES
FITST	LDY	PD,U
	LDX	,Y++	GET BGL
	CMPX	#$FFFF	OVER??
	LBEQ	ENEMY	ONTO NEXT THING
	PSHS	U,Y
	JSR	SCINIT	ONB SETUP
	CLR	ATRSW2
	CLR	ATRSW3
	LDX	#ECNTS	CLEAR ENEMY COUNTS
FITSS1	CLR	,X+
	CMPX	#ECEND
	BNE	FITSS1
	LDA	#1
	STA	CURPLR
	JSR	PLINDX	GET PLAYER X
	STX	PLRX
	LDA	#2	WAVE 2 PARAMS
	STA	PWAV,X
	JSR	GETW0V
	JSR	PTRANV
	JSR	RAND	RANDOMY
	ANDA	#$3F
	ADDA	#-$20
	ADDA	PLAY16
	STA	PLAY16
	STA	PLAYC
	STA	NPLAYC
	LDA	LSEED	RANDOM X
	ANDA	#$3F
	ADDA	#-$20
	ADDA	#$50
	STA	PLAXC
	STA	NPLAXC
	STA	PLAX16
	LDA	HSEED
	BMI	FITST0
	LDD	NPLAD
	COMA
	COMB
	ADDD	#1
	STD	NPLAD
	STD	PLADIR
FITST0	LDA	#$80
	STA	STATUS
	PULS	U,Y
	LDA	,Y+	GET ENEMY #
	PSHS	U,Y
	JSR	[,Y]	GO START EM UP
	PULS	U,Y
	LEAY	2,Y
	LDA	,Y+	OUTPUT MESSAGE #
	LDX	,Y++	MESSAGE ADDR
	STY	PD,U
	JSR	WRD5V
	LDA	#3
	JSR	BRTV	ON LITES
	JSR	ONCOLV
	LDU	CRPROC
	LDD	#$300
	STD	PD2,U
	LDA	#1
	STA	PD4,U	UP/DOWN TIMER
	STA	PD5,U	ACCEL TIMER
	LDA	SEED
	LSRA
	STA	PD6,U	REV TIMER
FITLP	LDX	PD2,U	FIGHT TIME-OUT CHECK
	LDA	#16
	STA	STRCNT	KEEP THE STARS GOING
	LEAX	-1,X
	STX	PD2,U
	LBEQ	FDTIM
	LDA	STATUS	CHECK DEATH
	CMPA	#$DA
	LBEQ	FDIE	WE HAVE DIED
	LDA	PLAY16	Y OUT OF RANGE??
	CMPA	#$48
	BLS	FIT1A
	CMPA	#$C0
	BHS	FIT1AA
	DEC	PD4,U	UP/DOWN TIMER
	BEQ	FIT1B
	JSR	RAND	MAKE A MOVE??
	CMPA	#$FC
	BLO	FIT2	NO
	JSR	RAND
	LSRA
	BCS	FIT1A
FIT1AA	LDD	#$0001	UP
	BRA	FIT1X
FIT1A	LDD	#$8000	DOWN
	BRA	FIT1X
FIT1B	LDD	#$0000	LEVEL
FIT1X	STB	ATRSW3
	LDB	ATRSW2
	ANDB	#$2
	STB	ATRSW2
	ORA	ATRSW2
	STA	ATRSW2
	JSR	RAND
	LSRA
	LSRA
	NOP
	STA	PD4,U
FIT2	DEC	PD5,U	ACCEL CHANGE??
	BNE	FIT3	NO
	JSR	RAND
	LDB	ATRSW2
	CMPA	#$80
	BHI	FIT2A
	ORB	#2
	BRA	FIT2X
FIT2A	ANDB	#$FD
FIT2X	STB	ATRSW2
	LDA	SEED
	LSRA
	LSRA
	STA	PD5,U
FIT3	JSR	RAND	LASER ???
	CMPA	#$0C
	BHI	FIT4
	NEWP	LFIRE,LTYPE
FIT4	DEC	PD6,U	REV???
	BNE	FITX
	MAKP	REV
	LDA	SEED
	ORA	#$10
	STA	PD6,U
FITX	NAP	1,FITLP
*TIME OUT
FDTIM	JSR	GNCDAV
	LDA	#$8	PLAYER IS DEAD, NO COLLISIONS WITH PROCESSES DEAD
	ORA	STATUS
	STA	STATUS
	LDA	#2
	JSR	DIMV
	LDA	#$FF
	STA	STATUS
	JMP	FITST
*DEAD
FDIE	MAKP	DIEDIE
	NAP	$6C,FDTIM
*START OVER AGAIN
FIGHTX	LDX	#FITAB
	STX	PD,U
	JMP	FITST
*DYING PROCESS
DIEDIE	LDX	NPLAXC
	JSR	PXVCT	BLOW HIM
	JMP	SUCIDE
*
*FIGHT TABLE
*BGL,ENEMY#,ENEMY START ROUTINE,MESS#,MESS ADDR
*
*FIREBOMBERS
FITAB	FDB	$B000	BGL
	FCB	8	#
	FDB	FBSTRT	START ROUTINE
	FCB	FBAM	MESS #
	FDB	$3227	MESS UPPER LEFT
*YLLABIANS
	FDB	$8000
	FCB	20
	FDB	YLST
	FCB	YLAM
	FDB	$3627
*DYNAMOS
	FDB	$8000
	FCB	10
	FDB	DYNSTV
	FCB	DYNAM
	FDB	$3327
*MUNCHIES
	FDB	0
	FCB	0
	FDB	MCHST
	FCB	MCHAM
	FDB	$2D27
*END
	FDB	$FFFF
*
*ON SCANNER,TERRAIN,SCORES,STARS
*X=BGL
SCINIT	PSHS	X,U,D,Y
	LDA	#$FF
	STA	STATUS
	JSR	SCRCLR
	LDA	#$66
	STA	SCNCOL
	JSR	TDISP
	LDX	#MESSUL
	LDD	#$4005
	JSR	BLKCLR	CLEAR OUT MESSAGE AREA
	JSR	OINIT
	LDD	2,S	GET X
	STD	BGL
	STD	BGLX
	JSR	BGINIT
	JSR	STINIT
	JSR	FISS
	JSR	THINIT
	JSR	PLINV	INIT PLAYER STUFF
	JSR	SCLEAN	CLEAN UP SAM
	MAKP	SCPROC
	PULS	X,U,D,Y,PC
*
*START MUNCHIES
*
MCHST	LDA	#$18
	STA	BMSTIM	MUNCHY MULTIPLICATION
	MAKP	MCHS
	RTS
MCHS	JSR	BMSTV
	NAP	50,MCHS
*
*ENEMIES PAGE
*
ENEMY	JSR	SCRCLR
	JSR	CRINIT
	JSR	SCLEAN
	JSR	OINIT
	JSR	ONCOLV
	CLRA
	CLRB
	STD	BGL
	STD	BGLX
	LDA	#$DB
	STA	STATUS
	LDU	CRPROC
	LDX	#EMTAB	ENEMIES
	STX	PD,U
ENEMY1	LDY	PD,U
	LDD	,Y
	BEQ	ENEMY2
	JSR	GETEM
	STY	PD,U
	NAP	8,ENEMY1
ENEMY2	NAP	01,ENEM2A
ENEM2A	LDX	#EMESTB	MESSAGES
	STX	PD,U
ENEM3A	LDY	PD,U
	LDA	,Y+
	BEQ	ENEM4
	LDX	,Y++
	STY	PD,U
	JSR	WRD5FV
	NAP	4,ENEM3A
ENEM4	LDA	#30
	STA	PD2,U
ENM4AA	CLR	PD3,U	FLAVOR
	LDX	#$2F76	REFRESH CURSOR
	STX	PD4,U
	LDX	#SGRTAB
	STX	PD6,U
ENEM4A	LDX	#EMESTB	REFRESH MESSAGES
	STX	PD,U
	DEC	PD2,U
	LBEQ	HALVCT	OVER...
ENEM4B	LDY	PD,U
	LDA	,Y+
	CMPA	#SGAM
	BEQ	ENEM4A
	LDX	,Y++
	STY	PD,U
	JSR	WRD5FV
	NAP	$4,ENEM4C
ENEM4C	LDA	PD3,U
	STA	TXFLAV
	LDX	PD4,U
	LDY	PD6,U
	CMPY	#SGRTE
	BEQ	ENM4AA
	LDA	,Y+
	LDB	#$AA
	STB	TEXCOL
	JSR	PR57V
	LDA	TXFLAV
	STA	PD3,U
	STX	PD4,U
	STY	PD6,U
	NAP	2,ENEM4B
*REFRESH TABLE
SGRTAB	FCC	'STARGATE   ENEMIES'
SGRTE	EQU	*
*
*GET ENEMY SUBROUTINE
*Y=TABLE: OPICT(2),#ANIMS(1),DELAY(1),SCRN ADDR(2)
*
GETEM	PSHS	X,U
	MAKP	ANA
	TFR	X,U
	JSR	GETOB
	LDD	[,Y++]
	STD	OPICT,X
	STD	PD2,U
	STX	PD,U
	LDD	,Y++
	STD	PD4,U
	CLR	PD6,U
	CLRA
	CLRB
	STD	OXV,X
	STD	OYV,X
	LDD	,Y++
	STB	OY16,X
	CLR	OY16+1,X
	CLRB
	LSRA
	RORB
	LSRA
	RORB
	STD	OX16,X
	LDA	#1
	STA	OTYP,X	NON HYPER
	JSR	APVCT
	PULS	X,U,PC
*
*ANIMATION ROUTINE
*PD=OBJ,PD2=PICT,PD4=ANI#,PD5=TIME/FRAME,PD6=CURRENT#
*
ANA	LDA	PD4,U
	CMPA	#1
	LBEQ	SUCIDE	DIE NO ANIMATION
ANA0	LDX	PD,U
	LDD	OPICT,X
	CMPD	#NULOB
	BEQ	ANASLP
	LDA	PD6,U
	INCA
	CMPA	PD4,U
	BLO	ANA1
	CLRA
ANA1	STA	PD6,U
	LDB	#OBJL
	MUL
	LDY	PD2,U
	LEAY	D,Y
	STY	OPICT,X
ANASLP	LDA	PD5,U
	LDX	#ANA0
	JMP	SLEEP
*
*ENEMIES
*OPICT(2),ANIM#(1),RATE(1),ADDR(2)
*
EMTAB	FDB	PRBP1V
	FCB	1,0
	FDB	$6696
*
	FDB	YLPV
	FCB	5,8
	FDB	$1D4A
*
	FDB	LNDPV
	FCB	4,3
	FDB	$6848
*
	FDB	TIEP1V
	FCB	4,8
	FDB	$1372
*
	FDB	PHRDPV
	FCB	3,8
	FDB	$47BA
*
	FDB	SCZP1V
	FCB	1,0
	FDB	$7D70
*
	FDB	FBMBPV
	FCB	4,1
	FDB	$3C3C
*
	FDB	DYNPV
	FCB	4,2
	FDB	$1294
*
	FDB	BAITPV
	FCB	7,2
	FDB	$7E4A
*
	FDB	FBALPV
	FCB	8,2
	FDB	$563C
*
	FDB	HUMPV
	FCB	2,4
	FDB	$2C98
*
	FDB	SWP1V
	FCB	1,0
	FDB	$7C9A
*
	FDB	BREDPV
	FCB	3,8
	FDB	$36BA
*
	FDB	MCHGPV
	FCB	3,8
	FDB	$5DBC
*
	FDB	0
SWP1V	FDB	SWPIC1
SCZP1V	FDB	SCZP1
PRBP1V	FDB	PRBP1
TIEP1V	FDB	TIEP1
*
*ENEMY MESSAGES
*MESS#(1),SCREEN ADDR(2)
*
EMESTB	FCB	YLAM
	FDB	$0C58
	FCB	LNDAM
	FDB	$6058
	FCB	BMBAM
	FDB	$0C7E
	FCB	MCHAM
	FDB	$2DC8
	FCB	MUTAM
	FDB	$767E
	FCB	FBAM
	FDB	$3030
	FCB	PODAM
	FDB	$60A2
	FCB	DYNAM
	FDB	$0CA2
	FCB	BTAM
	FDB	$7858
	FCB	SGAM
	FDB	$2F78
	FCB	0

	END
