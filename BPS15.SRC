	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E  >>>>>
	ORG	BPSORG
	JMP	UFOST$
	JMP	PRBST$
	JMP	MMSW$
	JMP	RIPST
	JMP	MPROB$
	JMP	BMST
	FDB	UFNP1
	FDB	BMRP0
	FDB	BMSP0
	FDB	MCHYP0
	FDB	MCHRP0
	FDB	MCHGP0
*
*UFO PROCESS START
*PD=OBJECT,PD2=SHOT TIMER,PD5=18 FRAME (3 X 6) VELOCITY DIVIDER
*PD4 COUNTS NUMBER OF ITERS LEFT IN IMAGE LOOP
UFOST$	LDA	SEED	START BAITER MASTER????
	CMPA	#$C0
	LBLS	BMST
	MKPROB	UFOLP,UFNP1,UFOKIL,$3333
	BEQ	UFOSTX
	INC	UFOCNT
	LDA	#3	INITIALIZE VELOCITY DIVIDER
	STA	PD5,U
	LDD	SEED
	ANDA	#$1F
	ADDD	BGL
	STD	OX16,X
	LSRB
	ADDB	#YMIN
	STB	OY16,X
	LDA	#8
	STA	PD2,U	INIT SHOT TIMER
	BSR	UFONV0
	JMP	APVCT
UFOSTX	RTS
*UFO PROCESS
UFOLP	LDX	PD,U
	LDD	OPICT,X
	CMPD	#NULOB
	BEQ	UFO4
	DEC	PD2,U
	BNE	UFO1
	LDA	UFSTIM
	JSR	RMAX
	STA	PD2,U
	JSR	SHOOTV
	BEQ	UFO1
	LDD	#USHSND
	JSR	SNDLD
UFO1	DEC	PD5,U	CALL VELOCITY SUBROUTINE ONLY ON 1 OF 3 TRIPS (18 FR)
	BNE	UFO2	NOT THIS TIME
	LDA	#3	RESET DIVIDER
	STA	PD5,U
	BSR	UFONV	AND DO THE VELOCITY NUMBER
UFO2	LDD	OBJX,X	SEE IF ON SCREEN
	BEQ	UFO4	THEN TAKE A VERY LONG SNOOZE
	LDA	#6
	STA	PD4,U
UFO5	NAP	1,UFO3	SHORT NAP TILL NEXT IMAGE UPDATE
UFO3	LDX	PD,U	GET IMAGE	
	BSR	UFOPIC	AND DO THE IMAGE NUMBER
	DEC	PD4,U
	BNE	UFO5	1 MORE ITERATION
	BRA	UFOLP
UFO4	NAP	6,UFOLP
*
UFOPIC	LDY	OPICT,X	GET CURRENT IMAGE
	LEAY	OBJL,Y	PUSH TO NEXT
	CMPY	#UFNP7	PAST LAST??
	BLS	UFOPL1	NOPE
	LDY	#UFNP1	THEN PUT FIRST IN
UFOPL1	STY	OPICT,X	AND SET IN PLACE
	RTS		KAY.BYE
*
*UFO VELOCITY
*
UFONV	LDA	SEED
	CMPA	UFOSK
	BLS	UFONVX
UFONV0	LDD	#$4001
	STD	XTEMP
	LDD	OX16,X
	SUBD	PLABX
	BMI	UFONV1
	NEG	XTEMP
UFONV1	ADDD	#20*32	X CLOSE?
	CMPD	#40*32
	BLS	UFONV3	YES, DONT SEEK
	LDB	XTEMP
	SEX
	ADDD	PLAXV
	STD	OXV,X
UFONV3	LDA	OY16,X
	SUBA	PLAYC
	BMI	UFONV4
	NEG	XTEMP+1
UFONV4	ADDA	#10
	CMPA	#20	Y CLOSE?
	BLS	UFONVX	YES, QUIT
	CLRB
	LDA	XTEMP+1
	ADDD	PLAYV
	ASRA	DIVIDE	BY 2
	RORB
	STD	OYV,X
UFONVX	RTS
*UFOKILL
UFOKIL	DEC	UFOCNT
	KILP	0120,UFHSND
	RTS
*
*MAKE A PROCESS+OBJECT
*RET=PROCESS,RET+2=OBJECT PICT,RET+4=COLL VECTOR,RET+6=BLIP
*U RETURNED WITH PROCESS, X=OBJECT
*X-PD,U;0-OXV,X OYV,X
*RET EQ IF NOTHING LEFT
MPROB$	PSHS	D,Y
	LDX	OFREE
	BEQ	MPRER	NONE LEFT
	LDX	FREE
	BEQ	MPRER
	CLRA
	LDU	4,S
	PULU	X
	JSR	MKPROC
	LEAY	,X
	JSR	GETOB
	PULU	D
	STD	OPICT,X
	PULU	D
	STD	OCVECT,X
	PULU	D
	STD	OBJCOL,X
	STU	4,S	NEW RET ADDR
	LEAU	,Y
	STU	OBJID,X
	STX	PD,U
	CLRA
	CLRB
	STD	OYV,X
	STD	OXV,X
	LDA	#1
	PULS	D,Y,PC
MPRER	LDU	4,S	CLEAN UP STACK
	LEAU	8,U
	STU	4,S
	CLRA		RETURN EQ, NONE LEFT
	PULS	D,Y,PC
*
*START BAITER MASTER
*PD=OBJ,PD2=IMAGE#,PD4=BASE IMAGE TABLE,PD6=SHOT TIMER
*
BMST	MKPROB	BM,BMRP0,BMKIL,$3333
	BEQ	BMSTX
	INC	BMCNT
	CLR	PD2,U
	LDD	SEED
	ANDA	#$1F
	ADDD	BGL
	STD	OX16,X	INIT X
	LSRB
	ADDB	#YMIN
	STB	OY16,X
	LDA	BMSTIM
	JSR	RMAX
	STA	PD6,U
	LDY	#BMPT1
	LDA	SEED
	ANDA	#3	CHOOSE BM TYPE
	BNE	BMST1	*******************************BSO BONER
	FCB	$31,$A9,$00,$0C ((LEAY	BMPT2-BMPT1,Y))*********
BMST1	STY	PD4,U	BASE IMAGE TABLE***************BSO BONER
	BSR	BMNV0
	JSR	MCHPIC	GET IMAGE
	JMP	APVCT
BMSTX	RTS
*
*BAITER MASTER PROCESS
*PD=OBJ,PD2=IMAGE#,PD4=BASE IMAGE TABLE,PD6=SHOT TIMER
*
BM	LDX	PD,U
	LDD	OPICT,X
	CMPD	#NULOB	APPEARING?
	BEQ	BMX
	LDA	#-1
	LDB	PLAYC
	SUBB	OY16,X
	ADDB	#20
	CMPB	#10
	BHS	BM1
	LDA	#-4	4X FASTER WHEN CLOSE IN Y
BM1	ADDA	PD6,U
	CMPA	#$FC
	BLO	BM2
	JSR	BMSHT
	LDA	BMSTIM
	LDY	PD4,U
	CMPY	#BMPT2
	BNE	BM1A
	LSRA	MORE	SHOTS FOR RED GUYS
BM1A	JSR	RMAX
BM2	STA	PD6,U
	BSR	BMNV	GET VEL+PICT
	JSR	MCHPIC
BMX	NAP	6,BM
*
*GET BM NEW VELOCITY
*
BMNV	LDA	SEED	SMART OR DUMB
	CMPA	BMSK
	BLS	BMNV5
BMNV0	LDD	#$2001
	STD	XTEMP
	LDD	OX16,X
	SUBD	PLABX
	BMI	BMNV1
	NEG	XTEMP
BMNV1	ADDD	#20*32	TOO CLOSE
	CMPD	#40*32
	BLS	BMNV2	YIP
	LDB	XTEMP
	SEX
	ADDD	PLAXV
	STD	OXV,X
BMNV2	LDA	OY16,X
	SUBA	PLAYC
	SUBA	#4
	BMI	BMNV3
	NEG	XTEMP+1
BMNV3	ADDA	#10
	CMPA	#20
	BLS	BMNV5	TOO CLOSE
	LDA	XTEMP+1
	ADDD	PLAYV
	ASRA
	RORB
	STD	OYV,X
BMNV5	RTS
*
*BAITER MASTER SHOOTING
*
BMSHT	PSHS	X,U,D,Y
	LDA	OX16,X
	SUBA	BGL
	CMPA	#$26
	BHI	BMSHTX	NO
	LDA	MCHCNT
	CMPA	#12
	BHS	BMSHTX
	LEAY	,X
	MKPROB	MCH,MCHP1,MCHKIL,$3300	LAUNCH A BUTTFACE
	BEQ	BMSHTX	NOBODY HOME
	INC	MCHCNT
	LDD	#BMSSND
	JSR	SNDLD
	CLR	PD2,U
	LDA	SEED
	ANDA	#3
	BEQ	BMSHT1
	DECA
BMSHT1	LDB	#MCHPT2-MCHPT1
	MUL
	ADDD	#MCHPT1
	STD	PD4,U	BASE PICT TABLE
	LDB	#$40	INIT XV
	JSR	RMAX
	LDA	OX16,Y
	CMPA	PLABX
	BMI	BMSHT2
	NEGB
BMSHT2	SEX
	ADDD	PLAXV
	STD	OXV,X
	LDA	MCHSLP
	LSRA
	JSR	RMAX
	STA	PD3,U
	JSR	RAND
	LDB	HSEED
	SEX
	STD	OYV,X
	LDD	OX16,Y
	STD	OX16,X
	LDD	OY16,Y
	STD	OY16,X
	JSR	MCHPIC	PICTURE
	STX	OPTR
BMSHTX	PULS	X,U,D,Y,PC
*
*KILL A MUNCHY-POOH
*
MCHKIL	DEC	MCHCNT
	KILP	0050,MCKSND
	RTS
*
*KILL BAITER MASTER
*
BMKIL	DEC	BMCNT
BMKX	KILP	0120,BMKSND
	RTS
BMSSND	FCB	$D0,$01,$10,$18,0 BM SHOOT SND
BMKSND	FCB	$D0,$01,$0C,$01,$1,$08,$17,0 BM KILL
MCKSND	FCB	$D0,$01,$0C,$1A,$1,$08,$17,0 MUNCHY KILL
*
*MUNCHIES
*PD=OBJECT,PD2=IMAGE#,PD3=SLEEP TIME,PD4=BASE IMAGE TABLE
*
MCH	LDX	PD,U	GET OBJ
	DEC	PD3,U
	BNE	MCH10
	LDB	MCHXV	CALC VEL LIMITS
	CLRA
	PSHS	D
	NEGB
	SEX
	PSHS	D
	LDA	MCHYV
	CLRB
	PSHS	D
	NEGA
	PSHS	D
*VEL LIMITS 0,S =-YV; 2,S =YV; 4,S =-XV; 6,S =XV
	LDD	HSEED
	ANDA	#1
	ADDD	PLABX
	SUBD	OX16,X
	STD	XTEMP	DELTA X
	LDD	SEED
	ANDA	#7
	ADDD	PLAY16
	SUBD	OY16,X
	STD	XTEMP2
MCHVLP	ASR	XTEMP
	ROR	XTEMP+1
	ASR	XTEMP2
	ROR	XTEMP2+1
	LDD	XTEMP
	BPL	MCHV1
	CMPD	4,S
	BLT	MCHVLP
	BRA	MCHV2
MCHV1	CMPD	6,S
	BHI	MCHVLP
MCHV2	LDD	XTEMP2
	BPL	MCHV3
	CMPD	,S
	BLT	MCHVLP
	BRA	MCHVX
MCHV3	CMPD	2,S
	BHI	MCHVLP
MCHVX	LEAS	8,S	CLEAR STACK
	STD	OYV,X
	LDD	XTEMP
	ADDD	PLAXV
	STD	OXV,X
	LDA	MCHSLP	NEW SLEEP TIME
	JSR	RMAX
	STA	PD3,U
MCH10	BSR	MCHPIC
	NAP	6,MCH
*
*GET MUNCHY PICTURE
*X=OBJ,U=PROCESS
*PD2=IMAGE#,PD4=BASE IMAGE
MCHPIC	PSHS	D,Y
	LDA	PD2,U	GET IMAGE #
	INCA
	LDB	OXV,X
	BMI	MCHP1	CHECK DIR
	CMPA	#2
	BLS	MCHP2
	CLRA
	BRA	MCHP2
MCHP1	CMPA	#5
	BHI	MCHP11
MCHP10	CMPA	#3
	BHS	MCHP2
MCHP11	LDA	#3
MCHP2	STA	PD2,U
	LDY	PD4,U
	ASLA
	LDD	A,Y
	STD	OPICT,X
	PULS	D,Y,PC
*BAITER MASTER PICTURE TABLES
BMPT1	FDB	BMRP0,BMRP1,BMRP2,BMRP0,BMRP3,BMRP4	ROUND BM
BMPT2	FDB	BMSP0,BMSP1,BMSP2,BMSP0,BMSP3,BMSP4	SQUARE BM
*MUNCHY PICT TABLES
MCHPT1	FDB	MCHGP0,MCHGP1,MCHGP2,MCHGP0,MCHGP3,MCHGP4 GREEN
MCHPT2	FDB	MCHRP0,MCHRP1,MCHRP2,MCHRP0,MCHRP3,MCHRP4 RED
MCHPT3	FDB	MCHYP0,MCHYP1,MCHYP2,MCHYP0,MCHYP3,MCHYP4 YELLOW
*
*MUNCHY PICTS
*YELLOW MUNCHY
MCHYP0	FCB	3,5
	FDB	MCYD00,MCYD01,ON35,OFF35
MCHYP1	FCB	3,5
	FDB	MCYD10,MCYD11,ON35,OFF35
MCHYP2	FCB	3,5
	FDB	MCYD20,MCYD21,ON35,OFF35
MCHYP3	FCB	3,5
	FDB	MCYD30,MCYD31,ON35,OFF35
MCHYP4	FCB	3,5
	FDB	MCYD40,MCYD41,ON35,OFF35
MCYD00	FCB	$44,$44,$44,$44,$44
	FCB	$44,$44,$44,$44,$44
	FCB	$40,$40,$40,$40,$40
MCYD10	FCB	$44,$44,$44,$44,$44
	FCB	$44,$00,$00,$00,$44
	FCB	$40,$00,$00,$00,$40
MCYD20	FCB	$44,$44,$44,$44,$44
	FCB	$44,$44,$00,$44,$44
	FCB	$40,$40,$00,$40,$40
MCYD30	FCB	$44,$00,$00,$00,$44
	FCB	$44,$04,$04,$04,$44
	FCB	$40,$40,$40,$40,$40
MCYD40	FCB	$44,$44,$00,$44,$44
	FCB	$44,$44,$04,$44,$44
	FCB	$40,$40,$40,$40,$40
MCYD01	FCB	$04,$04,$04,$04,$04
	FCB	$44,$44,$44,$44,$44
	FCB	$44,$44,$44,$44,$44
MCYD11	FCB	$04,$04,$04,$04,$04
	FCB	$44,$40,$40,$40,$44
	FCB	$44,$00,$00,$00,$44
MCYD21	FCB	$04,$04,$04,$04,$04
	FCB	$44,$44,$40,$44,$44
	FCB	$44,$44,$00,$44,$44
MCYD31	FCB	$04,$00,$00,$00,$04
	FCB	$44,$00,$00,$00,$44
	FCB	$44,$44,$44,$44,$44
MCYD41	FCB	$04,$04,$00,$04,$04
	FCB	$44,$44,$00,$44,$44
	FCB	$44,$44,$44,$44,$44
*RED MUNCHY
MCHRP0	FCB	3,5
	FDB	MCRD00,MCRD01,ON35,OFF35
MCHRP1	FCB	3,5
	FDB	MCRD10,MCRD11,ON35,OFF35
MCHRP2	FCB	3,5
	FDB	MCRD20,MCRD21,ON35,OFF35
MCHRP3	FCB	3,5
	FDB	MCRD30,MCRD31,ON35,OFF35
MCHRP4	FCB	3,5
	FDB	MCRD40,MCRD41,ON35,OFF35
MCRD00	FCB	$22,$22,$22,$22,$22
	FCB	$22,$22,$22,$22,$22
	FCB	$20,$20,$20,$20,$20
MCRD10	FCB	$22,$22,$22,$22,$22
	FCB	$22,$00,$00,$00,$22
	FCB	$20,$00,$00,$00,$20
MCRD20	FCB	$22,$22,$22,$22,$22
	FCB	$22,$22,$00,$22,$22
	FCB	$20,$20,$00,$20,$20
MCRD30	FCB	$22,$00,$00,$00,$22
	FCB	$22,$02,$02,$02,$22
	FCB	$20,$20,$20,$20,$20
MCRD40	FCB	$22,$22,$00,$22,$22
	FCB	$22,$22,$02,$22,$22
	FCB	$20,$20,$20,$20,$20
MCRD01	FCB	$02,$02,$02,$02,$02
	FCB	$22,$22,$22,$22,$22
	FCB	$22,$22,$22,$22,$22
MCRD11	FCB	$02,$02,$02,$02,$02
	FCB	$22,$20,$20,$20,$22
	FCB	$22,$00,$00,$00,$22
MCRD21	FCB	$02,$02,$02,$02,$02
	FCB	$22,$22,$20,$22,$22
	FCB	$22,$22,$00,$22,$22
MCRD31	FCB	$02,$00,$00,$00,$02
	FCB	$22,$00,$00,$00,$22
	FCB	$22,$22,$22,$22,$22
MCRD41	FCB	$02,$02,$00,$02,$02
	FCB	$22,$22,$00,$22,$22
	FCB	$22,$22,$22,$22,$22
*GREEN MUNCHY
MCHGP0	FCB	3,5
	FDB	MCGD00,MCGD01,ON35,OFF35
MCHGP1	FCB	3,5
	FDB	MCGD10,MCGD11,ON35,OFF35
MCHGP2	FCB	3,5
	FDB	MCGD20,MCGD21,ON35,OFF35
MCHGP3	FCB	3,5
	FDB	MCGD30,MCGD31,ON35,OFF35
MCHGP4	FCB	3,5
	FDB	MCGD40,MCGD41,ON35,OFF35
MCGD00	FCB	$33,$33,$33,$33,$33
	FCB	$33,$33,$33,$33,$33
	FCB	$30,$30,$30,$30,$30
MCGD10	FCB	$33,$33,$33,$33,$33
	FCB	$33,$00,$00,$00,$33
	FCB	$30,$00,$00,$00,$30
MCGD20	FCB	$33,$33,$33,$33,$33
	FCB	$33,$33,$00,$33,$33
	FCB	$30,$30,$00,$30,$30
MCGD30	FCB	$33,$00,$00,$00,$33
	FCB	$33,$03,$03,$03,$33
	FCB	$30,$30,$30,$30,$30
MCGD40	FCB	$33,$33,$00,$33,$33
	FCB	$33,$33,$03,$33,$33
	FCB	$30,$30,$30,$30,$30
MCGD01	FCB	$03,$03,$03,$03,$03
	FCB	$33,$33,$33,$33,$33
	FCB	$33,$33,$33,$33,$33
MCGD11	FCB	$03,$03,$03,$03,$03
	FCB	$33,$30,$30,$30,$33
	FCB	$33,$00,$00,$00,$33
MCGD21	FCB	$03,$03,$03,$03,$03
	FCB	$33,$33,$30,$33,$33
	FCB	$33,$33,$00,$33,$33
MCGD31	FCB	$03,$00,$00,$00,$03
	FCB	$33,$00,$00,$00,$33
	FCB	$33,$33,$33,$33,$33
MCGD41	FCB	$03,$03,$00,$03,$03
	FCB	$33,$33,$00,$33,$33
	FCB	$33,$33,$33,$33,$33
*BAITER MASTER ROUND
BMRP0	FCB	3,7
	FDB	BMRD00,BMRD01,ON47,OFF47
BMRP1	FCB	4,7
	FDB	BMRD10,BMRD11,ON47,OFF47
BMRP2	FCB	4,7
	FDB	BMRD20,BMRD21,ON47,OFF47
BMRP3	FCB	4,7
	FDB	BMRD30,BMRD31,ON47,OFF47
BMRP4	FCB	4,7
	FDB	BMRD40,BMRD41,ON47,OFF47
BMRD00	FCB	$99,$98,$98,$98,$98,$98,$99
	FCB	$99,$88,$88,$88,$88,$88,$99
	FCB	$99,$88,$88,$88,$88,$88,$99
	FCB	$90,$90,$90,$90,$90,$90,$90
BMRD10	FCB	$99,$98,$98,$98,$98,$98,$99
	FCB	$99,$90,$90,$90,$90,$90,$99
	FCB	$99,$00,$00,$00,$00,$00,$99
	FCB	$90,$00,$00,$00,$00,$00,$90
BMRD20	FCB	$99,$98,$98,$98,$98,$98,$99
	FCB	$99,$88,$99,$90,$99,$88,$99
	FCB	$99,$88,$99,$00,$99,$88,$99
	FCB	$90,$90,$90,$00,$90,$90,$90
BMRD30	FCB	$99,$00,$00,$00,$00,$00,$99
	FCB	$99,$00,$00,$00,$00,$00,$99
	FCB	$99,$98,$98,$98,$98,$98,$99
	FCB	$90,$90,$90,$90,$90,$90,$90
BMRD40	FCB	$99,$98,$99,$00,$99,$98,$99
	FCB	$99,$88,$99,$00,$99,$88,$99
	FCB	$99,$88,$98,$98,$98,$88,$99
	FCB	$90,$90,$90,$90,$90,$90,$90
BMRD01	FCB	$09,$09,$09,$09,$09,$09,$09
	FCB	$99,$88,$88,$88,$88,$88,$99
	FCB	$99,$88,$88,$88,$88,$88,$99
	FCB	$99,$89,$89,$89,$89,$89,$99
BMRD11	FCB	$09,$09,$09,$09,$09,$09,$09
	FCB	$99,$89,$89,$89,$89,$89,$99
	FCB	$99,$00,$00,$00,$00,$00,$99
	FCB	$99,$00,$00,$00,$00,$00,$99
BMRD21	FCB	$09,$09,$09,$09,$09,$09,$09
	FCB	$99,$88,$89,$89,$89,$88,$99
	FCB	$99,$88,$99,$00,$99,$88,$99
	FCB	$99,$89,$99,$00,$99,$89,$99
BMRD31	FCB	$09,$00,$00,$00,$00,$00,$09
	FCB	$99,$00,$00,$00,$00,$00,$99
	FCB	$99,$09,$09,$09,$09,$09,$99
	FCB	$99,$89,$89,$89,$89,$89,$99
BMRD41	FCB	$09,$09,$09,$00,$09,$09,$09
	FCB	$99,$88,$99,$00,$99,$88,$99
	FCB	$99,$88,$99,$09,$99,$88,$99
	FCB	$99,$89,$89,$89,$89,$89,$99
*BAITER MASTER SQUARE
BMSP0	FCB	4,7
	FDB	BMSD00,BMSD01,ON47,OFF47
BMSP1	FCB	4,7
	FDB	BMSD10,BMSD11,ON47,OFF47
BMSP2	FCB	4,7
	FDB	BMSD20,BMSD21,ON47,OFF47
BMSP3	FCB	4,7
	FDB	BMSD30,BMSD31,ON47,OFF47
BMSP4	FCB	4,7
	FDB	BMSD40,BMSD41,ON47,OFF47
BMSD00	FCB	$FF,$FB,$FB,$FB,$FB,$FB,$FF
	FCB	$FF,$BB,$BB,$BB,$BB,$BB,$FF
	FCB	$FF,$BB,$BB,$BB,$BB,$BB,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$F0,$F0
BMSD10	FCB	$FF,$FB,$FB,$FB,$FB,$FB,$FF
	FCB	$FF,$F0,$F0,$F0,$F0,$F0,$FF
	FCB	$FF,$00,$00,$00,$00,$00,$FF
	FCB	$F0,$00,$00,$00,$00,$00,$F0
BMSD20	FCB	$FF,$FB,$FB,$FB,$FB,$FB,$FF
	FCB	$FF,$BB,$FF,$F0,$FF,$BB,$FF
	FCB	$FF,$BB,$FF,$00,$FF,$BB,$FF
	FCB	$F0,$F0,$F0,$00,$F0,$F0,$F0
BMSD30	FCB	$FF,$00,$00,$00,$00,$00,$FF
	FCB	$FF,$00,$00,$00,$00,$00,$FF
	FCB	$FF,$FB,$FB,$FB,$FB,$FB,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$F0,$F0
BMSD40	FCB	$FF,$FB,$FF,$00,$FF,$FB,$FF
	FCB	$FF,$BB,$FF,$00,$FF,$BB,$FF
	FCB	$FF,$BB,$FB,$FB,$FB,$BB,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$F0,$F0
BMSD01	FCB	$0F,$0F,$0F,$0F,$0F,$0F,$0F
	FCB	$FF,$BB,$BB,$BB,$BB,$BB,$FF
	FCB	$FF,$BB,$BB,$BB,$BB,$BB,$FF
	FCB	$FF,$BF,$BF,$BF,$BF,$BF,$FF
BMSD11	FCB	$0F,$0F,$0F,$0F,$0F,$0F,$0F
	FCB	$FF,$BF,$BF,$BF,$BF,$BF,$FF
	FCB	$FF,$00,$00,$00,$00,$00,$FF
	FCB	$FF,$00,$00,$00,$00,$00,$FF
BMSD21	FCB	$0F,$0F,$0F,$0F,$0F,$0F,$0F
	FCB	$FF,$BB,$BF,$BF,$BF,$BB,$FF
	FCB	$FF,$BB,$FF,$00,$FF,$BB,$FF
	FCB	$FF,$BF,$FF,$00,$FF,$BF,$FF
BMSD31	FCB	$0F,$00,$00,$00,$00,$00,$0F
	FCB	$FF,$00,$00,$00,$00,$00,$FF
	FCB	$FF,$0F,$0F,$0F,$0F,$0F,$FF
	FCB	$FF,$BF,$BF,$BF,$BF,$BF,$FF
BMSD41	FCB	$0F,$0F,$0F,$00,$0F,$0F,$0F
	FCB	$FF,$BB,$FF,$00,$FF,$BB,$FF
	FCB	$FF,$BB,$FF,$0F,$FF,$BB,$FF
	FCB	$FF,$BF,$BF,$BF,$BF,$BF,$FF
*
*	 PROBE START - PUT [A] PROBES OUT AND HAVE THEM COLIDE SOME 
*		AT A RANDOM COORDINATE SOME DELTA-T LATER
*

PRBST$	STA	XTEMP	COUNT IN RAM
	LDY	HSEED	GET A RANDOM X TO ARRIVE AT.
	PSHS	Y	OPERATE FROM STACK 
	LDA	SEED	GET A NEW ONE
	ANDA	#$1	KEEP IT SMALL
	LDB	HSEED
	ADDD	#$80	2 SECOND MINIMUM
	PSHS	D	OPERATE ON IT ON STACK 
	MAKP	PRBTIM	MAKE THE MESSAGE TIMER PROCESS
	LDD	,S	GET TIME
	STD	PD,X	AND PASS IT. 
PRBS1	OBI	PRBP1,PRBKIL,$CCCC GET THE OBJECT ETC.
	JSR	RAND
	LDB	LSEED
	LSRB
	ADDB	#YMIN
	STB	OY16,X
	LDB	SEED
	ANDB	#$3F
	LDA	XTEMP
	RORA
	BCC	PRBS0
	NEGB		EVERY OTHER ONE OPPOSITE DIR.
PRBS0	SEX
	STD	OXV,X	SET THAT VELOCITY
	BPL	PRBML1	NEGATIVE B
	NEGB
	BSR	PRBMUL	DO THE MULTIPLY
	BRA	PRBML2
PRBML1	BSR	PRBMUL
	COMA
	COMB
	ADDD	#1
PRBML2	PSHS	D
	LDB	HSEED	GET SOME FUDGE THEY WILL ALL PASS WITHIN 8
	SEX
	ASLB
	ROLA
	ASLB
	ROLA
	ASLB
	ROLA
	ASLB
	ROLA
	ADDD	4,S	GET ARRIVAL SPOT
	ADDD	0,S
	ADDD	0,S	TWICE BECAUSE OF SIGN BULLSHIT
	STD	OX16,X	THIS IS PLACE TO DEPART FROM
	LEAS	2,S
	LDB	LSEED
	ANDB	#$7F
	SUBB	#$40
	SEX
	BMI	PRBS2
	ORB	#$20
	BRA	PRBS3
PRBS2	ANDB	#$DF
PRBS3	STD	OYV,X
	LDA	#1	NO HYPER
	STA	OTYP,X
	JSR	APVCT
	DEC	XTEMP
	BNE	PRBS1
	LEAS	4,S	GET RID OF TEMPS
PRBSTX	RTS

PRBMUL	PSHS	B	SAVE MULTIPLIER
	LDA	3,S	GET HIGH HALF OF MULTIPLICAND
	MUL		COMPUTE SOME STUFF
	PSHS	B	SAVE LOW PART OF RESULT
	LDA	1,S	GET MULTIPLIER BACK
	LDB	5,S	LOW HALF OF MULTIPLICAND
	MUL
	ADDA	0,S	COLUMN ADD
	LSRA		DIVIDE BY 2 MAKING ROOM FOR SIGN BIT
	RORB			
	LEAS	2,S
	RTS		D HAS RESULT

PRBTIM	LDA	PRBCNT	CHECK OUT COUNT
	CMPA	#3
	BLO	PRBDIE	NOT ENOUGH...BYE BYE.
	CLR	XTEMP	USE AS COUNTER
	LDD	PD,U	GET 16 MS TIME
PRBT1	SUBD	#$40	TAKE A SECOND AWAY
	BCS	PRBT2
	INC	XTEMP
	BRA	PRBT1
PRBT2	LDA	XTEMP	GET THE NUMBER
	JSR	HEXBCD	MAKE BCD
	STA	PD,U	AND SAVE
PRBT3	LDX	#PODM1	GET TIME	
PRBT5	LDB	PD,U	SEND THE MESSAGE
	JSR	MESLDV	DONE.
	NAP	$40,PRBT4
PRBT4	LDA	PRBCNT	COUNT EM.
	CMPA	#3
	BLO	PRBDIE	NOT ENOUGH ANY MORE...BYE.
	LDA	PD,U	GET TIMER
	ADDA	#$99	TAKE ONE AWAY
	DAA
	BMI	PRBDIE	TIMER OUT...BYE
	STA	PD,U	GET READY FOR MESSAGE
	LDX	#PODM2
	BRA	PRBT5
PRBDIE	JMP	SUCIDE
RIPT2	FCB	$76
*
PODM1	FCB	$A0,PTR!+CLEAR,$50,PODM
	FDB	PODM2
PODM2	FCB	$A0,COND		
	FDB	PODM2
	FCB	$50,PODTM

*
*KILL PROBE
*
PRBKIL	LDD	#PHPOD	SUBSTITUTE PHONEY PICTURE
	STD	OPICT,X
	KILO	0210,PRHSND
	DEC	PRBCNT
	LDA	#6
	JSR	RMAX
	LDY	,S	GET RETURN ADDRESS
	CMPY	#SBRET	IS IT A SMART BOMB CALLING US??
	BEQ	PRBSB	THEN HANDLE SLIGHTLY DIFFERENTLY
	LEAY	,X
	JSR	MMSW
	RTS
*
PRBSB	LDB	#$45	450 POINTS NOMINALLY
	JSR	RAND	HALF OF THE TIME GIVE EXTRA
	RORA
	LDA	#$1	10'S HAVE THE POWER
	BCC	PRBS$1
	LDB	#$60	600 HALF OF THE TIME		
PRBS$1	JSR	SCORE
	JSR	GETOB	GET OBJECT BACK FROM POD.
	TFR	X,Y	SAVE OBJECT
	MAKP	SBSWP	MAKE A PROCESS TO DO THIS DEED
	STY	PD,X	AND PASS IT THE OBJECT
	RTS		AND WE'RE DONE!

SBSWP	NAP	4,SBSWP1 DELAY TO LET THE POD STUFF MOVE.
SBSWP1	LDA	#3	DO 3 OF THEM.		
	STA	PD+2,U
BOOM1	LDX	PD,U	GET OBJECT
	JSR	XSVCT	DO AN EXPLOSION
	LDD	PHFBP	LOAD FROM PHONEY FIREBALL POINTER
	STD	OPICT,X	THIS MAKES 2 PURPLE, 2 RED-YELLOW.
	DEC	PD+2,U
	BEQ	BOOM2
	NAP	3,BOOM1
BOOM2	LDD	OFREE
	STD	,X
	STX	OFREE
	DIE

RIPST	JSR	GNCIDE	STOP ALL OF THE ACTION
	LDA	#$FF
	STA	STATUS	FOR REAL!
	NAP	1,RIP99	STOP SAM BIT INTERFERENCE
RIP99	JSR	SCRCLR	CLEAR THE SCREEN
	JSR	PRINV	INITIALIZE PRINTING PARAMETERS
	LDB	#$7F
	STB	PCRAM+1	MAKE SURE ITS VISIBLE
	LDY	RIPTV
	LEAY	B,Y
RIPST0	LDA	,Y+	GET A BYTE
	BEQ	RIPEND	END.	
	CMPA	#$2	CHECK TIME??
	BNE	RIPST8
	LDA	PIA21	CHECK LEVER
	BPL	RIPEN2	HE LET GO...CAN IT!
	BRA	RIPST0	GO AND GET NEXT BYTE.
RIPST8	CMPA	#$1	CURSOR??
	BNE	RIPST1	NOPE
	LDX	,Y++
	CLR	TXFLAV	MAKE IT ALL LINE UP PRETTY
	BRA	RIPST0
RIPST1	ADDA	#$2E	GET OUT OF READABLE ASCII
	LDU	#RIPST0
	PSHS	U
	LDU	RIPT1
	LDB	RIPT2
	LEAU	B,U
	LEAU	B,U
	LEAU	B,U
	JMP	,U
*	BRA	RIPST0	THIS IS WHAT WOULD HAVE FOLLOWED A CONVENTIONAL JSR.
RIPEND	NAP	1,RIPEN1
RIPEN1	LDA	PIA21
	BMI	RIPEND
RIPEN2	JSR	SCRCLR
	JMP	[$FFFE]	END RESET
RIPT1	FDB	PR57V-($76*3)
*
*RANDOM VELOCITY
*X=OBJECT
RANDV	JSR	RAND
	LDB	SEED
	SEX
	ASLB
	ROLA	X2
	STD	OYV,X
	LDB	LSEED
	ANDB	#$3F
	ADDB	#-$20
	SEX
	STD	OXV,X
	RTS
*
*MAKE A MINI SWARMER
*Y=OBJECT AT CENTER,A=#
*
MMSW$	PSHS	D,X,Y,U
	STA	XTEMP
MMSLP	LDA	SWCNT
	INCA
	CMPA	#20
	BHI	MMSWX	TOO MANY GUYS
	MKPROB	MSWM,SWPIC1,MSWKIL,$2424
	BEQ	MMSWX	NONE LEFT
	STA	SWCNT
	LDD	OX16,Y
	STD	OX16,X
	LDD	OY16,Y
	STD	OY16,X
	BSR	RANDV
	LDD	HSEED
	ANDB	SWAC
	STB	PD2,U	ACCEL
	ANDA	#$1F
	STA	PTIME,U
	LDA	SWSTIM
	JSR	RMAX
	STA	PD4,U
	STX	OPTR
	DEC	XTEMP
	BNE	MMSLP
MMSWX	PULS	D,X,Y,U,PC
*
*MINI SWARM KILL
*X=OBJECT
*
MSWKIL	DEC	SWCNT
	JSR	KILOFF	TRASH IT
	PSHS	X
	JSR	KILLOP	AND ITS PROCESS
	PULS	X
	LDD	OX16,X	MODIFY FOR LARGER IMAGE
	SUBD	#$40
	STD	OX16,X
	LDA	OY16,X
	SUBA	#2
	STA	OY16,X
	LDU	#SWXP1
	STU	OPICT,X
	LDD	#$115	
	JSR	SCORE
	LDD	#SWHSND
	JSR	SNDLD	GET THE STUPID STUFF OUT OF THE WAY
	JMP	XSVCT

*
*MINI SWARM PROCESS
*PD=OBJECT,PD2 ACCELERATION,PD4=SHOT TIMER
*
MSWM	LDX	PD,U
	LDB	SWXV
	LDY	PLABX
	CMPY	OX16,X
	BHS	MSWM2
	NEGB
MSWM2	SEX
	STD	OXV,X	X VELOCITY
	BRA	MSWL
MSWLP	LDB	PD2,U
	LDX	PD,U
	LDA	PLAYC	GET Y ACCEL
	CMPA	OY16,X
	BHI	MSWM3
	NEGB
MSWM3	SEX
	ADDD	OYV,X
	CMPD	#$0200
	BLT	MSWM4
	LDD	#$0200
MSWM4	CMPD	#-$200
	BGT	MSWM5
	LDD	#-$200
MSWM5	STD	OYV,X
*DAMPING
	COMA
	COMB
	ASLB
	ROLA
	ASLB
	ROLA
	TFR	A,B
	SEX
	ADDD	OYV,X
	STD	OYV,X
*RANDOM FACTOR
	LDB	SEED
	ANDB	#$1F
	ADDB	#-$10
	SEX
	ADDD	OYV,X
	STD	OYV,X
	LDD	PLABX	PAST HIM?
	SUBD	OX16,X
	ADDD	#150*32
MSP	CMPD	#300*32	WAY PAST?
	BHI	MSWM	GO BACK + GIT IM
*FIRE?
MSWMF	DEC	PD4,U
	BNE	MSWL
	JSR	SWBMB	BOMB GUY
MSWL	NAP	3,MSWLP
*
*SWARM BOMB
*
SWBMB	PSHS	X
	LDD	PLABX
	SUBD	OX16,X
	EORA	OXV,X
	BMI	SWBX	NOT HEADED FOR PLAYER
	LEAY	,X
	SHL	FBOUT,BMBP1,BKIL
	BEQ	SWBX
	LDD	OXV,Y
	ASLB
	ROLA
	ASLB
	ROLA
	ASLB
	ROLA
	STD	OXV,X
	LDD	#SWSSND	SWARMER SOUND
	JSR	SNDLD
	CLRB
	LDA	PLAYC
	SUBA	OY16,X
SWB1	ASRA
	RORB
	ASRA
	RORB
	ASRA
	RORB
	ASRA
	RORB
	ASRA
	RORB
	STD	OYV,X
SWBX	LDA	SWSTIM
	JSR	RMAX
	STA	PD4,U
	PULS	X,PC
*
PHPOD	FCB	4,8
	FDB	PHPOD0,PHPOD0
	FDB	ON48,OFF48

PHPOD0	FCB	$00,$0E,$88,$D8,$F8,$8E,$08,$00
	FCB	$D8,$88,$8C,$C8,$8C,$88,$EF,$8F
	FCB	$F8,$8E,$88,$C8,$88,$8E,$8D,$E8
	FCB	$00,$80,$E8,$DD,$D8,$E8,$80,$00

UFNP1	FCB	6,4
	FDB	BAIT10,BAIT11
	FDB	ON64,OFF64
UFNP2	FCB	6,4
	FDB	BAIT20,BAIT21
	FDB	ON64,OFF64
UFNP3	FCB	6,4
	FDB	BAIT30,BAIT31
	FDB	ON64,OFF64
UFNP4	FCB	6,4
	FDB	BAIT40,BAIT41
	FDB	ON64,OFF64
UFNP5	FCB	6,4
	FDB	BAIT50,BAIT51
	FDB	ON64,OFF64
UFNP6	FCB	6,4
	FDB	BAIT60,BAIT61
	FDB	ON64,OFF64
UFNP7	FCB	6,4
	FDB	BAIT70,BAIT71
	FDB	ON64,OFF64
BAIT10	FCB	$00,$03,$31,$03
	FCB	$33,$11,$11,$33
	FCB	$33,$00,$00,$33
	FCB	$33,$01,$01,$33
	FCB	$30,$13,$11,$33
	FCB	$00,$00,$30,$00
BAIT11	FCB	$00,$00,$03,$00
	FCB	$03,$31,$11,$33
	FCB	$33,$10,$10,$33
	FCB	$33,$00,$00,$33
	FCB	$33,$11,$11,$33
	FCB	$00,$30,$13,$30
BAIT20	FCB	$00,$03,$31,$03
	FCB	$33,$11,$11,$33
	FCB	$33,$10,$10,$33
	FCB	$33,$00,$00,$33
	FCB	$30,$13,$11,$33
	FCB	$00,$00,$30,$00
BAIT21	FCB	$00,$00,$03,$00
	FCB	$03,$31,$11,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$00,$00,$33
	FCB	$33,$01,$01,$33
	FCB	$00,$30,$13,$30
BAIT30	FCB	$00,$03,$31,$03
	FCB	$33,$11,$11,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$00,$00,$33
	FCB	$30,$03,$01,$33
	FCB	$00,$00,$30,$00
BAIT31	FCB	$00,$00,$03,$00
	FCB	$03,$31,$11,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$10,$10,$33
	FCB	$33,$00,$00,$33
	FCB	$00,$30,$13,$30
BAIT40	FCB	$00,$03,$31,$03
	FCB	$33,$01,$01,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$10,$10,$33
	FCB	$30,$03,$01,$33
	FCB	$00,$00,$30,$00
BAIT41	FCB	$00,$00,$03,$00
	FCB	$03,$30,$10,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$00,$00,$33
	FCB	$00,$30,$13,$30
BAIT50	FCB	$00,$03,$31,$03
	FCB	$33,$00,$00,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$11,$11,$33
	FCB	$30,$03,$01,$33
	FCB	$00,$00,$30,$00
BAIT51	FCB	$00,$00,$03,$00
	FCB	$03,$30,$10,$33
	FCB	$33,$01,$01,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$10,$10,$33
	FCB	$00,$30,$13,$30
BAIT60	FCB	$00,$03,$31,$03
	FCB	$33,$00,$00,$33
	FCB	$33,$01,$01,$33
	FCB	$33,$11,$11,$33
	FCB	$30,$13,$11,$33
	FCB	$00,$00,$30,$00
BAIT61	FCB	$00,$00,$03,$00
	FCB	$03,$30,$10,$33
	FCB	$33,$00,$00,$33
	FCB	$33,$11,$11,$33
	FCB	$33,$11,$11,$33
	FCB	$00,$30,$13,$30
BAIT70	FCB	$00,$03,$31,$03
	FCB	$33,$10,$10,$33
	FCB	$33,$00,$00,$33
	FCB	$33,$11,$11,$33
	FCB	$30,$13,$11,$33
	FCB	$00,$00,$30,$00
BAIT71	FCB	$00,$00,$03,$00
	FCB	$03,$31,$11,$33
	FCB	$33,$00,$00,$33
	FCB	$33,$01,$01,$33
	FCB	$33,$11,$11,$33
	FCB	$00,$30,$13,$30

	END
