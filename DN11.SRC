	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E  >>>>>

.OPTR	EQU	PD
.VEL	EQU	PD+2
.SHOT	EQU	PD+3

	ORG	DNORG
	JMP	DYNSRT
	FDB	DYN0
	FDB	MIS0
*
DYNSRT	PSHS	A	COUNT ON STACK
DYNST1	MAKP	DYNPRC
	DEC	,S	ANY LEFT??
	BNE	DYNST1
	PULS	A,PC

DYNPRC	OBI	DYN0,DYNKIL,DYNBLP	
	JSR	RAND	CYCLE ONCE
	LSRA		DOWN THE HORIZON
	ADDA	#$40
	STD	OX16,X	SETS X.
	ADDA	#YMIN+$D0 TOWARD CENTER.
	STD	OY16,X
	LDD	#0
	STD	OXV,X
	STD	OYV,X	*************************************BSO BONER
	FCB	$AF,$C9,$00,$07 ((STX	.OPTR,U)) SAVE OBJECT 
	STX	OPTR	*************************************BSO BONER
	LDA	#1	*************************************BSO BONER
	FCB	$A7,$C9,$00,$09 ((STA	.VEL,U)) SETUP FOR INITIAL VELOCITY CORRECTION
	LDA	DYNSHT	*************************************BSO BONER
	COMA		*************************************BSO BONER
	FCB	$A7,$C9,$00,$0A ((STA	.SHOT,U)) DELAY FIRST SHOT ACCORDINGLY
*			*************************************BSO BONER

DNLOOP	JSR	DNPICT	CYCLE THE PICTURE AND TIMERS
	TST	.VEL,U	NEW VELOCITY TIME??
	BNE	DNLOP1	NOPE
DNWAKE	LDA	HSEED	GET A SEEDY GUY
	ANDA	#$1F	UP TO 1 SECOND 
	ADDA	#$10	.5 - 1.5
	STA	.VEL,U	NEW TIME
	LDD	OYV,X	GET THE CURRENT Y VELOCITY
	BPL	YV1	POS. CHECK
	CMPD	#$FF80	1/2 PIX??
	BHI	YV2	YEP...NORMAL CHANGE
	LDB	SEED
	BRA	DNLP5
YV1	CMPD	#$80
	BLO	YV2	
	LDB	SEED
	BRA	DNLOP2
YV2	LDB	SEED
	LDA	OY16,X	GET THE CURRENT HEIGHT
	CMPA	#($100-YMIN)/2	HIGH OR LOW??
	BHS	DNLOP2	HIGH..GO LOWER
DNLP5	ANDB	#$3F
	BRA	DNLOP3
DNLOP2	ORB	#$C0
DNLOP3	SEX
	ADDD	OYV,X	CHANGE VELOCITY APPRORIATELY.
	STD	OYV,X
	LDD	OXV,X	CHECK X VELOCITY FOR TOO LARGE
	BPL	DNLP0
	CMPD	#-40	
	BHS	DNLOP4	OK
	BRA	DNLP1
DNLP0	CMPD	#40
	BLS	DNLOP4
DNLP1	ASRA
	RORB
	STD	OXV,X
DNLOP4	LDB	LSEED
	ASRB
	ASRB
	ASRB
	ASRB
	SEX
	ADDD	OXV,X
	STD	OXV,X
DNLOP1	TST	.SHOT,U	SHOT TIME?
	BNE	DNLOP9
	LDD	OX16,X	GET THE X COORD
	SUBD	BGL	SEE IF ON SCREEN
	CMPD	#300*32
	BHS	DNLOP9	NOT ON SCREEN
	LDD	PLABX	GET PLAYERS WORLD COORD
	SUBD	OX16,X	SUBTRACT OURS
	BPL	DNLOP5
	COMA
	COMB		MAKE POSITIVE FOR COMPARE
DNLOP5	CMPD	#50*32	WITHIN 50 PIXELS??
	BLS	DNLOP9	THEN DON'T FIRE.
	LDA	#$18
	JSR	RMAX
	STA	XTEMP
	LDA	DYNSHT	GET FREQUENCY PARAMETER
	COMA
	ADDA	XTEMP
	BCC	DNLPP6
	LDA	#$FF
DNLPP6	STA	.SHOT,U	AND SAVE IT DOWN
	LDA	MISCNT	HOW MANY OUT THERE??
	CMPA	#6	6??
	BHS	DNLOP9	YEP...DONT MAKE ONE
	LEAY	,X	PUT OBJECT IN Y
	MKPROB	MISILE,MIS0,MISKIL,MISBLP
	BEQ	DNLOP9	FAIL!
	STY	PD+2,U	PASS OUR OBJECT.
	INC	MISCNT	INDICATE ONE MORE
	LDD	#MISSND
	JSR	SNDLD	SEND OUT A MISSLE SOUND
	LDD	OX16,Y
	CMPD	PLABX
	BPL	DNLP1$	PLAYER'S TO OUR LEFT
	LDD	OXV,Y	GET OUR VELOCITY
	BMI	DNLOP9	HES TO OUR RIGHT, VELOCITY TO LEFT...NEGATIVE OK
	BRA	DNLP2$
DNLP1$	LDD	OXV,Y
	BPL	DNLOP9
DNLP2$	COMA
	COMB
	STD	OXV,Y
DNLOP9	NAP	2,DNLOOP

MISSND	FCB	$C0,$01,$40,$19,$1,$01,$16,$0

DNPICT	LDX	.OPTR,U	GET OUR POINTER
	LDD	OX16,X	ON SCREEN??
	SUBD	BGL
	ADDD	#50*32
	CMPD	#400*32
	BHS	DNPIC4
	LDD	OBJX X	SEE IF ACTUALLY ON SCREEN.
	BNE	DNPCC1	YEP
	LDA	#19	STOP A QUICK SHOT!
	STA	.SHOT,U	
DNPCC1	LDY	OPICT,X	GET OUR PICTURE POINTER
	LDD	OXV,X	CHECK DIRECTION
	BMI	DNPIC5	NEGATIVE
	LEAY	-OBJL,Y
	CMPY	#DYN0
	BHS	DNPIC1	IN RANGE...JUST STORE
	LDY	#DYN3
	BRA	DNPIC1
DNPIC5	LEAY	OBJL,Y	MOVE TO NEXT
	CMPY	#DYN3	PAST LAST??
	BLS	DNPIC1	NOPE
	LDY	#DYN0
DNPIC1	STY	OPICT,X	STORE
DNPIC2	LDA	.SHOT,U	SHOT TIMER ZERO??
	BEQ	DNPIC3
	DEC	.SHOT,U	DECREMENT SHOT TIMER
DNPIC3	DEC	.VEL,U	VELOCITY TIMER
	RTS

DNPIC4	LDA	#19
	STA	.SHOT,U
	NAP	$10,DNWAK1 FAR OFF SCREEN...				
DNWAK1	LDX	PD,U	GET OBJECT.
	JMP	DNWAKE

DYNKIL	DEC	DYNCNT
	LDD	#PHDYN
	STD	OPICT,X
	KILP	0120,TIHSND	
	RTS

MISILE	LDY	PD+2,U	GET OBJECT FIRING US.
	LDX	PD,U	AND GET OUR OBJECT
	LDD	OX16,Y	GET OUR X COORD.
	SUBD	PLABX	SEE WHICH SIDE PLAYER IS ON
	BPL	MISS11	HE'S TO OUR LEFT
	LDD	OX16,Y	GET OBJECTS X
	ADDD	#7*32	2 PIXELS OVER.
	BRA	MISS22
MISS11	LDD	OX16,Y
	SUBD	#3*32
MISS22	STD	OX16,X
	LDD	OY16,Y
	ADDA	#2	SAME FOR Y.
	STD	OY16,X	
	LDD	#0
	STD	OXV,X
	STD	OYV,X
	INCA
	STA	PD+2,U	INSTANT VELOCITY CHANGE
	STX	OPTR
	STX	.OPTR,U
*
MSLOOP	JSR	MSPICT	DO THE PICTURE NUMBER
	DEC	PD+2,U	VELOCITY DIVIDER
	BNE	MSLOP1	NOT THIS TIME
	LDA	#4
	STA	PD+2,U
	BSR	XCHK	CHECK THE X RANGE.
	BLS	NEWY	IN HITTING DISTANCE....
	BSR	YCHK	X NOT GOOD BE CAREFUL ABOUT Y
	BHI	NEWY	Y NOT CLOSE ENOUGH...ZERO IN.
	LDD	#0	Y CLOSE ENOUGH.
	STD	OYV,X
	BRA	MSLOP2
NEWY	LDD	PLAYC	GET THE PLAYERS Y.
	CMPD	OY16,X	COMPARE TO OURS
	BHS	NEWY1
	LDB	MISVEL
	COMB
	BRA	NEWY2
NEWY1	LDB	MISVEL
NEWY2	SEX
	ASLB
	ROLA		DOUBLE FOR Y.
	STD	OYV,X
MSLOP2	BSR	XCHK
	BLS	MSLOP6	CLOSE ENOUGH IN X
	LDD	PLABX
	CMPD	OX16,X
	BPL	NEWX1	HE'S IN FRONT OF US	(NO LINE)
	LDB	MISVEL	1 PIXEL PER SECOND
	COMB
	BRA	NEWX2
NEWX1	LDB	MISVEL
NEWX2	SEX
	STD	OXV,X	STORED.
MSLOP1	NAP	3,MSLOOP LOOP TIME.
MSLOP6	LDD	#0
	STD	OXV,X
	BRA	MSLOP1

XCHK	LDD	PLABX
	ADDD	#32*5	ADD 5 PIXELS TO MAKE SYMMETRIC
	SUBD	OX16,X	FIND DIFFEENCE
	BPL	XCHK1
	COMA
	COMB
	ADDD	#1
XCHK1	CMPD	#9*32
	RTS

YCHK	LDD	PLAYC
	SUBD	OY16,X
	BPL	YCHK1
	COMA
	COMB
	ADDD	#1
YCHK1	CMPD	#$A00
	RTS

MSPICT	LDX	.OPTR,U
	LDD	OX16,X
	SUBD	BGL	
	CMPD	#300*32	ON SCREEN??
	BHI	MSPIC2
	LDY	OPICT,X
	LEAY	OBJL,Y
	CMPY	#MIS1
	BLS	MSPIC1
	LDY	#MIS0
MSPIC1	STY	OPICT,X
	RTS

MSPIC2	NAP	12,MSLOOP

MISKIL	DEC	MISCNT
	LDD	#PHMIS
	STD	OPICT,X
	KILP	0110,TIHSND
	RTS

DYN0	FCB	5,9
	FDB	DYNP00,DYNP01
	FDB	ON59,OFF59

DYN1	FCB	5,9
	FDB	DYNP10,DYNP11
	FDB	ON59,OFF59

DYN2	FCB	5,9
	FDB	DYNP20,DYNP21
	FDB	ON59,OFF59

DYN3	FCB	5,9
	FDB	DYNP30,DYNP31
	FDB	ON59,OFF59

PHDYN	FCB	5,9
	FDB	PHDYN0,PHDYN0
	FDB	ON59,OFF59

PHDYN0	FCB	$00,$00,$00,$02,$99,$02,$00,$00,$00
	FCB	$00,$02,$99,$92,$92,$92,$99,$02,$00
	FCB	$90,$92,$99,$22,$92,$22,$99,$92,$90
	FCB	$00,$00,$90,$92,$99,$92,$90,$00,$00
	FCB	$00,$00,$00,$00,$90,$00,$00,$00,$00

DYNP00	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
	FCB	$00,$02,$90,$02,$00,$02,$90,$02,$00
	FCB	$90,$02,$00,$02,$90,$02,$00,$02,$90
	FCB	$00,$00,$90,$00,$00,$00,$90,$00,$00
	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
DYNP10	FCB	$00,$00,$00,$09,$00,$00,$00,$00,$00
	FCB	$00,$09,$20,$00,$20,$09,$00,$00,$00
	FCB	$00,$00,$20,$09,$20,$00,$20,$09,$00
	FCB	$00,$00,$00,$00,$20,$09,$20,$00,$00
	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
DYNP20	FCB	$00,$00,$00,$02,$90,$02,$00,$00,$00
	FCB	$00,$00,$90,$02,$00,$02,$90,$00,$00
	FCB	$00,$00,$00,$02,$90,$02,$00,$00,$00
	FCB	$00,$00,$90,$02,$00,$02,$90,$00,$00
	FCB	$00,$00,$00,$00,$90,$00,$00,$00,$00
DYNP30	FCB	$00,$00,$00,$00,$00,$09,$00,$00,$00
	FCB	$00,$00,$00,$09,$20,$00,$20,$09,$00
	FCB	$00,$09,$20,$00,$20,$09,$20,$00,$00
	FCB	$00,$00,$20,$09,$20,$00,$00,$00,$00
	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
DYNP01	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
	FCB	$00,$00,$09,$00,$00,$00,$09,$00,$00
	FCB	$09,$20,$00,$20,$09,$20,$00,$20,$09
	FCB	$00,$20,$09,$20,$00,$20,$09,$20,$00
	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
DYNP11	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
	FCB	$00,$00,$02,$90,$02,$00,$00,$00,$00
	FCB	$00,$90,$02,$00,$02,$90,$02,$00,$00
	FCB	$00,$00,$00,$90,$02,$00,$02,$90,$00
	FCB	$00,$00,$00,$00,$00,$90,$00,$00,$00
DYNP21	FCB	$00,$00,$00,$00,$09,$00,$00,$00,$00
	FCB	$00,$00,$09,$20,$00,$20,$09,$00,$00
	FCB	$00,$00,$00,$20,$09,$20,$00,$00,$00
	FCB	$00,$00,$09,$20,$00,$20,$09,$00,$00
	FCB	$00,$00,$00,$20,$09,$20,$00,$00,$00
DYNP31	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
	FCB	$00,$00,$00,$00,$02,$90,$02,$00,$00
	FCB	$00,$00,$02,$90,$02,$00,$02,$90,$00
	FCB	$00,$90,$02,$00,$02,$90,$00,$00,$00
	FCB	$00,$00,$00,$90,$00,$00,$00,$00,$00

PHMIS	FCB	3,5
	FDB	PHMIS0,PHMIS0
	FDB	ON35,OFF35

MIS0	FCB	3,5
	FDB	MIS00,MIS01
	FDB	ON35,OFF35

MIS1	FCB	3,5
	FDB	MIS10,MIS11
	FDB	ON35,OFF35

MIS00	FCB	$00,$09,$90,$09,$00
	FCB	$90,$09,$00,$09,$90
	FCB	$00,$00,$90,$00,$00

MIS01	FCB	$00,$00,$09,$00,$00
	FCB	$09,$90,$00,$90,$09
	FCB	$00,$90,$09,$90,$00

MIS10	FCB	$00,$00,$22,$00,$00
	FCB	$20,$20,$92,$20,$20
	FCB	$00,$00,$20,$00,$00

MIS11	FCB	$00,$00,$02,$00,$00
	FCB	$02,$02,$29,$02,$02
	FCB	$00,$00,$22,$00,$00

PHMIS0	FCB	$00,$09,$92,$09,$00
	FCB	$90,$29,$92,$29,$90
	FCB	$00,$00,$90,$00,$00

MISBLP	EQU	$9000
DYNBLP	EQU	$9229

	ORG	$5F70
	FCB	$B7	MATCH THE PROMS GOOFY


	END
