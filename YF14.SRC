	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E  >>>>>

	ORG	YFORG
	JMP	FBSTR$
	JMP	YLST$
	JMP	RIPCHK
	FDB	PHFBPC
	FDB	FBMR0
	FDB	FBALL0
	FDB	YLPL0
*
*YLLABIAN START
*A=#
*RANDOM SQUADS 2,3 PER
*
YLST$	PSHS	A,B
YLST0	LDB	,S
	BEQ	YLSTX	DONE
	CMPB	#2
	BLS	YLSQ
	JSR	RAND	2 OR 3?
	LDB	#2
	LSRA
	BCC	YLSQ
	ADDB	#1
*MAKE A SQUAD
YLSQ	STB	1,S	SAVE #
	LDD	HSEED	RAND START POINT
	ASRA
	ASRA
	ADDD	PLABX
	ADDA	#$80
	STD	XTEMP2
	LDA	YLXV
	PSHS	A
	LSRA
	LSRA
	NEGA
	ADDA	,S+	CRUISE V =.75 NORMAL
	JSR	RMAX
	BITA	#1
	BEQ	YLSQ1
	NEGA
YLSQ1	STA	XTEMP+1	VELOCITY GUYS
	LDA	LSEED
	LSRA
	STA	XTEMP	Y OFFSET
YLSQ0	MKPROB	YLPP,YLPL0,YLKIL,$2222
	BEQ	YLSTX	NOBODY HOME
	JSR	RAND
	LDD	HSEED
	ANDA	#3
	ADDD	XTEMP2	X SPREAD
	STD	OX16,X
	JSR	GETALT
	SUBA	XTEMP
	STA	OY16,X
	LDA	XTEMP	Y SPACING
	ADDA	#4
	STA	XTEMP
	STA	PD2,U	Y SPACING
	LDB	XTEMP+1
	SEX
	STD	OXV,X
	JSR	GYLPIC	GET YUR PICTY-POOH
	JSR	APVCT	APPEAR GUYS
	INC	YLCNT
	DEC	,S	DEC TOTAL CNT
	DEC	1,S	DEC SQUAD COUNT
	BNE	YLSQ0
	BRA	YLST0
YLSTX	PULS	D,PC
*
*GET YLLABIAN PICTURE
*X=OBJ
GYLPIC	LDY	#YLPL0
	LDA	OXV,X
	BPL	GYLP0
	LEAY	50,Y	ACCESS NEGATIVE IMAGES
GYLP0	LDD	OYV,X
	ASLB
	ROLA
	ADDA	#2
	BPL	GYLP1
	CLRA
GYLP1	CMPA	#4
	BLS	GYLP2
	LDA	#4
GYLP2	LDB	#10
	MUL
	LEAY	B,Y
	STY	OPICT,X
	RTS
*
*YLLABIAN BOMB ROUTINE
*X=OBJ
YLBOMB	PSHS	X
	LDD	PLABX
	SUBD	OX16,X
	EORA	OXV,X
	BMI	YLBX	NOT HEADED FOR PLAYER
	LDA	PLAYC
	SUBA	OY16,X
	EORA	OYV,X
	BMI	YLBX	NOT HEADED FOR PLAYER
	LDA	YLSTIM	NEW TIMER
	JSR	RMAX
	STA	PD6,U
	LDA	BMBCNT
	CMPA	#15
	BHI	YLBX	TOO MANY ALREADY
	LEAY	,X
	SHL	YLBOUT,YLBP1,BKIL
	BEQ	YLBX	NONE LEFT
	LDB	SEED
	ANDB	#$1F	RANDOM FACTOR
	ADDB	#-$10
	ADDB	PLAXC
	SUBB	OBJX,X
	SEX
	ASLB
	ROLA
	ASLB
	ROLA
	STD	OXV,X
	LDD	OXV,Y
	ASLB
	ROLA
	ASLB
	ROLA
	ADDD	OXV,X
	STD	OXV,X
	LDB	LSEED
	ANDB	#$1F
	ADDB	#-$10
	ADDB	PLAYC
	SUBB	OBJY,X
	SEX
	ASLB
	ROLA
	ASLB
	ROLA
	ADDD	PLAYV
	ASRA
	RORB
	STD	OYV,X
	LDD	#YLSSND	MAKE SHOT SOUND
	JSR	SNDLD
YLBX	PULS	X,PC
YLSSND	FCB	$C0,$03,$08,$02,$0 YLLAB SHOT
*
*KILL YLLABIANS
*
YLKIL	DEC	YLCNT	DEAD GUY
	KILP	0120,UFHSND POINTS,SOUND
	RTS
*
*YLLABIAN PROCESS
*PD=OBJECT,PD2=ALT OFFSET
*PD3=DIVE OFFSET, PD4=ACCEL, PD5=DIVE TIME
*PD6=SHOT TIMER
YLPP	LDX	PD,U
	LDD	OPICT,X
	CMPD	#NULOB	APPEARING?
	BNE	YLPP1
	NAP	6,YLPP	WAIT TO FINISH
YLPP1	LDX	PD,U
	LDA	OBJY,X	ON SCREEN?
	BNE	YLPS	TARGET ENGAGED
YLPP2	JSR	GETALT	FLY IN FORMATION
	CLRB
	SUBA	OY16,X
	SUBA	PD2,U
	CMPA	#-10
	BGT	YLPP3
	LDB	#-1
YLPP3	CMPA	#10
	BLT	YLPP4
	LDB	#1
YLPP4	STB	OYV,X	Y VELOCITY
	NAP	10,YLPP1
*ENGAGE THE ENEMY
YLPS	LDA	#$0F	RANDOM SLEEP
	JSR	RMAX
	LDX	#YLPS1
	JMP	SLEEP	RANDOM SLEEP TIME
*START A DIVE
YLPS1	LDX	PD,U	GET OBJECT PTR
	LDA	SEED
	ANDA	#$1F
	SUBA	#$F
	STA	PD3,U	DIVE Y OFFSET
	LDA	LSEED
	ANDA	#$F
	ADDA	#$2
	STA	PD5,U	DIVE TIMER
	LDA	YLSTIM	SHOT TIMER
	STA	PD6,U
	LDA	YLAC
	JSR	RMAX
	STA	PD4,U	ACCELERATION
*X VELOCITY
	LDD	PLABX
	CMPD	OX16,X
	PSHS	CC
	LDA	YLXV
	JSR	RMAX
	TFR	A,B
	PULS	CC
	BHS	YLPS2
	NEGB
YLPS2	SEX
	STD	OXV,X
*Y ACCELL
YLPS2L	LDX	PD,U
	LDB	PD4,U	GET ACCELL
	LDA	PLAYC
	ADDA	PD3,U	DIVE OFFSET
	CMPA	OY16,X
	BHI	YLPS5
	NEGB
YLPS5	SEX
	ADDD	OYV,X
	STD	OYV,X
	COMA
	COMB
	ASRA
	RORB
	ASRA
	RORB
	ASRA	
	RORB
	ADDD	OYV,X
	STD	OYV,X
	DEC	PD6,U	TIME TO SHOOT
	BNE	YLPS6
	JSR	YLBOMB	BOMB THE GUY
YLPS6	DEC	PD5,U	NEW DIVE?
	BEQ	YLPS1
	JSR	GYLPIC
	NAP	4,YLPS2L
*
* RIPOFF CHECK
*

RIPCHK	MAKP	RIPC0
	RTS

RIPC0	LDA	RIPOFF	ALREADY ACTIVE??
	BNE	RIPDIE	THEN NO NEED TO LIVE.
	INC	RIPOFF	EXCLUDE PLEASE
	LDA	#$3
	STA	PD,U
RIPC2	NAP	8,RIPC1
RIPC1	LDA	PIA21
	CMPA	#$51	REV-START2-FIRE
	BEQ	RIPC4
	CMPA	#$E2	STILL OLD COMBO??
	BEQ	RIPC2	THEN DON'T COUNT
	DEC	PD,U
	BNE	RIPC2
	BRA	RIPC3
RIPC4	LDA	#$3
	STA	PD,U
RIPC5	NAP	8,RIPC6
RIPC6	LDA	PIA21
	CMPA	#$A3	DN-START1-TH-FIRE
	LBEQ	RIPV1	DONE!
	CMPA	#$51	OLD COMBO??
	BEQ	RIPC5
	DEC	PD,U	LESS TIME
	BNE	RIPC5
RIPC3	CLR	RIPOFF
RIPDIE	JMP	SUCIDE
*
.OPTR	EQU	PD	OBJECT POINTER
.COAST	EQU	.OPTR+2	NUMBER OF WAKEUPS TILL NEXT UPDATE
.DELA	EQU	.COAST+1
.RET	EQU	PD+6	PLACE TO HOLD CALLER
*
*	FIREBOMBER	(TM) (R) (C) (P) (BS) (ETC)
*
FBSTR$	PSHS	A	COUNT ON STACK 
FBSTR1	NEWP	FBPROC,STYPE JUST CREATE A BUNCH OF PROCS.
	DEC	,S	ANY MORE??
	BNE	FBSTR1
	PULS	A,PC	AND RETURN

FBPROC	OBI	FBMR0,FBKILL,FBBLIP BOY WHAT A STUPID OBJECT SYSTEM!
	JSR	RAND	GET A RADNUB
	LDB	#$50
	MUL
	ADDA	#$80	MAKE IT APPEAR TOWARD ASS.
	STD	OX16,X	THIS IS THE 16 BIT X
	ADDA	#YMIN+$80 BRING BACK 1/4 THEN ADD YMIN.
	STD	OY16,X	LOOKS VALID TO ME.
	LDA	#1	NON-HYPERABLE FOR SHOWDOWNS!
	STA	OTYP,X	*********************************************BSO BONER
	FCB	$AF,$C9,$00,$07 ((STX	.OPTR,U)) SAVE IN PROCESS BLOCK*******
	STX	OPTR	*********************************************BSO BONER
*
***	 OFF SCREEN NEW VELOCITY
*
FBONV	JSR	FBXV	GET THE X VELOCITY
	LDB	SEED
	ANDB	#$0F
	ADDB	#8
	STB	.COAST,U NUMBER OF WAKEUPS TO COAST
	JSR	FBYV	GET AN ADEQUATE Y VELOCITY
*
*	OFF SCREEN UPDATE
*
FBOFUP	LDX	.OPTR,U	GET OBJECT POINTER
	LDA	STATUS	SEE IF OBJECTS DISABLED
	BITA	#$20
	BNE	FBLOOP	THEN TURN ON IN CASE OF QUICK APPEAR.
	JSR	FBDIST	CHECK OUT OUR ON-SCREENESS
	BLS	FBLOOP	TURN IT ON...WE'RE GETTING CLOSE!
	DEC	.COAST,U ALL AND ALL ITS JUST ANOTHER FRAME IN THE BALL.
	BMI	FBONV	FIND AN OFF SCREEN NEW VELOCITY
	NAP	10,FBOFUP TAKE IT VERY EASY WHEN THIS FAR AWAY!

*	ON SCREEN...AGREEABLE TO THE CURRENT Y POSITION.

FBYDLT	EQU	3	DISTANCE WHEN BOMB SHOULD LAUNCH

FBLOOP	CLR	.DELA,U	INDICATE NO DELAY ON ENTRY
FBGO	LDA	.DELA,U	CHECK DELAY
	BEQ	FBLOP0
	DEC	.DELA,U	TAKE ONE AWAY.
FBLOP0	LDX	.OPTR,U	GET OUR POINTER
FBGO0	LDA	OY16,X	GET HEIGHT
	SUBA	PLAYC	SUBTRACT Y COORD.
	STA	XTEMP	SAVE FOR POSSIBLE CORRECTION
	BMI	FBGO1	HE'S BELOW US.....
	CMPA	#FBYDLT+6 ADD SHIP HEIGHT ALSO
	BHI	FBGOF0	NOT IN RANGE....CONTINUE
	LDY	#FBFIRU	OK TO FIRE??
	BSR	FIREFB	THEN FIRE UP
FBGOF0	CMPA	#(2*FBYDLT)+6
	BHS	FBGO3	NOT EVEN CLOSE!
FBGOO	LDB	FBDV	GET THE CURRENT DODGE VELOCITYT MUCH
	CLRA
	TST	XTEMP
	BPL	FBGOOO
	COMA	NEGATE
	COMB
FBGOOO	TST	INVFLG	INVISIBLE??
	BNE	FBGOO2	THEN ACT STUPID
	ASLB
	ROLA
FBGOO2	STD	OYV,X	STORE
	BRA	FBGOO1
FBGO1	NEGA		MAKE POSITIVE.....MY SANITY IS WORTH 2 CYCLES.
	CMPA	#FBYDLT+9 ADD MY HEIGHT 
	BHI	FBGOF1	AND FIRE DOWNWARD AT HIM.
	LDY	#FBFIRD	TEST
	BSR	FIREFB	OK TO FIRE.
FBGOF1	CMPA	#(2*FBYDLT)+9
	BLS	FBGOO	CLOSE ENOUGH TO RUN!!
FBGO3	DEC	.COAST,U WELL...TIME FOR NEXT CYCLE??
	BMI	FBGO4	YEP
	BRA	FBGOV	NAP AND STUFF
FBGO4	JSR	FBXV	GET A NEW X.
	BSR	FBYV	GET A NEW Y.
FBGOO1	LDA	SEED	RADNUB
	ANDA	#$7F
	ADDA	#30	MINIMUM
	STA	.COAST,U TIME TILL NEXT
FBGOV	LDD	OBJX,X	GET PICTURE
	BNE	FBGOV1	ON SCREEN...OK.
	NAP	1,FBOFUP THEN DO ON SCREEN UPDATE
FBGOV1	JSR	FBIMAG
	NAP	1,FBGO	AND SLEEP ON IT.

FBYV	LDB	LSEED	GET A RAD.
	SEX
	BPL	FBGO5
	SUBD	#$A0
FBGO5	ADDD	#$50	MIN OF 20 REAL UNITS
	STD	OYV,X
	RTS

*
*	FIRE A BOMB UPWARD
*

FIREFB	LDU	PLRX
	LDA	PWAV,U
	LDU	CRPROC
	DECA		WAVE 1 DONT SHOOT
	BEQ	FIRBYE
	CMPA	#$2	WAVE 2 OR 3??
	BHI	FIRFB$	NOPE
	DECA
	STA	XTEMP	WAVE NUMBER
	JSR	RAND	SEE BASED ON RANDOM
	CMPA	#$E0
	BHS	FIBYE$	TOO HIGH FOR EITHER WAVE
	CMPA	#$80
	BLS	FIRFB$
	LDA	XTEMP
	BEQ	FIBYE$
FIRFB$	TST	.DELA,U	DELAY SET UP
	BNE	FIRBYE	THEN NO BOMB
	LDD	OBJX,X	OFF SCREEN
	BEQ	FIRBYE
	JMP	,Y	OTHERWISE GO TO DESTINATION
FIRBYE	RTS		NOT FIRABLE...RETURN

FIBYE$	LDA	FBFREQ	SETUP FIRING DELAY
	COMA
	STA	.DELA,U
	RTS


FBFIRU	JSR	FBSETY	SET A NEW Y BASED ON POSITIONS
	LDD	OX16,X
	SUBD	PLABX	COMPARE TO PLAYERS
	BMI	FBU1	HE'S TO OUR RIGHT
	JSR	FBXR	GIVE US AN X TO THE RIGHT (SHOOT LEFT)
	LDY	#-$60	FIRE TO LEFT
	BRA	BOTFIR	FIRE ONE FROM THE BOTTOM AND MERGE
FBU1	JSR	FBXL	GIVE US AN X TO THE LEFT
	LDY	#$60
	BRA	BOTFIR

*
***	 FIRE DOWN
*

FBFIRD	JSR	FBSETY	SET THE NEW Y
	LDD	OX16,X
	SUBD	PLABX
	BMI	FBD1	HE'S TO OUR RIGHT
	JSR	FBXL	HE'S TO OUR LEFT..SO GO LEFT
	LDY	#-$80	FIRE TO THE LEFT
	BRA	TOPFIR	FROM THE TOP
FBD1	JSR	FBXR
	LDY	#$80
TOPFIR	LDA	SEED	DIRECTION OF Y ACCELERATION (DOWN)
	ANDA	#$7
	ADDA	#18	RANDOM ARC DOWNWARD
	STA	XTEMP2
	LDD	OY16,X	USE THE SAME Y FOR TOP FIRING
	BRA	BOTFI1	

BOTFIR	LDA	SEED	(WAS 30)
	ORA	#$F8
	SUBA	#-18
	STA	XTEMP2
	LDD	OY16,X
	ADDA	#4	ADD THE HEIGHT DIFFERENCE.
BOTFI1	STD	XTEMP	SAVE THE Y
	LDD	OX16,X	GET THE X
	ADDD	#32*2	SLIDE OVER 2 PIXELS
	MKPROB	FBALL,FBALL0,FKILL,FBLIP GET A FIREBALL
	BEQ	BFIRX
	STD	OX16,X
	LDD	XTEMP
	STD	OY16,X
	STY	OXV,X
	LDA	#1
	STA	OTYP,X	MAKE NON-HYPERABLE	 (IS THIS BIT OK???????)
	STX	OPTR	NOW PUT IT ON LIST
	LDA	XTEMP2	GET ACCELERATION
	STA	PD+2,U	AND PASS IT
BFIRX	LDA	FBFREQ	TIME TILL NEXT BALL ELIGABLE
	COMA
	LDU	CRPROC	GET BACK TO DADDY
	STA	.DELA,U	TIME UNTIL NEXT FIREBALL
	LDX	.OPTR,U	YANK BACK OBJECT POINTER FOR ENDGAME
	JMP	FBGOV	CYCLE AND NAP

FBIMAG	LDY	OPICT,X	GET CURRENT IMAGE
	LDA	OXV,X	GET VELOCITY
	BMI	FBIMG3	GOING LEFT
	LEAY	10,Y	MOVE TO NEXT IMAGE
	CMPY	#FBMR3
	BLS	FBIMG2
	LDY	#FBMR0
FBIMG2	STY	OPICT,X	STORE NEW IMAGE
	RTS
FBIMG3	LEAY	-10,Y	MOVE TO PREVIOUS
	CMPY	#FBMR0
	BHS	FBIMG4
	LDY	#FBMR3
FBIMG4	STY	OPICT,X
	RTS

FBSETY	LDA	OY16,X	GET OUR Y
	CMPA	PLAYC	COMPARE TO PLAYERS
	BHI	FBSET1	HE'S BELOW US
	LDB	FBDV	GET VELOCITY TO MAKE NEGATIVE
	COMB
	LDA	#$FF
	SUBD	#$10
	BRA	FBSET2
FBSET1	LDB	FBDV
	ADDB	#10
	CLRA
FBSET2	TST	INVFLG	INVISIBLE??
	BNE	FBSET3	YEP...ACT STUPIDER.
	ASLB	
	ROLA
FBSET3	STD	OYV,X
	RTS

FBXR	LDD	#32	1 PIXEL
	STD	OXV,X
	RTS

FBXL	LDD	#-32
	STD	OXV,X
	RTS

*	FBXV - FIRE BOMBER GET NEW X VELOCITY
*	X HAS OBJECT, AND B SHOULD BE RETURNED WITH A 4 BIT RANDOM NUMBER!

FBXV	LDB	LSEED	GET A RADNUB
	ASRB	
	ASRB		
	ASRB
	ASRB		1/2 PIXEL MAX.
	BMI	FBX1	NEGATIVE
	ADDB	#20
FBX1	SUBB	#10	MINIMUM 10 IN SOME DIRECTION
	SEX
	STD	OXV,X	MAKE THAT X VELOCITY
	RTS

*
*	FIREBALL
*

FBALL	LDD	#SSHSND
	JSR	SNDLD	MAKE A LAUNCH SOUND
	LDA	#$20	TIMER
	STA	PD+3,U
FBAL11	NAP	3,FBAL22
FBAL22	LDX	PD,U	GET THE OBJECT
	JSR	FBDIST	CHECK ITS ON SCREENESS
	BHI	FBAL4	GONE!
	DEC	PD+3,U	TIMER
	BEQ	FBAL4	KILL THE SUCKER
	LDA	SEED
	ANDA	#$7	8 POSSIBLE CHANGES
	LDB	#10
	MUL		BYTE OFFSET
	ADDD	OPICT,X	ADD TO CURRENT
	CMPD	#FBALL7	PAST TOP
	BLS	FBAL3	NOPE
	SUBD	#80	BRING IT BACK INTO THE TABLE
FBAL3	STD	OPICT,X	STORE NEW IMAGE
	LDB	PD+2,U	GET ACCEL
	SEX
	ADDD	OYV,X
	STD	OYV,X
	BRA	FBAL11
FBAL4	JSR	KILLOB	TRASH THE OBJECT
	LDD	OBJX,X	ON SCREEN
	BEQ	FBAL5	NOPE
	LDY	OPICT,X	FOR ROUTINE
	JSR	[OBJDEL,Y]
FBAL5	JMP	SUCIDE	AND DIE

FKSUB	JSR	SCORE	PROCESS THE SCORE
	TFR	Y,D
	JSR	SNDLD	AND THE SOUND
	PSHS	X
	JSR	KILLOP	CAN THE PROCESS
	PULS	X
	JSR	KILLOB	CAN THE OBJECT
	JSR	OFSHIT	AND ITS IMAGE
	JMP	GETOB	BUT GET IT OFF OF THE FREE LIST

*	FKILL - KILL A FIREBALL

FKILL	LDD	#$201	100 POINTS	
	LDY	#AHSND	SOUND
	BSR	FKSUB	CAN THE PROCESS ETC.
	LDD	#PHFBPC
	BRA	FBKIL1
*
*	KILL A FIREBOMBER
*

FBKILL	DEC	FBCNT	1 LESS FIRE BOMBER
	LDD	#$125	50 POINTS	
	LDY	#AHSND	SOUND
	BSR	FKSUB	CAN THE PROCESS ETC.
	LDD	#PHFBMB
FBKIL1	STD	OPICT,X	MAKE PHONEY PICTURE
	TFR	X,Y	MOVE THE PROCESS
	MAKP	BOOM	DO IT.
	STY	PD,X	AND PASS IT THE OBJECT
	RTS		AND RETURN

BOOM	LDA	#3	DO IT 4 TIMES
	STA	PD+2,U
BOOM1	LDX	PD,U	GET OBJECT
	JSR	XSVCT	DO AN EXPLODE
	DEC	PD+2,U
	BEQ	BOOM2
	NAP	5,BOOM1
BOOM2	LDD	OFREE	
	STD	,X	LINK FREE LIST TO US
	STX	OFREE	AND MAKE US THE FREE LIST
	DIE	DONE!

*
*	 
*	FBDIST -	SEE IF FIREBOMBER FAR OFF SCREEN...X POINTS AT OBJECT
*	BLS FOR NEAR THE SCREEN......BHI FOR OFF SCREEN
*

FBDIST	LDD	OX16,X	GET THE CURRENT OBJECT POINTER
	SUBD	BGL	COMPARE TO LEFT OF SCREEN
	ADDD	#75*32	ADD 50 PIXELS
	CMPD	#450*32	SEE IF 50 EITHER SIDE.
	RTS		

*
*YLLABIANS
*
YLPL0	FCB	5,7
	FDB	YLDL00,YLDL01,ON57,OFF57
YLPL1	FCB	5,7
	FDB	YLDL10,YLDL11,ON57,OFF57
YLPL2	FCB	5,7
	FDB	YLDL20,YLDL21,ON57,OFF57
YLPL3	FCB	5,7
	FDB	YLDL30,YLDL31,ON57,OFF57
YLPL4	FCB	5,7
	FDB	YLDL40,YLDL41,ON57,OFF57
YLPR0	FCB	5,7
	FDB	YLDR00,YLDR01,ON57,OFF57
YLPR1	FCB	5,7
	FDB	YLDR10,YLDR11,ON57,OFF57
YLPR2	FCB	5,7
	FDB	YLDR20,YLDR21,ON57,OFF57
YLPR3	FCB	5,7
	FDB	YLDR30,YLDR31,ON57,OFF57
YLPR4	FCB	5,7
	FDB	YLDR40,YLDR41,ON57,OFF57
YLDL00	FCB	$00,$0A,$06,$66,$00,$0F,$00
	FCB	$AA,$A2,$66,$62,$FF,$88,$FF
	FCB	$A0,$AA,$60,$66,$F0,$8F,$F0
	FCB	$00,$00,$00,$61,$00,$00,$00
	FCB	$00,$00,$00,$10,$00,$00,$00
YLDL01	FCB	$00,$00,$00,$06,$00,$00,$00
	FCB	$0A,$AA,$66,$66,$0F,$F8,$0F
	FCB	$AA,$2A,$66,$26,$FF,$88,$FF
	FCB	$00,$A0,$00,$66,$00,$F0,$00
	FCB	$00,$00,$00,$11,$00,$00,$00
YLDL10	FCB	$00,$00,$06,$66,$0F,$00,$00
	FCB	$00,$AA,$66,$FF,$88,$FF,$00
	FCB	$00,$A0,$6A,$F6,$8F,$F0,$00
	FCB	$00,$00,$00,$61,$00,$00,$00
	FCB	$00,$00,$00,$10,$00,$00,$00
YLDL11	FCB	$00,$00,$00,$06,$00,$00,$00
	FCB	$00,$0A,$66,$6F,$F8,$0F,$00
	FCB	$00,$AA,$66,$FF,$88,$FF,$00
	FCB	$00,$00,$A0,$66,$F0,$00,$00
	FCB	$00,$00,$00,$11,$00,$00,$00
YLDL20	FCB	$00,$00,$06,$6F,$00,$00,$00
	FCB	$00,$00,$FF,$88,$FF,$00,$00
	FCB	$00,$00,$F0,$8F,$F0,$00,$00
	FCB	$00,$00,$00,$61,$00,$00,$00
	FCB	$00,$00,$00,$10,$00,$00,$00
YLDL21	FCB	$00,$00,$00,$06,$00,$00,$00
	FCB	$00,$00,$6F,$F8,$0F,$00,$00
	FCB	$00,$00,$FF,$88,$FF,$00,$00
	FCB	$00,$00,$00,$F6,$00,$00,$00
	FCB	$00,$00,$00,$11,$00,$00,$00
YLDL30	FCB	$00,$00,$0F,$66,$0A,$00,$00
	FCB	$00,$FF,$88,$FF,$A2,$AA,$00
	FCB	$00,$F0,$8F,$F6,$AA,$A0,$00
	FCB	$00,$00,$00,$61,$00,$00,$00
	FCB	$00,$00,$00,$10,$00,$00,$00
YLDL31	FCB	$00,$00,$00,$06,$00,$00,$00
	FCB	$00,$0F,$8F,$6F,$AA,$0A,$00
	FCB	$00,$FF,$88,$FF,$2A,$AA,$00
	FCB	$00,$00,$F0,$66,$A0,$00,$00
	FCB	$00,$00,$00,$11,$00,$00,$00
YLDL40	FCB	$00,$0F,$06,$66,$00,$0A,$00
	FCB	$FF,$88,$FF,$62,$A2,$A2,$AA
	FCB	$F0,$8F,$F0,$66,$A0,$AA,$A0
	FCB	$00,$00,$00,$61,$00,$00,$00
	FCB	$00,$00,$00,$10,$00,$00,$00
YLDL41	FCB	$00,$00,$00,$06,$00,$00,$00
	FCB	$0F,$F8,$6F,$66,$0A,$AA,$0A
	FCB	$FF,$88,$FF,$26,$2A,$2A,$AA
	FCB	$00,$F0,$00,$66,$00,$A0,$00
	FCB	$00,$00,$00,$11,$00,$00,$00
YLDR00	FCB	$00,$00,$00,$11,$00,$00,$00
	FCB	$00,$0A,$00,$66,$00,$0F,$00
	FCB	$AA,$A2,$66,$62,$FF,$88,$FF
	FCB	$A0,$AA,$66,$66,$F0,$8F,$F0
	FCB	$00,$00,$00,$60,$00,$00,$00
YLDR01	FCB	$00,$00,$00,$01,$00,$00,$00
	FCB	$00,$00,$00,$16,$00,$00,$00
	FCB	$0A,$AA,$06,$66,$0F,$F8,$0F
	FCB	$AA,$2A,$66,$26,$FF,$88,$FF
	FCB	$00,$A0,$60,$66,$00,$F0,$00
YLDR10	FCB	$00,$00,$00,$11,$00,$00,$00
	FCB	$00,$00,$0A,$66,$0F,$00,$00
	FCB	$00,$AA,$66,$FF,$88,$FF,$00
	FCB	$00,$A0,$66,$F6,$8F,$F0,$00
	FCB	$00,$00,$00,$60,$00,$00,$00
YLDR11	FCB	$00,$00,$00,$01,$00,$00,$00
	FCB	$00,$00,$00,$16,$00,$00,$00
	FCB	$00,$0A,$A6,$6F,$F8,$0F,$00
	FCB	$00,$AA,$66,$FF,$88,$FF,$00
	FCB	$00,$00,$60,$66,$F0,$00,$00
YLDR20	FCB	$00,$00,$00,$11,$00,$00,$00
	FCB	$00,$00,$00,$6F,$00,$00,$00
	FCB	$00,$00,$FF,$88,$FF,$00,$00
	FCB	$00,$00,$F6,$8F,$F0,$00,$00
	FCB	$00,$00,$00,$60,$00,$00,$00
YLDR21	FCB	$00,$00,$00,$01,$00,$00,$00
	FCB	$00,$00,$00,$16,$00,$00,$00
	FCB	$00,$00,$0F,$F8,$0F,$00,$00
	FCB	$00,$00,$FF,$88,$FF,$00,$00
	FCB	$00,$00,$60,$F6,$00,$00,$00
YLDR30	FCB	$00,$00,$00,$11,$00,$00,$00
	FCB	$00,$00,$0F,$66,$0A,$00,$00
	FCB	$00,$FF,$88,$FF,$A2,$AA,$00
	FCB	$00,$F0,$8F,$F6,$AA,$A0,$00
	FCB	$00,$00,$00,$60,$00,$00,$00
YLDR31	FCB	$00,$00,$00,$01,$00,$00,$00
	FCB	$00,$00,$00,$16,$00,$00,$00
	FCB	$00,$0F,$F8,$6F,$AA,$0A,$00
	FCB	$00,$FF,$88,$FF,$2A,$AA,$00
	FCB	$00,$00,$F0,$66,$A0,$00,$00
YLDR40	FCB	$00,$00,$00,$11,$00,$00,$00
	FCB	$00,$0F,$00,$66,$00,$0A,$00
	FCB	$FF,$88,$FF,$62,$A2,$A2,$AA
	FCB	$F0,$8F,$F6,$66,$A0,$AA,$A0
	FCB	$00,$00,$00,$60,$00,$00,$00
YLDR41	FCB	$00,$00,$00,$01,$00,$00,$00
	FCB	$00,$00,$00,$16,$00,$00,$00
	FCB	$0F,$F8,$0F,$66,$0A,$AA,$0A
	FCB	$FF,$88,$FF,$26,$2A,$2A,$AA
	FCB	$00,$F0,$60,$66,$00,$A0,$00

*
*	FIREBOMBERS AND FIREBALLS
*

FBBLIP	EQU	$ACCA

FBMR0	FCB	5,9
	FDB	FBR10,FBR11
	FDB	ON59,OFF59

FBMR1	FCB	5,9
	FDB	FBR20,FBR21
	FDB	ON59,OFF59

FBMR2	FCB	5,9
	FDB	FBR30,FBR31
	FDB	ON59,OFF59

FBMR3	FCB	5,9
	FDB	FBR40,FBR41
	FDB	ON59,OFF59

PHFBMB	FCB	5,9	PHONEY FOR EXPLOSION
	FDB	PHBMB0,PHBMB0
	FDB	ON59,OFF59

FBR10	FCB	$00,$00,$00,$00,$00,$11,$11,$01,$00
	FCB	$00,$00,$00,$00,$00,$9A,$99,$11,$11
	FCB	$11,$91,$A9,$AA,$A0,$A0,$A0,$90,$10	
	FCB	$10,$11,$91,$91,$00,$00,$00,$00,$00
	FCB	$00,$00,$10,$10,$00,$00,$00,$00,$00

FBR11	FCB	$00,$00,$00,$00,$00,$01,$01,$00,$00
	FCB	$00,$00,$00,$00,$00,$19,$19,$11,$01
	FCB	$01,$09,$0A,$0A,$0A,$AA,$9A,$19,$11
	FCB	$11,$11,$99,$A9,$00,$00,$00,$00,$00
	FCB	$00,$10,$11,$11,$00,$00,$00,$00,$00

FBR20	FCB	$00,$00,$11,$11,$11,$11,$11,$01,$00
	FCB	$00,$00,$00,$90,$9A,$9A,$90,$00,$00
	FCB	$00,$00,$00,$0A,$AA,$00,$00,$00,$00	
	FCB	$00,$01,$91,$91,$91,$91,$01,$00,$00
	FCB	$00,$00,$10,$10,$10,$10,$10,$00,$00

FBR21	FCB	$00,$00,$01,$01,$01,$01,$01,$00,$00
	FCB	$00,$00,$10,$19,$19,$19,$19,$10,$00
	FCB	$00,$00,$00,$00,$AA,$A0,$00,$00,$00
	FCB	$00,$00,$09,$A9,$A9,$09,$00,$00,$00
	FCB	$00,$10,$11,$11,$11,$11,$11,$00,$00

FBR30	FCB	$00,$01,$11,$11,$19,$00,$00,$00,$00
	FCB	$11,$11,$99,$9A,$AA,$00,$00,$00,$00
	FCB	$00,$00,$00,$00,$AA,$0A,$09,$01,$01
	FCB	$00,$00,$00,$00,$A9,$91,$91,$11,$10
	FCB	$00,$00,$00,$00,$10,$10,$10,$00,$00

FBR31	FCB	$00,$00,$01,$01,$01,$00,$00,$00,$00
	FCB	$01,$11,$19,$19,$9A,$00,$00,$00,$00
	FCB	$10,$10,$90,$A0,$AA,$00,$00,$00,$00
	FCB	$00,$00,$00,$00,$AA,$A9,$99,$11,$11
	FCB	$00,$00,$00,$00,$91,$11,$11,$10,$00

FBR40	FCB	$00,$01,$00,$00,$00,$00,$00,$00,$00
	FCB	$11,$11,$99,$0A,$00,$00,$09,$11,$11
	FCB	$11,$11,$99,$A0,$A0,$AA,$99,$11,$11
	FCB	$10,$10,$00,$00,$00,$00,$90,$11,$10
	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00

FBR41	FCB	$00,$00,$00,$00,$00,$00,$00,$00,$00
	FCB	$01,$11,$09,$00,$00,$00,$00,$01,$01
	FCB	$11,$11,$99,$AA,$0A,$0A,$99,$11,$11
	FCB	$11,$11,$90,$00,$00,$A0,$99,$11,$11
	FCB	$00,$00,$00,$00,$00,$00,$00,$10,$00

PHBMB0	FCB	$00,$01,$11,$11,$19,$11,$11,$01,$00
	FCB	$11,$11,$99,$9A,$AA,$9A,$99,$11,$11
	FCB	$11,$91,$A9,$AA,$AA,$AA,$A9,$91,$11
	FCB	$10,$11,$91,$91,$A9,$91,$91,$11,$10
	FCB	$00,$00,$10,$10,$10,$10,$10,$00,$00

FBLIP	EQU	$4000	YELLOW

FBALL0	FCB	4,5
	FDB	BAL00,BAL01
	FDB	FBALON,OFF45

FBALL1	FCB	4,5
	FDB	BAL10,BAL11
	FDB	FBALON,OFF45

FBALL2	FCB	4,5
	FDB	BAL20,BAL21
	FDB	FBALON,OFF45

FBALL3	FCB	4,5
	FDB	BAL30,BAL31
	FDB	FBALON,OFF45

FBALL4	FCB	4,5
	FDB	BAL40,BAL41
	FDB	FBALON,OFF45

FBALL5	FCB	4,5
	FDB	BAL50,BAL51
	FDB	FBALON,OFF45

FBALL6	FCB	4,5
	FDB	BAL60,BAL61
	FDB	FBALON,OFF45

FBALL7	FCB	4,5
	FDB	BAL70,BAL71
	FDB	FBALON,OFF45

BAL00	FCB	$00,$02,$02,$20,$02
	FCB	$00,$40,$44,$04,$02
	FCB	$20,$20,$40,$22,$00
	FCB	$00,$00,$00,$00,$00
BAL01	FCB	$00,$00,$00,$02,$00
	FCB	$00,$24,$24,$00,$20
	FCB	$02,$02,$44,$42,$20
	FCB	$00,$00,$00,$20,$00
BAL10	FCB	$02,$20,$04,$02,$00
	FCB	$02,$20,$40,$24,$00
	FCB	$00,$02,$40,$00,$20
	FCB	$00,$00,$00,$00,$00
BAL11	FCB	$00,$02,$00,$00,$00
	FCB	$20,$02,$44,$22,$00
	FCB	$20,$00,$04,$40,$02
	FCB	$00,$20,$00,$00,$00
BAL20	FCB	$00,$02,$24,$02,$00
	FCB	$20,$02,$04,$40,$20
	FCB	$00,$20,$02,$20,$00
	FCB	$00,$00,$00,$00,$00
BAL21	FCB	$00,$00,$02,$00,$00
	FCB	$02,$20,$40,$24,$02
	FCB	$00,$22,$40,$02,$00
	FCB	$00,$00,$20,$00,$00
BAL30	FCB	$00,$02,$00,$20,$02
	FCB	$00,$40,$44,$02,$00
	FCB	$20,$22,$20,$02,$00
	FCB	$00,$00,$00,$00,$00
BAL31	FCB	$00,$00,$00,$02,$00
	FCB	$00,$24,$04,$00,$20
	FCB	$02,$02,$42,$20,$00
	FCB	$00,$20,$00,$20,$00
BAL40	FCB	$00,$20,$02,$00,$00
	FCB	$20,$44,$00,$44,$00
	FCB	$00,$00,$40,$00,$20
	FCB	$00,$00,$00,$00,$00
BAL41	FCB	$00,$02,$00,$00,$00
	FCB	$02,$04,$20,$04,$00
	FCB	$00,$40,$04,$40,$02
	FCB	$00,$00,$00,$00,$00
BAL50	FCB	$00,$02,$20,$02,$02
	FCB	$02,$24,$44,$40,$02
	FCB	$00,$20,$00,$20,$00
	FCB	$00,$00,$00,$00,$00
BAL51	FCB	$00,$00,$02,$00,$00
	FCB	$00,$22,$04,$24,$20
	FCB	$20,$42,$40,$02,$20
	FCB	$00,$00,$00,$00,$00
BAL60	FCB	$02,$02,$04,$20,$00
	FCB	$00,$00,$04,$24,$00
	FCB	$20,$20,$42,$22,$00
	FCB	$00,$00,$00,$00,$00
BAL61	FCB	$00,$00,$00,$02,$00
	FCB	$20,$20,$40,$02,$00
	FCB	$02,$02,$44,$42,$00
	FCB	$00,$00,$20,$20,$00
BAL70	FCB	$00,$20,$04,$02,$00
	FCB	$20,$44,$40,$42,$20
	FCB	$00,$02,$20,$00,$20
	FCB	$00,$00,$00,$00,$00
BAL71	FCB	$00,$02,$00,$00,$00
	FCB	$02,$04,$44,$24,$02
	FCB	$00,$40,$02,$20,$02
	FCB	$00,$20,$00,$00,$00

PHFBPC	FCB	5,8	PHONEY FIREBALL FOR EXPLOSION
	FDB	PFB0,PFB0
	FDB	ON58,OFF58

PFB0	FCB	$00,$02,$22,$22,$22,$22,$02,$00
	FCB	$22,$22,$22,$24,$24,$22,$22,$22
	FCB	$22,$22,$44,$44,$44,$44,$22,$22
	FCB	$22,$22,$22,$42,$42,$22,$22,$22
	FCB	$00,$20,$22,$22,$22,$22,$20,$00

	END
