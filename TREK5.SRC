	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E  >>>>>

YMAXX	EQU	$F6
YMINN	EQU	$0A
XMAX	EQU	$9C
XMIN	EQU	$0
NRINGS	EQU	7
BLOPT	EQU	$5080	PLACE TO EXPLODE
RADIUS	EQU	$140
RINDEL	EQU	$9	UPDATES UNTIL NEXT RING
DELTA	EQU	$14	STEP SIZE (IN Y DIRECTION)

	ORG	WRPRAM
TEMP1	RMB	1	TIME TILL NEXT COLOR CHANGE
TEMP6	RMB	1	VALUE TO RESTORE TEMP1 WITH TO ACCELERATE
TEMP2	RMB	1	USED TO MEASURE TIME TILL NEXT GUY OUT.
TEMP3	RMB	1	NUMBER OF RINGS TO DO.
TEMP4	RMB	2	USED BY RNG
TEMP5	RMB	2	USED BY ROCK GENERATOR
FROBPT	RMB	2	THIS POINTS AT CURRENT FROB
TABPTR	RMB	2	POINTER TO FIRST UNUSED ENTRY IN TABLE
SDDD	EQU	*
HSDDD	RMB	1
LSDDD	RMB	1
PCSAV	RMB	8	SAVE OLD PCRAM VALUES

PTABLE	EQU	*

.YACC	RMB	2
.XACC	RMB	2
.XV	RMB	2
.YV	RMB	2
.XPOS	RMB	2
.OLD	RMB	2
.YPOS	EQU	.OLD+1
	RMB	1	LOW HALF OF YPOS
.FROB	RMB	1

PSIZZ	EQU	*-PTABLE
XV	EQU	.XV-PTABLE
YV	EQU	.YV-PTABLE
XPOS	EQU	.XPOS-PTABLE
YPOS	EQU	.YPOS-PTABLE
FROB	EQU	.FROB-PTABLE
OLD	EQU	.OLD-PTABLE
YACC	EQU	.YACC-PTABLE
XACC	EQU	.XACC-PTABLE
	RMB	(PSIZZ*(300-1)) ROOM FOR 351 ENTRIES

	ORG	WRPORG

	JMP	ROCK0	VECTOR COMES IN AT ENTRY.

PIECES	FCB	$11
	FCB	$22
	FCB	$33
	FCB	$44
	FCB	$55
	FCB	$66
	FCB	$77
	FCB	$88
ENDPIC	EQU	*

PCTAB	FCB	$FF	WHITE
	FCB	$3F	PURPLE
	FCB	$F8	LIGHT BLUE
	FCB	$07	RED
	FCB	$FF	WHITE
	FCB	$38	GREEN
	FCB	$C7	YELLOW
	FCB	$C0	BLUE

ROKSUB	CLRB
	CLRA
ROKSB1	STD	TEMP5	SAVE INDEX
	STD	,X++	Y DONE FOR FIRST POINT
	BSR	RNDDIS	GET A RANDMOM DISPLACEMENT IN D
	STD	,X++
	BSR	STORSB	STORE THE REST OF THE CRUD
	LDD	TEMP5	GET IT BACK
	STD	,X++	USE THE SAME Y
	BSR	RNDDIS
	COMA
	COMB
	ADDD	#1
	STD	,X++
	BSR	STORSB
	LDD	TEMP5
	COMA
	COMB
	ADDD	#1
	STD	PSIZZ,X	STORE THE NEGATIVE SIDE FOR THE NEXT ONE
	STD	,X++
	BSR	RNDDIS
	STD	,X++
	BSR	STORSB
	LEAX	2,X	ALREADY STORED BUMB IT
	BSR	RNDDIS
	COMA
	COMB
	ADDD	#1
	STD	,X++
	BSR	STORSB
	LDD	TEMP5
	ADDD	#DELTA	MOVE UP TO NEXT
	CMPD	#RADIUS	ARE WE UP TO RADIUS??
	BLO	ROKSB1	NOPE...DO ANOTHER
	STX	TABPTR	THIS IS THE NEW POINTER VALUE
	RTS

RNDDIS	JSR	RANDM
	BPL	RNDDS1
	ORB	#$C0
	BRA	RNDDS2
RNDDS1	ANDB	#$3F
RNDDS2	SEX
	SUBD	TEMP5	THIS FORMS X.
	ADDD	#RADIUS
	ASRA	HALVE	FOR X
	RORB
	RTS

STORSB	CLRA
	CLRB
	STD	,X++	VELOCITY
	STD	,X++
	LDD	#BLOPT!.$FF00
	STD	,X++
	LDD	#BLOPT	OLD
	STD	,X++
	CLR	,X+
	LDA	[FROBPT]
	STA	,X+	PUT THE FROB DOWN
	RTS

ROCK0	LDU	CRPROC
	PULS	X	GET RETURN ADDRESS
	STX	PD+6,U
	LDX	#PCRAM+1
	LDY	#PCSAV
	LDU	#PCTAB
ROCK00	LDD	,X
	STD	,Y++
	LDD	,U++
	STD	,X++
	CMPX	#PCRAM+9
	BNE	ROCK00
	LDD	#RCKSND
	JSR	SNDLD
	LDD	SDDD	RANDMOMIZE IT
	ADDD	#$3456
	STD	SDDD
	LDD	#PIECES
	STD	FROBPT
	LDA	#NRINGS
	STA	TEMP3
	LDA	#RINDEL	NUMBER OF UPDATES UNTIL NEXT WAVE
	STA	TEMP2
	LDA	#$7	INITIAL COUNTDOWN FOR BACKGROUND COLOR CHANGE
	STA	TEMP6	HOLD FOR ACCELERATION
	LDA	#1	LET COLOR TIMER TIME OUT RIGHT AWAY
	STA	TEMP1
	LDX	#PTABLE	LETS WALK THROUGH AND INIT THE TABLE
	JSR	ROKSUB	ADD TEMP1 ROCKS

UPDATE	CLRA
	DEC	TEMP1	DECREMENT COLOR TIMER
	BNE	UPDAT9
	LDA	TEMP6	ACCELERATION RESTORE
	STA	TEMP1
	LDU	#PCTAB-2
	LDA	A,U	GET OFFSET CORRESPONDING TO TEMP6
UPDAT9	STA	PCRAM
	LDA	TEMP2
	DECA		REMOVE ONE
	STA	TEMP2
	BNE	UPDT1
	JSR	NWRING
	BRA	UPDTT2
UPDT1	NAP	2,UPDTT2 TAKE A NAP IF NO NEW RING
UPDTT2	LDY	#0
	LDX	#PTABLE	GET START OF TABLE
	CMPX	TABPTR	NOTHING IN TABLE??
	BNE	ROKUP2	SOMETHING THERE...CONTINUE
	CLR	PCRAM	RESTORE BACKGROUND COLOR
	LDY	#PCRAM+1 AND COLOR MATRIX
	LDX	#PCSAV
BYEBYE	LDD	,X++
	STD	,Y++
	CMPY	#PCRAM+9
	BNE	BYEBYE
	LDU	CRPROC	GET PROCESS IN CASE OF BASHING
	JMP	[PD+6,U] RETURN

ROKUP2	CMPX	TABPTR	AT END???
	BEQ	UPDATE
	STY	[OLD,X]	ERASES OLD ONE
	LDD	YACC,X
	ADDD	YV,X
	STD	YV,X
	ADDD	YPOS,X
	CMPA	#YMAXX
	BCC	BYEPT2
	CMPA	#YMINN
	BCS	BYEPT2
	STD	YPOS,X
	LDD	XACC,X
	ADDD	XV,X
	STD	XV,X
	ADDD	XPOS,X
	CMPA	#XMAX
	BCC	BYEPT2
	STD	XPOS,X
	STA	OLD,X
	LDA	FROB,X
	LDB	FROB,X
	STD	[OLD,X] STORE FROB
	LEAX	PSIZZ,X MOVE TO NEXT
	BRA	ROKUP2

BYEPT2	LDU	TABPTR	GET THE POINTER
	LEAU	-PSIZZ,U MOVE DOWN ONE ENTRY
	STU	TABPTR
	LDD	YACC,U
	STD	YACC,X
	LDD	XACC,U
	STD	XACC,X
	LDD	XV,U
	STD	XV,X
	LDD	YV,U
	STD	YV,X
	LDD	XPOS,U
	STD	XPOS,X
	LDD	OLD,U
	STD	OLD,X
	LDD	OLD+2,U
	STD	OLD+2,X
	BRA	ROKUP2

NWRING	LDA	TEMP6
	DECA
	CMPA	#5
	BLO	NWRNG9
	STA	TEMP6
NWRNG9	LDA	#RINDEL
	STA	TEMP2
	TST	TEMP3
	BEQ	NWRTS
	DEC	TEMP3
	LDD	FROBPT
	ADDD	#1
	CMPD	#ENDPIC	END??
	BNE	NWRNG1
	LDD	#PIECES
NWRNG1	STD	FROBPT
	LDX	TABPTR
	JMP	ROKSUB	ADD THE NEW WAVE AND RETURN

RCKSND	FCB	$FF,$20,2,$F2,$60,$01,$F2,0

RANDM	LDD	SDDD
	RORA
	RORB	B	NOW HAS HIGH HALF OF NEW SDDD
	LDA	HSDDD	FOR LOW SEVEN BITS
	ASLA
	EORA	HSDDD	BIT 6-0 IS NOW LOW SEVEN BITS OF NEW SDDD
	STB	HSDDD	NOW STORE OFF B
	ASLA		BIT 7-1 NOW HAVE LOW SEVEN BITS OF NEW SDDD
	LDB	LSDDD	GET THE EITH BIT FROM THE OLD LOW BIT
	RORB
	RORA
	STA	LSDDD
	LDB	LSDDD	RETURN IN B
NWRTS	RTS

	END
