	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<< S T A R G A T E  >>>>>
	ORG	SWTAB
*SWTAB
	FDB	LFIRE	FIRE
	FCB	LTYPE,$E8
	FDB	0	THRUST
	FCB	0,0
	FDB	SBOMB	SMART BOMB
	FCB	SBTYPE,$E8
	FDB	HYPER
	FCB	STYPE,$E8
	FDB	ST2
	FCB	STYPE,0
	FDB	ST1
	FCB	STYPE,0
	FDB	REV
	FCB	STYPE,$E8
	FDB	0
	FCB	0,0
*SWTAB1
	FDB	0
	FCB	0,0
	FDB	ADVSW
	FCB	STYPE,0
	FDB	RCOIN
	FCB	CTYPE,0
	FDB	HSBUTV	HI SCORE RESET
	FCB	ATYPE,0
	FDB	LCOIN
	FCB	CTYPE,0
	FDB	CCOIN
	FCB	CTYPE,0
*ARBITRARILY MISC. VECTORS
	JMP	PLENDD
	JMP	LDISP
	JMP	SBDISP
	JMP	GETWV
	JMP	TDISP
	JMP	RPHKK
	JMP	INVST
	JMP	PLEND
	JMP	WVCHK
	JMP	ENDWAV
	JMP	INIT$$
	FDB	RIPTAB-$7F
	JMP	PLINIT
	JMP	ONCOL
	JMP	GETWV0
	JMP	PTRANS
	JMP	INIT20
	JMP	DIM
	JMP	BRT
	JMP	SGSTRT
	FCC	' COPYRIGHT (C) 1981 WILLIAMS ELECTRONICS INC.'
	FCC	' ALL RIGHTS RESERVED '
*
* REPLAY HOOK
*
RPHKK	INC	PLAS,X
	INC	PSBC,X
	PSHS	X
	LDX	#GA1+12
	JSR	RCMOSA
	JSR	BCDHEX	DEHUMANIZE>>>>
	PULS	X
	ADDA	PINV,X
	BCS	RPHK1
	STA	PINV,X
RPHK1	JMP	IVDISP	DISPLAY INVISO
*
*INITIIALIZE
*
*PIA INIT TAB
PIATAB	FDB	$0034,$FF35,$0034,$003E
	FDB	PIA0,PIA1,PIA2,PIA3
*
INIT$$	ORCC	#$FF	NO INTERRUPTS YET PLEASE
	LDS	#HSTK	ONLY SETUP HARDWARE STACK
	LDA	#RAM!>8	SETUP DIRECT PAGE
	TFR	A,DP
	LDA	#1
	STA	RWCNTL	LETS RUN OUT OF ROMS TODAY.
* PIAS INITIALIZED HERE.
	LDX	#PIATAB
INIT1	CLRA
	CLRB
	STD	[8,X]	CLEAR CR, SELECT DDR
	LDD	,X++
	STD	[6,X]	SET DDR,CR
	CMPX	#PIATAB+8
	BNE	INIT1
	JSR	SCRCLR	CLEAR THE SCREEN
	LDA	#$3F
	STA	SOUND	GET READY TO SEND A WHETEVER TO THE SOUND
*CLEAR RAM STORAGE
	LDX	#$9C00
INIT10	CLR	,X+
	LDB	#WDATA
	STB	WDOG
	CMPX	#HSTK+1
	BNE	INIT10
	JSR	CMINIT	INIT CMOS
	LDX	#CREDST	GET CREDITS IN CMOS
	JSR	RCMOSB
	TFR	B,A
	CMPA	#$20	TOO HI??
	BHI	INIT11
	ANDA	#$F
	CMPA	#9	NON BCD LSD???
	BLS	INIT12
INIT11	CLRB
INIT12	STB	CREDIT	INTO RAM
	LDD	#$A55A
	STD	HSEED
	LDD	#$FF70
	STD	XXX1
	CLR	XXX3
	LDB	#$FF
	STD	PIA01	COINS STUCK
	LDA	#$55
	STA	SCNCOL
	JSR	P1SW	UPRIGHT SCREEN
	JSR	PINIT	INIT PROCESSES
	BSR	INIT20	MISC ROUTINES
	JSR	PRINV	INIT TEXT
	NEWP	ATTR,ATYPE
	LDA	#$C	BACKGROUND SOUND OFF TO SILENCE
	STA	SOUND
	COM	STATUS	FF---STATUS
	ANDCC	#$00	ENABLE IRQ,FIRQ
	JMP	EXEC
*
*MISC INIT ROUTINES
*
INIT20	JSR	CRINIT
	JSR	FISS	INIT LASR EXHAUST TABLE
	JSR	STINIT	INIT STARS
	JSR	OINIT	INIT OBJECT LIST
	JSR	SCLEAN	INITIALIZE SAM EXPLOSION/APPEAR DATA BASE
	JMP	THINIT	INIT THRUST
*
*ONE PLAYER START
*
ST1	LDA	STATUS
	BPL	ST2X
	LDA	FREEPL+1  CHECK FREEPLAY
	ANDA	#$F
	BEQ	ST1A
	LDA	#1
	STA	CREDIT
ST1A	LDA	CREDIT
	BEQ	ST2X
	LDX	#ST1SND
	LDA	#1	# TO START
	BRA	ST2B
*
*TWO PLAYER START
*
ST2	LDA	STATUS
	BPL	ST2X
	LDA	FREEPL+1
	ANDA	#$F
	BEQ	ST2A
	LDA	#2
	STA	CREDIT
ST2A	LDA	CREDIT
	CMPA	#2	NO CREDY NO GAMEY POOH
	BLO	ST2X
	LDX	#ST2SND
	LDA	#2
ST2B	BSR	START
ST2X	JMP	SUCIDE
*
*START GAME
*X=SOUND,A =# TO START
START	LDU	#CHKS1-$4E
	STA	PLRCNT
	TFR	X,D
	JSR	SNDLD
	CLR	CUNITS
	CLR	BUNITS
	JSR	GNCIDE	KILL ALL
	JSR	SCRCLR	CLEAN SCREEN
	LDY	#0	CALCULATE HIDDEN CHECKSUM
	CLRA
STR$$1	ADDA	,Y
	LEAY	$10,Y
	CMPY	#$9000
	BLO	STR$$1
	STA	$4E,U
	LDA	#$7F
	STA	STATUS
	LDA	#1
	STA	CURPLR
	STA	PDFLG	DISPLAY PROMPT
	LDX	#PLDATA
START1	CLR	,X+
	CMPX	#PLDEND
	BNE	START1
	LDA	#10
	STA	.P1TRG
	LDA	PLRCNT
	LDB	#8	TOTAL PLAYS IS AUDIT 8
	JSR	AUD
	PSHS	X
	LDX	#N2SHIP	SEE IF 2 CREDIT GAME ALLOWED??
	JSR	RCMOSB
	PULS	X
	TSTB
	BEQ	STR3$$	NOT ALLOWED
	ASLA	2	CREDIT GAME??
	LDU	#N2SHIP
	CMPA	CREDIT
	BLS	START2
	LSRA
STR3$$	LDU	#NSHIP
START2	TFR	A,B	COUNT IN B
	LDA	CREDIT
STR2$	ADDA	#$99
	DAA
	DECB
	BNE	STR2$
	STA	CREDIT
	LDX	#CREDST	BACKUP CREDITS
	JSR	WCMOSA
	TFR	U,X	GET SHIP #
	JSR	RCMOSA
	JSR	BCDHEX	CONVERT TO HUMAN
	STA	.P1LAS
	STA	.P1SBC
	LDX	#GA1+12
	TFR	A,B	SAVE LASER #
	JSR	RCMOSA	INITIAL INVISO TIME
	JSR	BCDHEX
	MUL
	STB	.P1INV
	LDX	#PLDATA
	JSR	GETWV
	LDX	#REPLAY
	JSR	RCMOSB
	CLRA
	ASLB
	ROLA
	ASLB
	ROLA
	ASLB
	ROLA
	ASLB
	ROLA
	STD	REPLA
	STD	.P1RP+1
	LDX	#PLDATA
STTRAN	LDA	,X+	COPY INTO SECOND PLAYER
	STA	PLDLEN-1,X
	CMPX	#.P1END
	BNE	STTRAN
	NEWP	PLSTRT,STYPE START PLAYER GOING
DRTS	EQU	*
STARTX	RTS
*
*PLAYER START PROCESS
*
PLSTRT	LDB	#7	HIT SHIP COUNTER
	JSR	AUD1
PLSTR0	JSR	SCLR1
	JSR	INIT20	RE:INIT THE BOYS
	JSR	GNCIDE
	LDA	#$7F
	STA	STATUS
	LDX	CRPROC
	CMPX	ACTIVE
	BNE	PLSTR1
	LDX	,X
	BEQ	PLSTR2
PLSTR1	NAP	15,PLST1A WAIT FOR COINS TO FINISH
PLST1A	LDA	LCCNT
	ORA	RCCNT
	ORA	LCCNT
	BNE	PLSTR1
PLSTR2	JSR	PINIT	REINIT
	NEWP	PLSTR3,STYPE
	JMP	EXEC
PLSTR3	LDA	PIA3
	BMI	PLSR2	COCKTAIL
	JSR	P1SW	ASSURE IRQ VECTOR RESTORED
	BRA	PLSTR5	(NO COCKTAIL)
PLSR2	JSR	SCRCLR
	LDA	CURPLR	PLAYER?
	DECA
	BNE	PLST3A
	JSR	P1SW	PLAYER 1
	BRA	PLSTR4
PLST3A	JSR	P2SW
PLSTR4	LDA	#$FF	STICK SWITCHES
	STA	PIA21
	STA	PIA22
PLSTR5	CLRA
	CLRB
	STD	BGL
	STD	BGLX
	JSR	BGINIT
	JSR	PLINIT	INIT THE PLAYER
	LDX	#TLIST
	STX	TPTR
	JSR	PLINDX	REDUCE LASER COUNT
	STX	PLRX	INDEX TO PLAYER STORE AREA
	LDA	PWAV,X
	ANDA	#$F
	LDU	#WCTAB
	LDA	A,U
	STA	SCNCOL	CURRENT BORDER COLOR
	DEC	PLAS,X
	JSR	TDISP
	LDD	.P1SCR
	BNE	PL00	NOT START OF GAME
	LDD	.P1SCR+2
	BNE	PL00
	CLR	MESSSP
	LDA	#120
	STA	MESSTM
	LDX	#MESSUL+$200 WILLY CP MESSAGE
	LDA	#CP2M
	JSR	WRD5FV
PL00	MAKP	SCPROC
	JSR	ONCOL	GET COLORING PROCS
	LDA	PDFLG	NO CHANGE IN PLAYER
	BEQ	PLS01
	LDB	PLRCNT	2 PLAYER GAME?
	DECB
	BEQ	PLS01	NO
PL01$	LDB	CURPLR	OUTPUT YOUR PLAYER UP MESSAGE
	LDA	#PLYRM
	LDX	#$3F80
	BSR	CHSUB	CHALLENGE??
	BCC	PL03$
	LDX	#$3F4A
PL03$	JSR	WRD7FV
	BSR	CHSUB
	BCS	PLS01	NO SLEEPY IF MORE TEXT COMING
	LDA	#$80
	LDX	#PLS01
	JMP	SLEEP
PLS01	BSR	CHSUB	IS IT CHALLENGE TIME????
	BCC	PLS5$
	JSR	PRCHM	JUMP TO THE ARCHAIC ROUTINE TO DO IT.
	NAP	$80,PLS5$
PLS5$	JSR	SCLR1
	LDB	#$00
	LDX	PLRX
	LDA	PTARG,X
	JSR	STCHKA
	JSR	ASTRES	GET ASTROS GOING
	LDX	#$20FB	'HIDDEN MESSAGE'
	LDY	#HIDTAB
HIDLP	LDA	,Y+
	EORA	#$5A
	BEQ	PLS00
	JSR	PR35V
	BRA	HIDLP
PLS00	CLR	OVCNT
	CLR	TIMER
	LDA	#16
	STA	STRCNT
	LDA	#$60
	LDX	#PLS1
	JMP	SLEEP
PLS1	JSR	PLRES
	CLR	OVCNT
	CLR	TIMER
	LDA	#16
	STA	STRCNT
	CLR	PDFLG
	INC	SCRFLG
	JMP	GEXEC	GAME HANDLER
*
* CHECK IF CURRENT START IS BEGINNING OF CHALLANGE WAVE.
*
CHSUB	PSHS	X,A
	LDX	PLRX	GET THE CURRENT PLAYER INDEX
	LDA	PWAV,X
	JSR	HEXBCD
	ANDA	#$F
	BEQ	CHYES1
	CMPA	#$5
	BEQ	CHYES2
CHNO	ANDCC	#$FE	CLC
	PULS	A,X,PC
CHYES1	JSR	LNKCHK	SEE HOW MANY ENEMIES OUT
	CMPA	#19	19 OBJECTS IN WAVE 10
	BNE	CHNO	NOT GOOD
	BRA	CHYES3
CHYES2	JSR	LNKCHK
	CMPA	#36	22-Y, 8-S, 6-D
	BNE	CHNO
CHYES3	ORCC	#$01	SEC
	PULS	A,X,PC
*
LNKCHK	LDX	PLRX	GET THE CURRENT PLAYER
	LDA	PENEMY+PRBCNT-ELIST,X
	ADDA	PENEMY+SWCNT-ELIST,X
	ADDA	PENEMY+YLCNT-ELIST,X
	ADDA	PENEMY+DYNCNT-ELIST,X
	ADDA	PENEMY+FBCNT-ELIST,X
	RTS
*
* PRINT CHALLENGE ROUND MESSAGE
*
PRCHM	PSHS	X,Y,D
	LDY	PLRX	GET THE PLAYER
	LDA	PWAV,Y
	JSR	HEXBCD
	TFR	A,B	PASS IN B.
	ANDA	#$0F
	BEQ	PRCHM1
	LDA	#CHYLM
	LDX	PLRX
	CLR	PTARG,X	CLEAR OUT PLANET FOR A DOGFIGHT
	BRA	PRCHM2
PRCHM1	LDA	#CHFBM
PRCHM2	JSR	WRD7V
	PULS	X,Y,D,PC
*
*'HIDDEN' MESAGE TABLE
*
HIDTAB	FCB	$5B!X$5A,'C!X$5A,$5C!X$5A
	FCB	' !X$5A,'1!X$5A,'9!X$5A,'8!X$5A,'1!X$5A,' !X$5A
	FCB	'W!X$5A,'I!X$5A,'L!X$5A,'L!X$5A
	FCB	'I!X$5A,'A!X$5A,'M!X$5A,'S!X$5A,' !X$5A
	FCB	'E!X$5A,'L!X$5A,'E!X$5A,'C!X$5A,$3D!X$5A
	FCB	' !X$5A,'I!X$5A,'N!X$5A,'C!X$5A,$3D!X$5A,0!X$5A
*
*COLORING PROCESS START
*
ONCOL	MAKP	COLR
	MAKP	THPROC
	MAKP	CBOMB
	MAKP	TIECOL
	MAKP	CTERR	MAKE A NORMAL PROCESS TERRAIN COLORING
	RTS
*
*PLAYER INITILAIZATION
*
PLINIT	EQU	*
	LDD	#$0300
	STD	PLADIR	THRUST+DIRECTION
	STD	NPLAD
	CLR	SGDFLG
	CLR	THFLG
	CLR	LFLG
	CLR	SCRFLG	NO SCORE
	CLR	REVFLG
	CLR	SBFLG
	CLR	BMBCNT
	CLR	MANCTR
	CLR	SWEXC
	CLR	RIPOFF
	LDD	#$2080	INIT COORD
	STD	NPLAXC
	STD	PLAXC
	LDD	#$2000
	STD	PLAX16
	LDD	#$2000!>2
	ADDD	BGL
	STD	PLABX
	LDD	#$8000
	STD	PLAY16
	CLRD
	STD	PLAXV
	STA	PLAXV+2
	STD	PLAYV
	STD	INVFLG
	RTS
*
*CHECK IF PLAYER REALLY DEAD
*
PLENDD	LDA	INVFLG	INVISIBLE?
	BNE	PLDDX
	MAKP	PLEND
	LDA	STATUS
	ORA	#8
	STA	STATUS
PLDDX	RTS
OUTSND	FCB	$EC,$01,$10,$08,0
*
* CALLED ON SCAN OF INVISO BUTTON
*
INVST	LDA	STATUS	STATUS CHECK
	BITA	#$E8
	BNE	PLDDX
	LDA	INVFLG
	BNE	PLDDX	ALREADY GONE
	CLRA
	LDX	#INVISO
	JMP	MKPROC	MAKE THE PROCESS AND DROP DEAD (OR RETURN ANYWAYS)
*
*TURN INVISIBLE
*
INVISO	LDX	PLRX
	LDA	PINV,X	TIME
	BNE	INVIS0
	LDA	OSNDD	ALREADY MADE
	BNE	INVXX	STALL IN PROGRESS
	INC	OSNDD
	LDD	#OUTSND
	JSR	SNDLD
	JMP	SUCIDE
INVIS0	STA	INVFLG
	LDA	STATUS	OFF PLAYER
	ORA	#$10
	STA	STATUS
	ORCC	#$10	SEI MAKE SURE WE ERASE HIM
	JSR	THOFF	OFF PLAYER AND THRUST
	JSR	THOFF1
	JSR	POFF
	ANDCC	#$EF	CLI
*BLOW UP THE PLAYER
	LDX	OFREE
	BEQ	INVL	NOTHING LEFT
	LDD	#PLAPIC
	TST	PLADIR
	BPL	INVIS1
	LDD	#PLBPIC
INVIS1	STD	OPICT,X
	LDD	PLABX
	STD	OX16,X
	LDD	PLAY16
	STD	OY16,X
	LDD	PLAXC
	ADDD	#$0403
	STD	CENTMP
	JSR	XSVCT
INVL	JSR	IVDISP	DISPLAY BAR GRAPH
	LDX	PLRX
	DEC	PINV,X
	BEQ	INVX	OUT
	LDA	PINV,X
	CMPA	#10	LESS THAN 1 SEC.
	BHI	INV2	FLASH REDD
	LDA	#$7
	STA	PCRAM
INV2	NAP	3,INV4
INV4	CLR	PCRAM
	LDA	PIA31	SWITCH GONE?
	BITA	#$2
	BEQ	INVX	YES
	NAP	3,INVL
INVX	CLR	INVFLG
	LDA	STATUS
	ANDA	#$EF
	STA	STATUS
	JSR	IVDISP	SHOW YOUR UNITS
INVXX	JMP	SUCIDE
*
*PLAYER END
*
PLEND	LDA	#DTYPE	DEATH TYPE
	LDU	CRPROC
	STA	PTYPE,U
	LDA	STATUS
	BPL	PLEND1
	LDA	#$D8
	STA	STATUS
	BRA	PLEND2
PLEND1	LDB	#$58
	JSR	STCHK0
PLEND2	LDD	BGL
	STD	BGLX	NOMO SCROLL
	LDX	PLAXC
	LDD	#$0806
	JSR	BLKCLR	OFF PLAYER
	JSR	PLSAV
	LDD	#PDSND
	JSR	SNDLD
*
*PLAYER DEATH
*
PDEATH	LDY	#PLAPIC
	LDA	NPLAD
	BPL	PDTH1
	LDY	#PLBPIC
PDTH1	LDX	#PXCTB	INIT COLOR
	STX	PD,U
	LDU	#MONOTB
	JSR	MONO
	TFR	U,X	PSEUDO VECTOR
	LDU	CRPROC
	STX	PD+4,U
PDTHL	LDD	NPLAXC
	LDY	PD+4,U
	JSR	COFF	BLANK
	LDA	#2
	LDX	#PDTH2
	JMP	SLEEP
PDTH2	LDD	NPLAXC
	LDY	PD+4,U
	JSR	CWRIT
	LDX	PD,U
	LDA	,X+
	BEQ	PDTH4
	STA	PCRAM+$B
	CLR	PCRAM	BLACK SCREEN
	STX	PD,U
PDTH3	LDA	#2
	LDX	#PDTHL
	JMP	SLEEP
PDTH4	TST	STATUS	ATTR??
	BPL	PDTH4A
	LDA	#$DA	DIE ATTR  GUYS
	STA	STATUS
	JMP	SUCIDE
PDTH4A	LDA	#$7B
	STA	STATUS
	LDA	#$FF
	STA	PCRAM
	LDA	#2
	LDX	#PDTH5
	JMP	SLEEP
PDTH5	CLR	PCRAM
	JSR	GNCIDE
	LDA	SAMEXC	ARE THE EXPLOSIONS LOCKED OUT??
	BEQ	PDTHH6	NOPE...CONTINUE
	JSR	SCLEAN	THEN INITILIZE THAT DATABASE TO BLOW!
PDTHH6	LDX	NPLAXC
	TST	OFREE
	BEQ	PDTHH7	NO OBJS LEFT
	JSR	PXVCT
PDTHH7	CLR	SNDTMR	OFF SOUND
	LDB	#$13
	JSR	SNDOUT
	JSR	WVCHK
	BNE	PLE01
	JSR	BONEME
	JSR	SCLR1
PLE01	LDA	CURPLR	SHIPS LEFT?
	LDX	PLRX
	LDB	PLAS,X
	BNE	PLE02	YES
	LDB	PLRCNT	1 PLAYER?
	DECB
	BEQ	PLE2	YES, GAME OVER
	EORA	#$3	OTHER GUY HAS SHIPS?
	JSR	PLDX
	LDB	PLAS,X
	BEQ	PLE2	NO,GAME OVER
	LDB	CURPLR
	LDA	#PLYRM
	LDX	#$3F78
	JSR	WRD7FV
	LDA	#GOM
	LDX	#$3D88
	JSR	WRD7FV
	NAP	$60,PLE02
PLE02	LDA	CURPLR
PLELP	INCA
	CMPA	PLRCNT
	BLS	PLE1
	LDA	#1
PLE1	JSR	PLDX
	LDB	PLAS,X
	BEQ	PLELP
	STA	CURPLR
	INC	PDFLG
	JMP	PLSTRT
PLE2	LDA	#$FF
	STA	STATUS
	LDA	#GOM
	LDX	#$3D80
	JSR	WRD7FV
	CLR	SNDTMR	OFF SOUNDS
	LDB	#$13
	JSR	SNDOUT
	NAP	$60,PLE3
PLE3	JMP	ENDPRC
*PLAYER GLOW TABLE
PXCTB	FCB	7,$F,$3F,$7F,$FF,$17,0
*WALL COLOR TABLE
WCTAB	FCB	$00,$BB,$33,$22,$11,$44,$66,$77
	FCB	$00,$99,$FF,$88,$11,$77,$99,$44
*
*SAVE PLAYER STATE
*
PLSAV	PSHS	D,X,U
	LDU	PLRX
	LEAU	PTARG,U
	LDA	#PLDLEN-PTARG
PLSAV1	CLR	,U+	CLEAR AREA
	DECA
	BNE	PLSAV1
*SAVE	ASTROS
	LDU	PLRX
	LDA	ASTCNT
	STA	PTARG,U
*SAVE ENEMY COUNTS
	LEAU	PENEMY,U
	LDX	#ELIST
PLSAV6	LDA	,X+
	CMPX	#ELIST1
	BHI	PLSAV7
	ADDA	ELEND-ELIST-1,X RES=ACT+RES
PLSAV7	STA	,U+
	CMPX	#ELEND
	BNE	PLSAV6
	PULS	D,X,U,PC
*
*RESTORE YOUR ASTRONAUTS
*
ASTRES	LDX	ASTPT	GET FROM FIXED INDIRECT (ADDRESS OF ASTRO ROUTINE)
	LDA	#STYPE
	JSR	MKPROC
	LDU	#TLIST
	LEAY	,U
	STU	PD,X	INIT PTR
ASTS1	CLR	,U+	CLEAR LIST
	CMPU	#TLEND
	BNE	ASTS1
	LDU	PLRX
	LDA	PTARG,U
	STA	ASTCNT
	BEQ	ASTSX
	CMPA	#7
	BLS	ASTS1B
	LSRA
	LSRA
	CLRB
ASTS1A	JSR	ASTST	QUADRANT AT AT TIME
	ADDB	#$40
	BNE	ASTS1A
	ASLA
	ASLA
	NEGA
	ADDA	PTARG,U	GET LEFTOVERS
	BEQ	ASTSX
ASTS1B	STA	XTEMP
ASTS1C	LDB	HSEED
	LDA	#1
	JSR	ASTST
	DEC	XTEMP
	BNE	ASTS1C
ASTSX	RTS
*
*TRANSFER PARAMETERS PENEMY-ELIST
*X=PLAYER INDEX
PTRANS	PSHS	X,U
	LDU	#ELIST2
	LEAX	PENEMY+ELIST2-ELIST,X
PTRLP	LDA	,X+
	STA	,U+
	CMPU	#ELEND
	BNE	PTRLP
	PULS	X,U,PC
*
*PLAYER RESTORE
*GET ENEMY COUNTS+PARAMS
*
*ENEMY COUNTS
PLRES	LDU	PLRX
	LEAU	PENEMY,U
	LDX	#ELIST
ELS1	LDA	,U+
	STA	,X+
	CMPX	#ELEND
	BNE	ELS1
*CLEAR ACTIVE COUNTS
	LDX	#ECNTS
ELS2	CLR	,X+
	CMPX	#ECEND
	BNE	ELS2
*GET ZERO WAVE
	LDX	PLRX
	LDA	PWAV,X
	DECA
	BNE	RSW
	JSR	GETWV0
	JSR	PTRANS
*RESTORE YOUR SWARMERS
RSW	LDA	SWCNT
	CLR	SWCNT
	BRA	RSW2
RSW0	JSR	GETOB	GET PHONY OBJECT
	LDA	SEED
	LSRA
	ADDA	#YMIN
	STA	OY16,X
	JSR	RAND
	ANDA	#$3F
	ADDA	#$80
	ADDD	BGL
	STD	OX16,X
	LDA	XTEMP2
	CMPA	#6
	BLS	RSW1
	LDA	#6
RSW1	LEAY	,X
	JSR	MMSW
	LDX	OFREE
	STX	,Y	KILL THE PHONY
	STY	OFREE
	NEGA
	ADDA	XTEMP2
RSW2	STA	XTEMP2
	BNE	RSW0
*RESTORE SCHITZOS
RSWX	LDA	SCZCNT
	BEQ	RSCZX
	CLR	SCZCNT
	JSR	SCZST
*RESTORE PROBES
RSCZX	LDA	PRBCNT
	BEQ	TIER
	JSR	PRBST
*RESTORE TIES
TIER	LDA	TIECNT
	BRA	TIER2
TIER0	CMPA	#3
	BLS	TIER1
	LDA	#3
TIER1	PSHS	A
	JSR	TIEST
	LDA	XTEMP2
	SUBA	,S+
TIER2	STA	XTEMP2
	BNE	TIER0
TIERX	LDA	SGCNT
	BEQ	SGSRX
	JSR	SGSTRT	START YOUR STAR GATES
SGSRX	LDA	YLCNT	START YLLABS
	BEQ	YLRX
	CLR	YLCNT
	JSR	YLST
*
*      TIME FOR SOME PRETTY FORMATTED CODE
* 
YLRX	LDA	FBCNT	CHECK OUT FIREBOMBERS
	BEQ	FBBYE	NONE...KAY BYE
	JSR	FBSTRT	START EM UP.
FBBYE	LDA	DYNCNT	ANY DYNAMOS???
	BEQ	DYNBYE
	JSR	DYNSTV	YEP...START EM
DYNBYE	CLR	TIMER	NO ENEMY HYPERING
	RTS
*
*GAME EXEC
*CHECKS END OF WAVE
*STAGES ATTACKS
*DISPATCHES SPOILERS
*PD=INTRAWALL CNTR,PD2=BOZ0 FLAG
GEXEC	LDU	CRPROC
	LDA	#40	DELTA COUNTER
	STA	PD,U
	LDA	UFOTIM
	STA	UFOTMR
	LDA	#1
	STA	WAVTMR
	CLR	PD2,U	CLEAR BOZO FLAG
GEX0	LDA	PIA31	SCAN INVISO SWITCH
	BITA	#$2
	BNE	GEX00$
	CLR	OSNDD	INDICATE SWITCH OPEN.
GEX00$	LDA	STATUS
	BITA	#8
	LBNE	GEX3	PLAYER DEAD
	JSR	WVCHK
	BNE	GEX00
GEX888	LDY	AFALLP	WAIT FOR FALLING DUDE
	JSR	PCHEK
	BNE	GEX999
	NAP	15,GEX888
ENDWAV	EQU	*
GEX999	LDA	#$77
	STA	STATUS
	JSR	GNCIDE
	JSR	PLSAV
	NAP	1,GEXBON LET THE SAM ROUTINE GET IN TO CLEAN UP THE WORD GARBAGE BUG. 
GEXBON	LDY	#CHKS1-$62
	MAKP	COLR
	MAKP	CBOMB
	MAKP	TIECOL
	LDA	#$37	HIDDEN CHECKSUM
	CMPA	$62,Y
	BEQ	GEXBN1
	LDA	SEED	LOOK AT SEED
	CMPA	#$30	1 IN 5 BONUSES
	BHS	GEXBN1
	LDB	HSEED
	LDA	#$9C
	TFR	D,X
	COM	,X
GEXBN1	JSR	BONEME	SETUP PARAMETERS AND MESSAGE FOR BONUS SUBROUTINE
	LDX	PLRX
	INC	PLAS,X
	JMP	PLSTR0	CYCLE
GEX00	CMPA	#8	ACCELERATE?
	BHI	GEX002
	LDB	UFOTIM
	LSRB
	CMPA	#3
	BHI	GEX001
	LSRB
GEX001	INCB
	CMPB	UFOTMR
	BHS	GEX002
	STB	UFOTMR
GEX002	DEC	UFOTMR
	BNE	GEX11
	CMPA	#4
	LDA	UFOTIM
	BHS	GEX003
	LSRA
	LSRA
	JSR	RMAX
GEX003	STA	UFOTMR
	LDA	UFOCNT
	CMPA	#12
	BHS	GEX11
	JSR	UFOST
GEX11	DEC	WAVTMR
	BEQ	GEX2
	LDA	LNDCNT
	BNE	GEX3
GEX2	LDA	WAVTIM
	STA	WAVTMR
	LDA	LNDRES
	BEQ	GEX3
	LDA	LNDCNT
	CMPA	#8
	BHS	GEX3
	LDA	WAVSIZ
	CMPA	LNDRES
	BLS	GEX20
	LDA	LNDRES
GEX20	JSR	LANDST	START A SQUAD
	NEGA
	ADDA	LNDRES
	STA	LNDRES
GEX3	LDA	STRCNT	GET STARS GOING AFTER OVERFLOW
	CMPA	#16
	BHS	GEX5
	INC	STRCNT
GEX5	LDA	GTIME
	INCA
	CMPA	#210
	BLS	GEX50
	LDB	#$06	METER 6
	JSR	AUD1
	CLRA
GEX50	STA	GTIME
	LDU	CRPROC
	DEC	PD,U
	BNE	GEX6
	LDB	#2
	LDY	#ELIST
	JSR	WDELT
	LDA	#40
	STA	PD,U
*CHECK WALL 1 BOZO MODE
GEX6	LDX	PLRX
	LDA	PWAV,X
	DECA
	BNE	GEX60	NOT WALL 1
	LDA	PD2,U
	BNE	GEX60	ALREADY DID IT
	LDA	MANCTR	EVER BEEN BONED UP THE ASS FOR BEING A WISE GUY??
	BNE	GEX6A	YIP...
	LDD	PSCOR+1,X
	CMPD	#$0020	2K??
	BHS	GEX6A
	LDA	PLAS,X
	CMPA	#1
	BLS	GEX60
	LDD	PSCOR+1,X
	CMPD	#$0010
	BLO	GEX60
GEX6A	LDA	STATUS	NO BOZO IF PLAYER IS DEAD
	BITA	#8
	BNE	GEX60
	LDA	#1
	STA	PD2,U
	JSR	GETWV0
	JSR	PTRANS
	JSR	BOZOV	WISE UP THE LANDERS
*MEN MESSAGES
GEX60	LDA	ASTCNT	MEN LEFT???
	BEQ	GEX66	NO
	LDA	MANCTR	IS HE HOLDING ANY??
	BNE	GEX66	THEN HE'S OK
	JSR	LEDTRG	ANY ON GROUND??
	BEQ	GEX66	YEP....NO TROUBLE
	LDX	#BLOMES	SEE IF THE MESSAGE IS THERE. 
	CMPX	MESSST
	BEQ	GEXT0	YEP...DON'T RE-SEND
	LDD	#$18FF	BLINK RATE,COLOR
	STD	MESSBL	GIVE IT TO THE MESSAGE UTILITY
	JSR	MESLDV	SEND THE MESSAGE.
GEXT0	LDA	#$FF	FLASH THE TERRAIN TO INDICATE TROUBLE
	STA	PCRAM+7
	NAP	5,GEXT1
GEXT1	LDA	#$FF
	STA	PCRAM+7
	NAP	4,GEXT2
GEXT2	CLR	PCRAM+7
	NAP	6,GEX0
GEX66	LDX	#BLOMES	IS THE CURRENT MESSAGE DANGER??
	CMPX	MESSST
	BNE	GEX666
	LDA	#1
	STA	MESSTM	MAKE THE TIME GO AWAY!
GEX666	LDA	PIA21	CHECK FOR RIPOFF MESSGE
	CMPA	#$E2	DN-REV-START1-THRUST ONLY
	BNE	GEX777
	JSR	RIPV
GEX777	NAP	15,GEX0

BLOMES	FCB 	$F0,CLEAR!+BLINK,$FF,TBLOM TERRAIN IN TROUBLE MESSAGE
*
*CHECK END OF WAVE
*
WVCHK	LDA	LNDCNT	END OF WAVE
	ADDA	LNDRES
	ADDA	TIECNT
	ADDA	PRBCNT
	ADDA	SWCNT
	ADDA	SCZCNT
	ADDA	YLCNT
	ADDA	FBCNT
	ADDA	DYNCNT
	RTS

*    MANCHK - X = PLAYER SAVE AREA.....RESTORES MEN IF THE RIGHT WAVE.

MANCHK	PSHS	D,X	SAVE THE STUFF WE BASH
	LDA	#5
	LDB	PWAV,X
	DECB
	BSR	REM
	BNE	MANCKX
	LDA	#10
	STA	PTARG,X
MANCKX	PULS	D,X,PC	DONE
*
*FIND REMAINDER
*A=DIVISOR,B=DIVIDEND
*RET EQ IF REMAINDER=0
REM	PSHS	A,B
REMLP	SUBB	,S
	BLO	REMX
	BNE	REMLP
REMX	PULS	A,B,PC
*
*GET ENEMY COUNTS A=WAVE#
*
GETCT	PSHS	D,X,Y,U
	LDU	#WVTAB
	JSR	PLINDX
	LEAX	PENEMY,X
	ADDA	#4
	LDY	#ELIST2-ELIST GET BYTE COUNT
GETCTL	LDB	A,U
	STB	,X+
	LEAU	$10,U
	LEAY	-1,Y
	BNE	GETCTL
	PULS	D,X,Y,U,PC
*
*GET NEW WAVE PARAMS
*X=PLAYER INDEX
GETWV0	PSHS	X,U,D
	BRA	GTWV02
GETWV	PSHS	X,U,D
	INC	PWAV,X
	BSR	MANCHK
GTWV01	LDA	PWAV,X
GTWV02	PSHS	A
	CMPA	#11
	BLS	GETWV1
	LDA	#11
GETWV1	LDU	#WVTAB
	ADDA	#4	OFFSET ADJUST
	LEAX	PENEMY,X
GETWV2	LDB	A,U
	STB	,X+
	LEAU	$10,U
	CMPU	#WVTEND
	BNE	GETWV2
	LDB	,S
	LDA	#5
	BSR	REM
	BNE	GTWV2B
	BSR	GETCT
GTWV2A	LDA	#10
	BSR	REM
	BNE	GTWV2B
	BSR	GETCT
GTWV2B	LDA	,S	HIGHER THAN 11?
	SUBA	#11
	BHS	GTWV30
	CLRA
GTWV30	STA	XTEMP
*CALC SLOPE PARAMETERS
	LDX	#GA1+6
	JSR	RCMOSD
	JSR	BCDHEX
	CMPA	,S	BEGIN SLOPE HI CURRENT WAVE #?
	BHI	GTWV3B	YES FORGET IT
	EXG	A,B
	JSR	BCDHEX
	CMPA	,S	END SLOPE LS CURRENT??
	BLS	GTWV3A	YES
	LDA	,S	NO USE CURRENT
GTWV3A	PSHS	B
	SUBA	,S+	FIND DIFF
	INCA
	LDB	GA1+11	GET SLOPE DELTA
	ANDB	#$F
	MUL
	ADDB	XTEMP
	STB	XTEMP
GTWV3B	LEAS	1,S	CLEAN UP STACK
	LDX	#GA1+2
	JSR	RCMOSD
	JSR	BCDHEX
	ADDA	XTEMP
	STA	XTEMP	ADD INITIAL DELTA
	BEQ	GETWVX
	TFR	B,A
	JSR	BCDHEX	CHECK CEILING
	CMPA	XTEMP
	BHS	GTWV31
	STA	XTEMP
GTWV31	LDA	XTEMP
GETWV4	LDB	#3
	JSR	PLINDX
	LEAY	PENEMY,X
	BSR	WDELT	GET YUR DELTAS
	DECA
	BNE	GETWV4
GETWVX	PULS	X,U,D,PC
*
*INTRA/INTER-WALL DELTA
*B=2 FOR INTRA,3=INTER
*Y=PENEMY FOR INTER-WALL
*Y=ELIST FOR INTRA-WALL
*
WDELT	PSHS	A,X,Y
	LDX	#WVTAB
WD0	LDA	B,X
	BMI	WD1
	ADDA	,Y
	BCS	WDL	OVERFLOW
	CMPA	,X
	BHI	WDL	MAXED OUT
	BRA	WD2
WD1	ADDA	,Y
	BCC	WDL	UNDERFLOW
	CMPA	1,X
	BLO	WDL	MINNED
WD2	STA	,Y
WDL	LEAY	1,Y	STEP THRU
	LEAX	$10,X
	CMPX	#WVTEND
	BNE	WD0
	PULS	A,X,Y,PC

BONEME	LDX	PLRX
	LDA	PWAV,X
	JSR	HEXBCD
	ANDA	#$F	END IN 0 (SHOWDOWN)
	CMPA	#$5
	BNE	EXMES1
	LDD	#$225
	JSR	SCORE
	LDA	#YLDUNM
	CLR	PCRAM
	JSR	SCLR1
	JSR	WRD7V
	PULS	X
	STX	PD,U
	NAP	$C0,EXME99
EXME99	LDX	PLRX
	JSR	GETWVV
	LDU	CRPROC
	JMP	[PD,U]
EXMES1	JSR	BSET
	JMP	BONUS

*
*WAVE DATA
*MAX,MIN,INTRADELT,INTERDELT
*W1,W2,W3,W4
*
WVTAB	FCB	20,0,0,0
	FCB	11,11,10,10,18,0,17,17,17,17,0,18  LANDERS
	FCB	3,0,0,0
	FCB	0,0,0,0,1,0,2,3,3,3,0,2 TIES
	FCB	6,0,0,0
	FCB	0,0,0,3,4,0,4,4,4,4,6,4  PROBES
	FCB	20,0,0,0
	FCB	0,0,0,0,0,0,0,0,0,0,0,0  SCHITZOS
	FCB	20,0,0,0
	FCB	0,0,0,0,0,8,0,0,0,0,0,0  SWARMERS
	FCB	10,0,0,0
	FCB	0,0,7,4,5,22,5,5,5,6,0,3  YLLABS
	FCB	3,0,0,0	STAR GATES
	FCB	1,1,1,1,1,1,1,1,1,1,1,1  #GATES
	FCB	10,0,0,0
	FCB	3,3,3,6,3,0,3,3,3,4,13,4  FIREBOMBERS
	FCB	3,0,0,0
	FCB	2,2,2,2,3,6,3,3,3,3,0,4  DYNAMO COUNT.
	FCB	30,0,0,0
	FCB	30,30,25,20,16,16,16,16,16,16,16,16   WAVE TIME
	FCB	4,0,0,0
	FCB	4,4,3,3,5,5,4,4,4,4,4,4  WAVE SIZE
	FCB	$60,0,3,2
	FCB	$16,$16,$1E,$24,$2E,$30,$32,$34,$36,$38,$3A,$3C  LANDER XV
	FCB	$01,0,0,0
	FCB	$00,$00,$00,$01,$01,$01,$01,$01,$01,$01,$01,$01 LANDER YV MSB
	FCB	$FF,0,$10,$00
	FCB	$50,$70,$B0,$00,$00,$00,$00,$00,$00,$00,$00,$00           LSB
	FCB	$FF,$10,$FC,$FE
	FCB	$FF,$4A,$3A,$2A,$2A,$28,$26,$24,$22,$20,$1E,$1C  LDSTIM
	FCB	$30,0,0,0
	FCB	$20,$20,$28,$2C,$30,$30,$30,$30,$30,$30,$30,$30  TIE XV
	FCB	3,0,0,0
	FCB	1,1,1,2,2,2,2,2,2,3,3,3                          SZRY
	FCB	1,0,0,0
	FCB	$00,$00,$00,$01,$01,$01,$01,$01,$01,$01,$01,$01  SZYV MSB
	FCB	$FF,$0,$08,$06
	FCB	$40,$62,$E0,$02,$12,$18,$1E,$24,$2A,$30,$36,$3C     " LSB
	FCB	$60,$0,$8,$4
	FCB	$08,$0C,$1C,$24,$28,$2C,$30,$34,$38,$3C,$40,$44     SZXV
	FCB	$FF,$08,$FE,$FE
	FCB	$FF,$2A,$22,$1E,$1C,$1A,$18,$16,$14,$12,$10,$0E   SZSTIM
	FCB	$60,$0,$08,$02
	FCB	$16,$16,$1E,$20,$22,$24,$26,$28,$2A,$2C,$2E,$30   SWXV
	FCB	40,10,-2,-1
	FCB	25,25,25,25,24,23,22,21,20,19,18,17               SWSTIM
	FCB	$3F,0,0,0
	FCB	$1F,$1F,$1F,$3F,$3F,$3F,$3F,$3F,$3F,$3F,$3F,$3F   SWAC
	FCB	$FF,$30,$F8,$FC
	FCB	$F0,$D4,$C4,$A4,$94,$90,$8C,$8A,$89,$88,$87,$86   UFOTIM
	FCB	20,3,$FF,$FF
	FCB	15,15,13,12,10,9,8,7,6,5,4,3                      UFSTIM
	FCB	200,40,-12,-8
	FCB	250,240,220,200,200,192,184,176,168,162,154,146   UFOSK
	FCB	$FF,$02,$00,$00
	FCB	$FF,$60,$0C,$06,$05,$04,$04,$04,$04,$04,$04,$03   YLLAB SHOT TIME
	FCB	$FF,0,0,0
	FCB	$0F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F   YLLAB ACCEL
	FCB	$68,$18,1,1
	FCB	$20,$20,$38,$48,$58,$58,$58,$58,$58,$58,$58,$58   YLLAB X VELOCITY
	FCB	$F2,$D0,1,2
	FCB	$80,$80,$C8,$D3,$D4,$D7,$DA,$DD,$E0,$E3,$E7,$EA   FIRE BOMB SHOT FREQ
	FCB	$FF,0,0,0
	FCB	$30,$30,$40,$50,$A0,$A0,$A0,$A0,$A0,$A0,$A0,$A0   FIRE BOMB DODGE VEL
	FCB	$FF,$0,$4,$8
	FCB	$28,$28,$38,$50,$50,$F8,$50,$60,$70,$80,$90,$A0   DYNSHT SHOT FREQUENCY
	FCB	$50,$0,$2,$2
	FCB	$01,$8,$20,$24,$28,$2C,$2C,$30,$30,$30,$30,$3C    MINE (MISSILE) SPEED
	FCB	$FF,0,0,0
	FCB	$F0,$F0,$E0,$D0,$C0,$B0,$A8,$A0,$98,$90,$88,$80   BMSK
	FCB	$FF,0,0,0
	FCB	$60,$60,$50,$40,$30,$2C,$28,$24,$20,$1C,$1C,$1C   BMBSTM
	FCB	$FF,0,0,0
	FCB	$10,$10,$10,$0F,$0E,$0D,$0C,$0B,$0A,$09,$08,$08   MCHSLP
	FCB	$FF,0,0,0
	FCB	$40,$40,$40,$40,$40,$40,$40,$40,$40,$40,$40,$40   MCHXV
	FCB	$FF,0,0,0
	FCB	$01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01   MCHYV
WVTEND	EQU	*
*
RIPTAB	FCB	$01,$0C,$28,$26,$1A,$1B,$25,$0C,$1B,$25
	FCB	$0C,$25,$26,$13,$24,$19,$13,$26,$17,2
	FCB	$01,$0C,$58,$16,$17,$25,$1B,$19,$20,$17,$16
	FCB	$0C,$17,$2A,$15,$1E,$27,$25,$1B,$28,$17,$1E
	FCB	$2B,$0C,$18,$21,$24
	FCB	$01,$0C,$68,$29,$1B,$1E,$1E,$1B,$13,$1F,$25,$0C
	FCB	$17,$1E,$17,$15,$26,$24,$21,$20,$1B,$15,$25,$0C
	FCB	$1B,$20,$15,$0F,2
	FCB	$01,$0C,$78,$14,$2B,$0C,$17,$27,$19
	FCB	$17,$20,$17,$0C,$22,$0F,$0C,$1C,$13,$24,$28,$1B
	FCB	$25,$0C,$13,$20,$16,$0C,$1E,$13,$29,$24,$17,$20
	FCB	$15,$17,$0C,$17,$0F,$0C,$16,$17,$1F,$13,$24,$02
	FCB	$01,$0C,$A8,$15,$21,$22,$2B,$24,$1B,$19,$1A,$26
	FCB	$0C,$03,$0B,$0A,$03,$0C,$29,$1B,$1E,$1E,$1B,$13
	FCB	$1F,$25,$0C,$17,$1E,$17,$15,$26,$24,$21,$20,$1B
	FCB	$15,$25,$0C,$1B,$20,$15,$0F
	FCB	$01,$0C,$B8,$13,$1E,$1E,$0C,$24,$1B,$19,$1A,$26
	FCB	$25,$0C,$24,$17,$25,$17,$24,$28,$17,$16,$00,$17,$22,$30
*************************
*STAR GATE JAZZ
*************************
*
*COLOR TERRAIN+ CURRENT GATE
*
CTERR	LDA	BGL
	LDX	#TERCTB
	LSRA
	LSRA
	LSRA
	ANDA	#$7F	DITCH LAST BIT
	LDB	A,X
	STB	PCRAM+7	TERRAIN COLOR
	LSRA
	LDX	#GATCTB	GET YUR GATE COLR
	LDB	A,X
	LDA	A,X
	STD	GATCL1
	STD	GATCL2
	STD	GATCL3
	LDB	PD,U
	INCB
	CMPB	#2
	BLS	CTERR1
	CLRB
CTERR1	STB	PD,U	TRINARY COUNTER
	LDX	#GATCL1
	ASLB		X2
	ABX
	LDD	#$AAAA	LASER COLOR CYCLER
	STD	,X
	NAP	4,CTERR
*GATE COLOR TABLE COLOR #S
GATCTB	FDB	$22BB,$BBBB,$BBBB,$8888
	FDB	$8888,$8888,$2222,$2222
*TERRAIN COLORING COLOR GUN DATA
TERCTB	FDB	$C7C7,$C7C7,$C7C7,$8747
	FDB	$4707,$0707,$0707,$0707
	FDB	$0707,$0F1F,$1717,$1717
	FDB	$1717,$1717,$1757,$9787
*
*STAR GATE START
*
SGSTRT	STA	XTEMP
	LDA	#$55
	LDB	SEED
	ANDB	#3
	BNE	SGS0
	INCB
SGS0	MUL
	TFR	B,A
	TFR	D,U	INIT X
SGS1	OBI	SGPIC,SGCOL,$1F00
	STU	OX16,X	FIXED X
	JSR	RAND
	ANDA	#$1F
	ADDA	#$5C
	STA	OY16,X
	LDA	#$11
	STA	OTYP,X	NON HYP NON SB
	CLRA
	CLRB
	STD	OXV,X
	STD	OYV,X
	STX	OPTR
	LEAU	$5000,U
	DEC	XTEMP
	BNE	SGS1
	RTS
*
*STAR GATE COLLISION
*
SGCOL	LDA	SAMEXC	CAN'T GET AT TERRAIN RAM??
	BNE	SGNK	THEN LEAVE THE GUY THERE.
	LDA	SGDFLG	JUST GOT OUT???
	BNE	SGNK
	LDA	STATUS	PLAYER DEAD??
	BITA	#$8
	BNE	SGNK	YEP
	LDA	PCFLG	PLAYER COLLISON?
	BEQ	SGNK	NO
	MAKP	SGTRAN
SGNK	CLRA
	PULS	D,PC	DONT KILL THE BASTARD
*STAR GATE TRANSPORT PROCESS
SGTRAN	LDX	#CHKS2+$32
	LDA	STATUS
	ANDA	#$2	SAVE THE TERRAIN BIT
	STA	PD,U	SAVE AWAY IN AN UNLIKELY PLACE
	LDA	#$77
	STA	STATUS	DISABLE SHIT
	LDB	#$56	HIDDEN CHECKSUM
	CMPB	-$32,X
	BEQ	STGT11
	LDA	HSEED	LOOK AT SEED
	CMPA	#$19	1 IN 10 STARGATE TRIPS
	BHS	STGT11
	LDB	LSEED
	LDA	#$9C
	TFR	D,X
	DEC	,X
STGT11	LDA	MANCTR
	LBEQ	SGC1	NO WARPIE-POOH
	LDX	#GA1+16	SEE IF BEYOND WARPABLE WAVE
	JSR	RCMOSA	GET THE WAVE
	JSR	BCDHEX
	LDX	PLRX	GET PLAYER STORE AREA.
	CMPA	PWAV,X	CHECK AGAINST WAVE
	BLO	SGC1	OUR CURRENT WAVE IS TOO HIGH TO WARP!
	LDB	GA1+15	GET NUMBER OF MEN NEEDED
	ANDB	#$0F	SAVE A BIT OF TIME!
	SUBB	MANCTR	COMPARE TO THE NUMBER WE HAVE.
	BHI	NOWRPT	NOT ENOUGH..HANDLE AS NORMAL STARGATE!
	LDA	NPLAD	LOOK AT DIRECTION
	EORA	PLAXV	COMPARE TO VELOCITY
	BMI	SGC1	THEY ARE NOT THE SAME....NO WARP!
*
	LDB	#9
	JSR	AUD1	BUMP IT
*
	BRA	WARP
NOWRPT	LDX	#WRPMES	B HAS NUMBER OF MEN NEEDED.
	CMPB	#1	JUST 1??
	BNE	SGCHK0
	LDX	#WRPMS2
SGCHK0	JSR	MESLDV
	BRA	SGC1
*
WARP	JSR	GNCIDE	KILL EVERYONE!
	JSR	SCRCLR	CLEAR THE SCREEN
	JSR	PLSAV	SAVE THE PLAYERS ASTRONAUTS ANYWAYS.
	NAP	1,WARP1	TAKE A NAP BEFORE CLOUDING UP THAT RAM
WARP1	INC	SAMEXC	NOW....LOCK HIM OUT HARD
	JSR	WARPV	DO THE EFFECT STUPID. (OR IS THAT STUPID EFFECT??)
	JSR	SCLEAN	CLEAN UP SAMRAM
	MAKP	COLR
	MAKP	CBOMB
	MAKP	TIECOL
	JSR	TDISP	PUT THE TOP STUFF BACK
	LDX	PLRX	GET THE PLAYER
	INC	PWAV,X	PUSH TO NEXT
	JSR	MANCHK	CHECKMAN RESTORE
	INC	PWAV,X	PUSH AGAIN
	JSR	MANCHK	CHECK MAN RESTORE
	LDA	PWAV,X	GET THE NEW WAVE
	INCA		COMPUTE THE WAVE HE'S GOING TO
	JSR	HEXBCD	MAKE READABLE
	TFR	A,B	MESSAGABLE
	LDA	#WARPM
	JSR	WRD7FV	PRINT OUT THE MESSAGE
	LDA	#$20	2000 POINTS PER MAN PLEASE
	JSR	BONUS	DO IT THERE
	LDX	PLRX
	INC	PLAS,X
	JMP	PLSTR0	DO ALL OF THE OTHER SUNDRY STARTUP STUFF AND PRAY
*
SGC1	LDB	#29	TIME OF EFFECT
	JSR	NITRV	PUT ALL OF THE PROCS TO SLEEP.
	NAP	1,SGC12
SGC12	LDX	SPTR	KILL BULLETS
	BEQ	SGC2
	JSR	KILSHL
	BRA	SGC12
SGC2	LDA	#2
	JSR	DIM	DIM THE LIGHTS.
SGCDIM	JSR	SCLR1	OFF
*
*LOCATE HIGHEST MAN
*
	LDU	#TLIST
	LDA	#$FF
LOCM0	STA	XTEMP
	LDX	#0
LOCML	LDY	,U++
	BEQ	LOCMX
	LDD	OCVECT,Y
	CMPD	ASKILP	IS IT MAN IN AIR???
	BEQ	LOCMX	NO DANGER
	LDD	OBJID,Y
	BNE	LOCMX	FALLING OR CAUGHT
	LDA	OY16,Y
	CMPA	XTEMP
	BHI	LOCMX
	STA	XTEMP
	LEAX	,Y
LOCMX	CMPU	#TLEND
	BNE	LOCML
	LEAX	,X
	BEQ	SGC20	NO MAN FOUND
	CLRA
	CLRB
	STD	PLAXV
	STD	PLAYV
	LDA	SEED	RANDOM HT.
	ANDA	#7
	NEGA
	ADDA	OY16,X
	SUBA	#$14
	CMPA	#YMIN+5
	BHS	SG99
	LDA	#YMIN+5
SG99	CMPA	#$68
	BLS	SG100
	LDA	#$68
SG100	STA	PLAY16
	LDD	#$2011
	TST	PLADIR
	BPL	SG200
	LDD	#$7015
SG200	STA	PLAX16
	PSHS	B
	STA	NPLAXC
	LDD	OX16,X	CENTER TARGET
	SUBA	,S+
	BRA	SGC21
SGC20	LDD	BGL
	ADDA	#$80	HALF WAY AROUND
SGC21	STD	BGL
	JSR	BGINIT
	NAP	1,BRIGHT LET INIT SLEEP.....THIS IS ALMOST A "JSR"
SGC3	CLRB
	LDA	INVFLG	RESET STATUS
	BEQ	SG30
	LDB	#$10
SG30	LDU	CRPROC	GET PROPER U
	ORB	PD,U	AND PUT BACK THE OLD TERRAIN BIT.
	STB	STATUS
	LDA	#$28
	STA	SGDFLG
	LDX	#SG31$
	JMP	SLEEP
SG31$	CLR	SGDFLG
	JMP	SUCIDE	THANK GOD ITS OVER...
WRPMES	FCB	$80,CLEAR,$90,NWARPM
WRPMS2	FCB	$80,CLEAR,$90,NOWRPM
*
*DIM THE LIGHTS
*A=RATE, 1=FASTEST
*PD6,U=RET ADDR;PD4=RATE;PD5=COUNT
DIM	PULS	X
	STX	PD6,U
	STA	PD4,U
	LDY	#MONOTB+110	SAVE COLORS IN MONO TABLE
	LDX	#PCRAM
DIM00	LDD	,X++	SAVE TABLE
	STD	,Y++
	CMPX	#PCRAM+16
	BNE	DIM00
	LDA	#7
	STA	PD5,U	SAVE
DIM0	LDA	PD4,U
	LDX	#DIM3
	JMP	SLEEP
DIM3	LDX	#PCRAM
DIM4	BSR	DIMSUB
	CMPX	#PCRAM+16
	BNE	DIM4
	DEC	PD5,U
	BPL	DIM0
	JMP	[PD6,U]

*      DIMSUB - TAKE AWAY BLUE, GREEN, RED

DIMSUB	LDA	,X	WORKING AREA
	LDB	,X	GET FOR RED CHECK
	ANDB	#$7	RED??
	CMPB	PD5,U	TOO MUCH RED??
	BLS	DIMSB1	NOPE.
	DECA		TAKE SOME RED AWAY
DIMSB1	LDB	,X	GET FOR GREEN
	LSRB
	LSRB
	LSRB
	ANDB	#$07
	CMPB	PD5,U
	BLS	DIMSB2	NOT ENOUGH GREEN
	SUBA	#$08	TAKE AWAY A GREEN UNIT.
DIMSB2	LDB	,X
	LSRB
	LSRB
	LSRB
	LSRB
	LSRB
	ANDB	#$06	ONLY 2 BITS OF BLUE
	CMPB	PD5,U
	BLS	DIMSB3
	SUBA	#$40	TAKE A BLUE AWAY.
DIMSB3	STA	,X+	RETURN TO SENDER
	RTS

BRIGHT	LDA	PD,U	SAVE STATUS FROM SCANNER CRASH
	PSHS	A
	JSR	ISCAN	RE-DO THE LISTS
	JSR	OSCAN
	JSR	SCNR	DO THE SCANNER
	LDU	CRPROC	RESTORE PROCESS ID
	PULS	A	RESTORE STATUS
	STA	PD,U
	LDA	STATUS	GET THE STATUS
	ANDA	#$DD	TAKE AWAY TERRAIN BIT...ACTIVATE OBJECTS
	ORA	PD,U	PUT TERRAIN BACK IF IT WAS THERE.
	STA	STATUS
	NAP	1,BRGHOB
BRGHOB	LDA	STATUS
	ORA	#$20	THEN DISABLE THEM AGAIN
	STA	STATUS
	LDA	#2
	JSR	BRT
	JMP	SGC3
*
	ORG	$8F00
*
*   MAKE ROOM FOR BRIGHTIE!
*
*ON LITES
*A=RATE
*PD4,U=RATE;PD5,U=COUNT;PD6,U=RET ADDR
*
BRT	PULS	X	GET RET ADDR
	STX	PD6,U
	STA	PD4,U
	LDA	#$7	ITERATIONS
	STA	PD5,U
BRGH2	LDA	PD4,U
	LDX	#BRGH3
	JMP	SLEEP
BRGH3	LDX	#PCRAM+1
	LDY	#MONOTB+111
BRGH7	LDA	,X	GET THE CURRENT BYTE
	LDB	,X
	ANDA	#$7	ONLY RED COUNTS
	STA	XTEMP
	LDA	,Y	GET ORIGINAL COLOR
	ANDA	#$7
	CMPA	XTEMP	DO WE HAVE ENOUGH RED??
	BLS	BRGH4	YEP
	INCB
BRGH4	LDA	,X	GET BACK
	ANDA	#$38	JUST CHECK GREEN
	STA	XTEMP
	LDA	,Y
	ANDA	#$38
	CMPA	XTEMP
	BLS	BRGH5
	ADDB	#$8	ADD A GREEN
BRGH5	LDA	PD5,U	GET INDEX
	BITA	#$1
	BNE	BRGH6	NOT A BLUE FRAME
	LDA	,X	GET COLOR BACK
	ANDA	#$C0
	STA	XTEMP
	LDA	,Y
	ANDA	#$C0
	CMPA	XTEMP
	BLS	BRGH6
	ADDB	#$40	ADD A BLUE
BRGH6	STB	,X+	PUT BACK
	LEAY	1,Y	KICK OTHER PTR.
	CMPX	#PCRAM+16
	BNE	BRGH7
	DEC	PD5,U	DEC IT
	BNE	BRGH2
	JMP	[PD6,U] RETURN
	END
