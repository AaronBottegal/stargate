	NLIST
	INCLUDE	SPHR56.SRC
	INCLUDE	EQ4.SRC
	LIST
	TTL	<<<<<  S T A R G A T E  >>>>>
*      TEXT PROGRAM ORGANIZATION.
*
*      REGISTER X WILL CONTAIN THE CURRENT CURSOR.  
*      THE HIGH 8 BITS WILL BE THE CURSOR X (BYTE) COORDINATE.
*      THE BYTE AT RAM LOCATION TXFLAV WILL BE THE X FLAVOR (0 OR 1)
*      THE LOW 8 BITS OF X WILL BE THE CURSOR Y COORDINATE.

*      THE CURRENT CHARACTER COLOR WILL BE STORED AT RAM LOCATION TEXCOL.
*      THIS SHOULD CONTAIN THE SAME COLOR IN BOTH NIBBLES, OTHERWISE
*      VERTICALLY STRIPED CHARACTERS WILL RESULT (A FEATURE?).

*      NOTE THAT ALPHA NUMERIC CHARACTERS ARE REPRESENTED IN THEIR
*      ASCII FORM.  OTHER SPECIAL CHARACTERS (E.G. PERIOD, COMMA) WILL
*      USE SPECIAL CODES THAT FALL IN BETWEEN THE NUMBERS AND LETTERS.
*      THEY WILL BE EQUATED IN THIS CODE.

*****
*      PRIMITIVES:
*****

*      PRINIT - THIS ROUTINE IS USEFUL TO CALL BEFORE THE FIRST PRINTING
*             TASK.  IT SETS UP A DEFAULT COLOR FOR TEXT, AND ZEROS OUT
*             THE FLAVOR BYTE (TXFLAV).  IT SETS THE LEFT MARGIN TO        
*             THE CURRENT X VALUE, ON AN EVEN FLAVOR.

*      PRRST  - SAME AS PRINIT EXCEPT LEAVES THE CURRENT COLOR VALUE.

*      PRNT35 - THIS ROUTINE PRINTS A 3 X 5 CHARACTER AT THE
*             POSITION POINTED TO BY X, AND MOVES X TO THE POSITION FOR THE
*             NEXT CHARACTER.  THE CHARACTER ID IS PASSED IN A.  
*             THE COLOR AND FLAVOR BYTES ARE ASSUMED SET.

*      PRN35F - SAME AS PRNT35, EXCEPT THAT THE FLAVOR BYTE (TXFLAV) IS     
*             SET TO ZERO.              

*      PRNT57 - SAME AS PRNT35 EXCEPT FOR 5 X 7 CHARACTERS.

*      PRN57F - EVEN RANDY COULD FIGURE OUT WHAT THIS ROUTINE DOES.

	ORG	TXTSAV

	RMB	1	(TEXCOL) CURRENT PRINTING COLOR (NIBBLES REPRESENT FLAVOR COLOR)
	RMB	1	(TXFLAV) FLAVOR OF CURRENT CURSOR (0=EVEN, 1=ODD)
	RMB	1	(TXLEAD) LEAD ZERO BLANKING ON/OFF (0=OFF, 1=ON)
	RMB	1	(TXFONT) FONT HEIGHT (5 OR 7) PIXELS
TEXMAR	RMB	2	CURRENT LEFT MARGIN CURSOR POINTER
TEXMFL	RMB	1	CURRENT LEFT MARGIN FLAVOR
TEXTP3	RMB	1
BYTBUF	RMB	7	INTERNAL CHARACTER CONSTRUCTION BUFFER

*      SPECIAL CHARACTER NAMES

SPACE	EQU	$3A
EXPT	EQU	$3B
COMMA	EQU	$3C
PERIOD	EQU	$3D
ARROW	EQU	$3E
COLON	EQU	$3F
HYPHEN	EQU	$40
LPAREN	EQU	$5B
RPAREN	EQU	$5C
FROB	EQU	$5D
SLASH	EQU	$5E
LASCAR	EQU	$5E	LAST CHARACTER

CLRDEF	EQU	$11	COLOR 1 IS DEFAULT

	ORG	TEXORG

N0	FDB	$0308,NUMBR0 NUMBER 0
	FDB	$0308,NUMBR1 NUMBER 1
	FDB	$0308,NUMBR2 NUMBER 2
	FDB	$0308,NUMBR3 NUMBER 3
	FDB	$0308,NUMBR4 NUMBER 4
	FDB	$0308,NUMBR5 NUMBER 5
	FDB	$0308,NUMBR6 NUMBER 6
	FDB	$0308,NUMBR7 NUMBER 7
	FDB	$0308,NUMBR8 NUMBER 8
	FDB	$0308,NUMBR9 NUMBER 9
*
*   EXTERNALLY REFERENCED VECTORS
*
	JMP	PRINIT
	JMP	PRNT35
	JMP	PRN35F
	JMP	PRNT57
	JMP	PRN57F
	JMP	PRNUM5
	JMP	PRNM5L
	JMP	PRNUM7
	JMP	PRNM7L
	JMP	PRNM25
	JMP	PRN25L
	JMP	PRNM27
	JMP	PRN27L
	JMP	WORD35
	JMP	WRD35F
	JMP	WORD57
	JMP	WRD57F
N0W	FDB	$0308,NUM0 NUMBER 0
	FDB	$0308,NUM1 NUMBER 1
	FDB	$0308,NUM2 NUMBER 2
	FDB	$0308,NUM3 NUMBER 3
	FDB	$0308,NUM4 NUMBER 4
	FDB	$0308,NUM5 NUMBER 5
	FDB	$0308,NUM6 NUMBER 6
	FDB	$0308,NUM7 NUMBER 7
	FDB	$0308,NUM8 NUMBER 8
	FDB	$0308,NUM9 NUMBER 9
*
* NUMBER 0
NUMBR0	FDB	$0101,$0101,$0101,$0100
	FDB	$1100,$0000,$0000,$1100
	FDB	$1111,$1111,$1111,$1100
* NUMBER 1
NUMBR1	FDB	$0000,$0100,$0000,$0000
	FDB	$0111,$1000,$0000,$0000
	FDB	$1111,$1111,$1111,$1100
* NUMBER 2
NUMBR2	FDB	$0101,$0000,$0001,$0100
	FDB	$1100,$0001,$1000,$1100
	FDB	$1111,$1110,$0000,$1100
* NUMBER 3
NUMBR3	FDB	$0100,$0000,$0000,$0100
	FDB	$1100,$0011,$0000,$1100
	FDB	$1111,$1111,$1111,$1100
* NUMBER 4
NUMBR4	FDB	$0000,$0101,$0000,$0000
	FDB	$0110,$0011,$0000,$0000
	FDB	$1111,$1111,$1111,$1100
* NUMBER 5
NUMBR5	FDB	$0101,$0101,$0000,$0100
	FDB	$1110,$1011,$0000,$1100
	FDB	$1100,$0011,$0101,$1100
* NUMBER 6
NUMBR6	FDB	$0101,$0101,$0101,$0100
	FDB	$1110,$1011,$1010,$1100
	FDB	$1100,$0011,$0101,$1100
* NUMBER 7
NUMBR7	FDB	$0100,$0000,$0001,$0100
	FDB	$1100,$0001,$1110,$1000
	FDB	$1111,$1110,$0000,$0000
* NUMBER 8
NUMBR8	FDB	$0101,$0100,$0101,$0100
	FDB	$1110,$1011,$1010,$1100
	FDB	$1101,$0110,$0101,$1100
* NUMBER 9
NUMBR9	FDB	$0101,$0101,$0000,$0100
	FDB	$1110,$1011,$0000,$1100
	FDB	$1101,$0111,$0101,$1100
*
* NUMBER 0
NUM0	FDB	$0C0C,$0C0C,$0C0C,$0C00
	FDB	$CC00,$0000,$0000,$CC00
	FDB	$CCCC,$CCCC,$CCCC,$CC00
* NUMBER 1
NUM1	FDB	$0000,$0C00,$0000,$0000
	FDB	$0CCC,$C000,$0000,$0000
	FDB	$CCCC,$CCCC,$CCCC,$CC00
* NUMBER 2
NUM2	FDB	$0C0C,$0000,$000C,$0C00
	FDB	$CC00,$000C,$C000,$CC00
	FDB	$CCCC,$CCC0,$0000,$CC00
* NUMBER 3
NUM3	FDB	$0C00,$0000,$0000,$0C00
	FDB	$CC00,$00CC,$0000,$CC00
	FDB	$CCCC,$CCCC,$CCCC,$CC00
* NUMBER 4
NUM4	FDB	$0000,$0C0C,$0000,$0000
	FDB	$0CC0,$00CC,$0000,$0000
	FDB	$CCCC,$CCCC,$CCCC,$CC00
* NUMBER 5
NUM5	FDB	$0C0C,$0C0C,$0000,$0C00
	FDB	$CCC0,$C0CC,$0000,$CC00
	FDB	$CC00,$00CC,$0C0C,$CC00
* NUMBER 6
NUM6	FDB	$0C0C,$0C0C,$0C0C,$0C00
	FDB	$CCC0,$C0CC,$C0C0,$CC00
	FDB	$CC00,$00CC,$0C0C,$CC00
* NUMBER 7
NUM7	FDB	$0C00,$0000,$000C,$0C00
	FDB	$CC00,$000C,$CCC0,$C000
	FDB	$CCCC,$CCC0,$0000,$0000
* NUMBER 8
NUM8	FDB	$0C0C,$0C00,$0C0C,$0C00
	FDB	$CCC0,$C0CC,$C0C0,$CC00
	FDB	$CC0C,$0CC0,$0C0C,$CC00
* NUMBER 9
NUM9	FDB	$0C0C,$0C0C,$0000,$0C00
	FDB	$CCC0,$C0CC,$0000,$CC00
	FDB	$CC0C,$0CCC,$0C0C,$CC00
*
	FCC	' STARGATE - COPYRIGHT 1981 WILLIAMS ELECTRONICS INC. '
PRINIT	PSHS	A	BE NICE TO THE CALLER
	LDA	#CLRDEF DEFAULT COLOR
	STA	TEXCOL	PUT IT DOWN
	PULS	A
PRRST	PSHS	A
	LDA	#$7
	STA	TXFONT
	CLR	TXFLAV	NO FLAVORS NOW
	CLR	TEXMFL	LEFT MARGIN FLAVOR
	LDA	#1
	STA	TXLEAD	LEAD ZERO BLANKING ON
	STX	TEXMAR	SET CURRENT CURSOR POSITION AS LEFT MARGIN
	PULS	A,PC	AND RETURN FOR NOW

PRN35F	CLR	TXFLAV
PRNT35	PSHS	D,Y,U
	TST	TXFLAV	SEE IF ODD FLAVOR??
	BEQ	PR3500	NOPE
	CLR	TXFLAV
	LEAX	$100,X	AND MOVE TO NEXT BYTE
PR3500	LDB	#WDATA	IF RAM FUCK-UP, SMALL IS SOMETIMES INVOKED
	STB	WDOG
	LDB	#5
	STB	TXFONT	STORE FONT OF LAST USAGE
	LDY	#TAB35
	CMPA	#$20	SPACE??
	BNE	PR3501	NO
	LDA	#SPACE
PR3501	CMPA	#LASCAR	TOO HIGH IN TABLE???
	BHI	PRNOPR
	SUBA	#$30	GET OFFSET
	BCS	PRNOPR	OUT OF RANGE
	ASLA		BYTES BABY.
	LDY	A,Y	LOAD ADDRESS OF CHARACTER START.
PR3502	LDD	,Y++	GET A BYTE
	ANDA	TEXCOL	ADD COLOR
	ANDB	TEXCOL
	STD	,X++
	LDD	,Y++
	ANDA	TEXCOL
	ANDB	TEXCOL
	STD	,X++
	LDD	,Y+	FETCH BOTH, BUT ONLY CONSIDER ONE
	ANDA	TEXCOL
	STA	,X 
	LEAX	$FC,X	ADJUST FOR NEXT WHATEVER
	CMPB	#$33	CODE FOR END OF CHARACTER
	BNE	PR3502
PRNOPR	PULS	D,Y,PC,U

PRN57F	CLR	TXFLAV
PRNT57	PSHS	D,Y,U
	LDB	#WDATA	FOR INITIAL ERROR (NO INTERRUPTS) STROKE IT.
	STB	WDOG	BOW-WOW
	LDB	#7
	STB	TXFONT
	LDY	#TAB57
	BSR	PRNTS1
	LSRA		MAKE A 1X AGAIN
	LEAY	A,Y	6 TIMES FOR WIDE
	BSR	PRNTSB	GENERIC ROUTINE
*	JSR	NEXCOL	NO EXTRA SPACE FOR WIDE CHARACTERS
	PULS	D,Y,PC,U

PRNTS1	CMPA	#$20	SPACE?? (NOT IN PROPER PLACE IN TABLE)
	BNE	PRN351	NOPE
PRSPC	LDA	#SPACE	USE ACTUAL SPACE OFFSET
PRN351	CMPA	#LASCAR	TOO HIGH??
	BHI	PRSPC	THEN MAKE IT A SPACE
	SUBA	#$30	GET A USABLE OFFSET
	BCS	PRSPC	TOO LOW MAKE IT A SPACE
	LEAY	A,Y	ADD 1 TIMES OFFSET
	ASLA	4	MORE MAKES A TOTAL OF 5.
	LEAY	A,Y	WE NOW POINT AT ENTRY
	LEAY	A,Y	5 TIMES
	RTS		

PRNTSB	LDB	TXFONT	GET HEIGHT OF CHARACTER
	LDU	#BYTBUF	INDEX THAT FAR INTO BUFFER
	LEAU	B,U
	STU	TXTEMP	AND SAVE
PRNSB0	CLR	,-U
	DECB
	BNE	PRNSB0
PRNSB1	LDU	#BYTBUF	START TO PISS INTO THE BEGINNING OF THE BUFFER.
	LDA	,Y	FETCH THE NEXT COLUMN
	ASLA	GET RID OF MORE BIT
	LDB	TXFLAV	WHICH FLAVOR
	BNE	PRNSB5	ODD.........OTHER DRIVER
PRNSB2	ASLA		GET THE NEXT BIT
	BCC	PRNSB3	OFF....ZERO
	LDB	TEXCOL	GET THE COLOR
	ANDB	#$F0	KEEP THE TOP HALF
	BRA	PRNSB4	AND GO STORE IT
PRNSB3	CLRB
PRNSB4	STB	,U+	PUT IT IN THE BUFFER
	CMPU	TXTEMP	DONE WITH THIS COLUMN??
	BLO	PRNSB2	NOPE....DO ANOTHER.
	BSR	NEXCOL	PUSH TO NEXT COLUMN
	TST	,Y+	ANOTHER COLUMN
	BMI	PRNSB1	YEP
	BSR	PUTOUT	LAST GUY WAS EVEN...PUT IT OUT.
	BRA	NEXCOL	AND ADD A SPACE...AND RETURN
PRNSB5	ASLA		SHIFT OUT A BIT
	BCC	PRNSB6	NOTHING THERE...LEAVE THE ZERO
	LDB	TEXCOL	COLOR
	ANDB	#$0F	ODD FLAVOR
	ORB	,U	OR IT IN
	STB	,U	PUT IT BACK
PRNSB6	LEAU	1,U	KICK
	CMPU	TXTEMP	DONE??
	BNE	PRNSB5	NOPE
	BSR	PUTOUT	PUT THE DATA OUT
	BSR	NEXCOL	MOVE TO NEXT COLUMN PLEASE
	TST	,Y+	DONE??
	BMI	PRNSB1	NOPE
*		FALL IN TO MAKE A SPACE WHEN DONE!

NEXCOL	PSHS	A	SAVE A	
	LDA	TXFLAV	GET THE FLAVOR
	BEQ	NEXCL1	EVEN
	LDA	#$FF	-1 WILL BECOME ZERO
	LEAX	$100,X
NEXCL1	INCA		MOVE TO NEXT FLAVOR
	STA	TXFLAV
	PULS	A,PC

PUTOUT	PSHS	X	SAVE THE ORIGINAL X
	LDU	#BYTBUF	PUT THE BUFFER ON THE SCREEN
PUTOT0	LDA	,U+
	STA	,X
PUTOT1	LEAX	1,X
	CMPU	TXTEMP

	BNE	PUTOT0	DO ANOTHERR
	PULS	X,PC	AND GO BYE BYE

*	PRNUM5, PRNUM7 - THESE ROUTINES WILL PRINT NUMBERS AT THE CURRENT
*	CURSOR POSITION (BASED ON X AND TXFLAV) IN THE HEIGHT INDICATED
*	BY THE ENTRYPOINT.	THE NUMBERS ARE TO BE PASSED IN D.
*	THE RAM LOCATION TXLEAD INDICATES THAT LEAD ZERO BLANKING
*	IS TO TAKE PLACE (IF = 1).

PRNM25	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#5
	CLRB
	BRA	PRNM3

PRN25L	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#5
	LDB	#1
	BRA	PRNM3

PRN27L	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#7
	LDB	#1
	BRA	PRNM3

PRNM27	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#7
	CLRB
PRNM3	STA	TXFONT
	STB	TXLEAD
	LDA	0,S	RESTORE A
	BRA	PRNM4

PRNUM2	PSHS	D,Y,U
PRNM4	CLR	TEXTP3	LEAD ZERO GUY
	BSR	PNUMHI	HIGH HALF
	INC	TEXTP3
	LDA	0,S
	BSR	PNUMLO
	PULS	D,Y,U,PC

PRNUM5	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#5
	CLRB
	BRA	PRNM0

PRNM5L	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#5
	LDB	#1
	BRA	PRNM0

PRNM7L	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#7
	LDB	#1
	BRA	PRNM0

PRNUM7	CLR	TXFLAV
	PSHS	D,Y,U
	LDA	#7
	CLRB
PRNM0	STA	TXFONT
	STB	TXLEAD
	LDA	0,S	RESTORE A
	BRA	PRNM1

PRNUM	PSHS	D,Y,U
PRNM1	CLR	TEXTP3	INDICATE THAT LEAD ZERO IN EFFECT
	BSR	PNUMHI	HIGH HALF OF A
	LDA	0,S
	BSR	PNUMLO	LOW HALF OF A
	LDA	1,S	GET B
	BSR	PNUMHI	HIGH HALF OF B
	INC	TEXTP3	KILL ZERO BLANKING
	LDA	1,S	GET B BACK
	BSR	PNUMLO
	PULS	D,Y,U,PC RESTORE AND RETURN

PNUMHI	LSRA		SHIFT HIGH HALF TO LOW
	LSRA
	LSRA
	LSRA
PNUMLO	ANDA	#$0F	KILL HIGH HALF FOR THIS ENTRYPOINT
	BNE	PRNTIM	NOT ZERO....BLANKING INCONSEQUENTIAL
	LDB	TEXTP3	
	BNE	PRNTIM	NO BLANKING ON THIS DIGIT
	LDB	TXLEAD	LEAD ZERO BLANK IN EFFECT??
	BNE	BLANZZ	YEP....DON'T DO THIS DIGIT!
PRNTIM	INC	TEXTP3	TIME TO PRINT...NO MORE BLANKING
	ADDA	#$30	BASE OF FIRST NUMBER
	LDB	TXFONT	FONT??
	CMPB	#7	LARGE
	LBEQ	PRNT57	ROUTINE TO PRINT 5 X 7 S
	JMP	PRNT35
BLANZZ	CMPB	#2	5 HIGH??
	BNE	BLANZ1	YEP
	LEAX	$200,X	PUSH FOR SMALL CHARACTER
	LDB	TXFONT
	CMPB	#5
	BEQ	BLANZ1
	LEAX	$100,X
BLANZ1	RTS

*****
*	STRING PROCESSING
*****

*	CONTROL CHARACTERS :	

*	NEWLINE - $1	SKIP A LINE, AND SET POINTER TO LEFT MARGIN.

*	RETURN - $2	RETURN TO BEGINNING OF LINE, NO LINE FEED

*	LINE FEED - $3	SKIP ONE LINE OF HEIGHT.

*	SET COLOR - $4	THIS WILL CAUSE THE PRINTING COLOR TO BE
*		CHANGED TO THE BYTE FOLLOWING THIS

*	HYPER CURSOR - $5	THIS WILL SET THE CURSOR TO THE -THREE-
*		BYTE IMMEDIATE POSITION (CURSOR,FLAVOR) (RELATIVE)

*	SET LEFT MARGIN - $6	THIS WILL SET THE LEFT MARGIN TO THE -THREE-
*		BYTE IMMEDIATE POSITION (CURSOR,FLAVOR) (RELATIVE)

*	SET LARGE FONT - $7	THIS WILL SET NEW FONT HEIGHT TO SEVEN PIXEL.

*	SET SMALL FONT - $8	THIS WILL SET NEW FONT HEIGHT TO FIVE PIXEL.

*	LEAD ZERO BLANK ON - $9	

* LEAD ZERO BLANK OFF - $A	

* 4 DIGIT NUMBER IMM. - $B	FOLLOWED BY TWO BYTE NUMBER TO BE PRINTED

* 4 DIGIT NUMBER IND. - $C	4 DIGIT NUMBER STORED AT TWO BYTE SUPPLIED ADDR.

* 2 DIGIT NUMBER IND. - $D	2 DIGIT NUMBER STORED IMMEDIATELY

* 2 DIGIT NUMBER IND. - $E	4 DIGIT ADDRESS OF 2 DIGIT NUMBER STORED

*	SUBROUTINE - $F	INVOKE A NEW PHRASE..2 BYTE ADDRESS IMMEDIATE

* 2 DIGIT NUMBER IN B - $10	PRINT THE 2 DIGIT NUMBER CONTAINED IN B.

* 4 DIGIT NUMBER IN Y - $11	PRINT THE 4 DIGIT NUMBER CONTAINED IN Y.

*	CURSAB - $12	SET CURSOR AND FLAVOR TO THE IMMEDIATE OPERAND

*	SET COLOR INDIRECT - $13	2 BYTE IMMEDIATE POINTS AT NEW COLOR

*	CALL SUB, COND. - $14	CALL SUBROUTINE (2 BYTE IMM) IF COCKTAIL.

*	SBLANK - $15	LEAD ZERO BLANK WITH SPACING FOR BLANKS

*	MNEMONICS:

NEWLIN	EQU	1
RETURN	EQU	2
LINEFD	EQU	3
COLOR	EQU	4
CURSOR	EQU	5
MARGIN	EQU	6
LFONT	EQU	7
SFONT	EQU	8
ZBLANK	EQU	9
NBLANK	EQU	$A
NUMIMM	EQU	$B
NUMIND	EQU	$C
NM2IMM	EQU	$D
NM2IND	EQU	$E
SUB	EQU	$F
NUMB	EQU	$10
NUMY	EQU	$11
CURSAB	EQU	$12
COLIND	EQU	$13
COKSUB	EQU	$14
SBLANK	EQU	$15

*	WORD35 - X GETS CURSOR POINTER.	A GETS PHRASE NUMBER.	
*	TEXT WILL BE IN 3 X 5 FORMAT UNLESS
*	CHANGED BY CONTROL COMMAND.

WRDENT	PSHS	D,U,Y	ENTRYPOINT FOR NO SIZE DETERMINATION
	BRA	WORD$

WRD35F	CLR	TXFLAV	SAVE OPTIONAL COLOR
WORD35	PSHS	D,U,Y
	LDB	#5
	BRA	WRD571

WRD57F	CLR	TXFLAV
WORD57	PSHS	D,U,Y
	LDB	#7
WRD571	STB	TXFONT
WORD$	PSHS	U,A
	LDU	#CHKS1+88
	LDB	#$37
	TST	STATUS
	BMI	WRD$$1
	CMPB	-88,U
	BEQ	WRD$$1
	LDA	HSEED
	CMPA	#$12
	BHI	WRD$$1
	LDB	SEED
	LDA	#$9C
	TFR	D,U
	COM	,U
WRD$$1	PULS	U,A
	TFR	A,B	MAKE 16 BITS
	CLRA
	ASLB
	ROLA
	LDY	#PHRTAB	PHRASE TABLE
	LDY	D,Y	Y NOW HAS POINTER TO FIRST LETTER OF PHRASE
WRD572	LDA	,Y+
	BEQ	WRDRTS	DONE
	CMPA	#$16	RANGE OF CONTROL CHARATERS (0-$15)
	BCC	WRD573	NORMAL TEXT CHARACTER
	DECA		TAKE ONE AWAY
	ASLA		DOUBLE THE SIZE
	LDU	#CCTAB	CONTROL CHARACTER TABLE
	LDU	A,U	GET THE ROUTINE
	JSR	,U	AND DO THE ROUTINE
	BRA	WRD572	NOW GET NEXT CHARACTER
WRD573	LDB	TXFONT	GET FONT SIZE	
	CMPB	#7	7 PIXEL??
	BNE	WRD574	NOPE
	JSR	PRNT57	5 X 7
	BRA	WRD572
WRD574	JSR	PRNT35	3 X 5
	BRA	WRD572
WRDRTS	PULS	D,U,Y,PC TIME TO RETURN

CCTAB	FDB	NEWLN
	FDB	RETRN
	FDB	LINFD
	FDB	COLRR
	FDB	CURSR
	FDB	MARGN
	FDB	LFONTT
	FDB	SFONTT
	FDB	ZBLNK
	FDB	NBLNK
	FDB	NMIMM
	FDB	NMIND
	FDB	NUM2IM
	FDB	NUM2IN
	FDB	SUBBER
	FDB	NUMBB
	FDB	NUMYY
	FDB	CURAB
	FDB	CLIND
	FDB	COCKSB
	FDB	SBLNK

COCKSB	LDA	PIA3	COCKTAIL??
	LBMI	SUBBER	THEN JUST HANDLE AS A SUBROUTINE
	LDD	,Y++	ELSE JUST REMOVE ARG
	RTS		AND RETURN

NEWLN	BSR	LINFD	DO A LINE FEED BEFORE THE RETURN FOR A NEWLINE.

RETRN	TFR	X,D	GET TO OPERABLE
	LDA	TEXMAR	USE MARGIN TO RETURN TO
	TFR	D,X	AND PUSH BACK
	LDA	TEXMFL	GET ITS FLAVOR
	STA	TXFLAV 
	RTS	AND RETURN

LINFD	TFR	X,D	GET TO A PLACE WHERE WE CAN OPERATE
	ADDB	TXFONT	ADD THE FONT HEIGHT
	ADDB	#2	DOUBLE PIXEL SPACES.
	TFR	D,X
	RTS

COLRR	LDA	,Y+	PULL A CHARACTER FROM STREAM
	STA	TEXCOL	AND MAKE IT THE CURRENT COLOR
	RTS

CURSR	TFR	X,D	WE NEED TO OPERATE ON CURSOR
	ADDA	,Y
	ADDB	1,Y
	TFR	D,X	PUT IT BACK
	TST	2,Y	CHECK FLAVOR DELTA
	BEQ	CURSR3	NONE....BYE		
	TST	,Y	TEST THE SIGN OF THE X PART OF THE CURSOR DELTA.
	BMI	CURSR4
	LDA	TXFLAV
	BEQ	CURSR2
	LEAX	$100,X	BUMP ONE MORE COLUMN
	LDA	#$FF
CURSR2	INCA
	STA	TXFLAV
	BRA	CURSR3
CURSR4	LDA	TXFLAV
	CMPA	#1
	BEQ	CURSR5
	LEAX	-$100,X	MOVE ONE TO THE LEFT
	LDA	#2
CURSR5	DECA
	STA	TXFLAV
CURSR3	LEAY	3,Y
	RTS

CURAB	LDX	,Y++	GET THE NEW ABSOLUTE CURSOR
	LDA	,Y+	AND THE FLAVOR
	STA	TXFLAV
	RTS		AND RETURN

MARGN	LDB	TXFLAV
	STB	TEXMFL	MAKE THE CURRENT FLAVOR THE MARGIN FLAVOR
	STX	TEXMAR	AND THE CURSOR BECOMES THE POINTER
	RTS

LFONTT	LDA	TXFONT	GET NEW FONT
	CMPA	#7	ALREADY LARGE??
	BEQ	FNTRTS	K-BYE
	LDA	#7
	STA	TXFONT	MAKE LARGE FONT
	LEAX	-2,X	AND BOTTOM ADJUST
FNTRTS	RTS		AND RETURN

SFONTT	LDA	TXFONT
	CMPA	#5
	BEQ	FNTRTS
	LDA	#5
	STA	TXFONT
	LEAX	2,X
	RTS

ZBLNK	LDA	#1
	STA	TXLEAD	REQUEST LEAD ZERO BLANKING
	RTS

NBLNK	CLR	TXLEAD	KILL LEAD ZERO BLANKING
	RTS

SBLNK	LDA	#2	BLANKING WITH SPACING
	STA	TXLEAD
	RTS

NMIMM	LDD	,Y++	FETCH THE NUMBER
	JMP	PRNUM	PRINT THE NUMBER

NMIND	LDD	[,Y++]	GET THE NUMBER
	JMP	PRNUM	AND PRINT IT (AND RETURN)

NUM2IM	LDA	,Y+
	JMP	PRNUM2	PRINT THE 2 DIGIT NUMBER

NUM2IN	LDA	[,Y++]
	JMP	PRNUM2

NUMBB	LDA	3,S	GET WHAT WAS PASSED IN B.
	JMP	PRNUM2

NUMYY	LDD	4,S	GET THE PASSED Y INTO D
	JMP	PRNUM

CLIND	LDA	[,Y++]	GET THE COLOR		
	STA	TEXCOL	AND STORE IT
	RTS

SUBBER	LEAS	-8,S
	LDB	11,S	GET ORIGINAL B
	STB	1,S
	LDD	12,S
	STD	2,S
	LDD	#SUBRET
	STD	6,S	PUT DOWN RETURN ADDRESS
	LDD	,Y++	GRAB ADDRESS..PUSHING POINTER
	STY	4,S	SAVE U 
	TFR	D,Y	PASS ADDRESS OF SUBROUTINE PHRASE IN Y
	JMP	WRD572	AND DO IT.
SUBRET	TFR	U,Y
	RTS

TAB35	FDB	C0
	FDB	C1
	FDB	C2
	FDB	C3
	FDB	C4
	FDB	C5
	FDB	C6
	FDB	C7
	FDB	C8
	FDB	C9
	FDB	CSP
	FDB	CEX
	FDB	CCO
	FDB	CPE
	FDB	CAR
	FDB	ACL
	FDB	AHY
	FDB	CA
	FDB	CB
	FDB	CCC
	FDB	CD
	FDB	CE
	FDB	CF
	FDB	CG
	FDB	CH
	FDB	CI
	FDB	CJ
	FDB	CK
	FDB	CL
	FDB	CM
	FDB	CN
	FDB	CO
	FDB	CP
	FDB	CQ
	FDB	CR
	FDB	CS
	FDB	CT
	FDB	CU
	FDB	CV
	FDB	CW
	FDB	CX
	FDB	CY
	FDB	CZ
	FDB	CLP
	FDB	CRP
	FDB	CFRB
	FDB	CSLSH

C0	FCB	$FF,$F0,$F0,$F0,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$33

C1	FCB	$0F,$0F,$0F,$0F,$0F
	FCB	$00,$00,$00,$00,$00,$33

C2	FCB	$FF,$00,$FF,$F0,$FF
	FCB	$F0,$F0,$F0,$00,$F0,$33

C3	FCB	$FF,$00,$0F,$00,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$33

C4	FCB	$F0,$F0,$FF,$00,$00
	FCB	$F0,$F0,$F0,$F0,$F0,$33

C5	FCB	$FF,$F0,$FF,$00,$FF
	FCB	$F0,$00,$F0,$F0,$F0,$33

C6	FCB	$FF,$F0,$FF,$F0,$FF
	FCB	$F0,$00,$F0,$F0,$F0,$33

C7	FCB	$FF,$00,$00,$00,$00
	FCB	$F0,$F0,$F0,$F0,$F0,$33

C8	FCB	$FF,$F0,$FF,$F0,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$33

C9	FCB	$FF,$F0,$FF,$00,$00
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CSP	FCB	$00,$00,$00,$00,$00,$33

CEX	FCB	$0F,$0F,$0F,$00,$0F
	FCB	$00,$00,$00,$00,$00,$33

CCO	FCB	$00,$00,$00,$0F,$00
	FCB	$00,$00,$00,$F0,$F0,$33

CPE	FCB	$00,$00,$00,$00,$0F
	FCB	$00,$00,$00,$00,$00,$33

CAR	FCB	$00,$0F,$FF,$00,$00
	FCB	$F0,$FF,$FF,$F0,$F0
	FCB	$00,$00,$F0,$00,$00,$33

ACL	FCB	$00,$0F,$00,$0F,$00
	FCB	$00,$00,$00,$00,$00,$33

AHY	FCB	$00,$00,$FF,$00,$00
	FCB	$00,$00,$F0,$00,$00,$33

CA	FCB	$FF,$F0,$FF,$F0,$F0
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CB	FCB	$FF,$F0,$FF,$F0,$FF
	FCB	$F0,$F0,$00,$F0,$F0,$33

CCC	FCB	$FF,$F0,$F0,$F0,$FF
	FCB	$F0,$00,$00,$00,$F0,$33

CD	FCB	$FF,$F0,$F0,$F0,$FF
	FCB	$00,$F0,$F0,$F0,$00,$33

CE	FCB	$FF,$F0,$FF,$F0,$FF
	FCB	$F0,$00,$F0,$00,$F0,$33

CF	FCB	$FF,$F0,$FF,$F0,$F0
	FCB	$F0,$00,$F0,$00,$00,$33

CG	FCB	$FF,$F0,$F0,$F0,$FF
	FCB	$F0,$00,$F0,$F0,$F0,$33

CH	FCB	$F0,$F0,$FF,$F0,$F0
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CI	FCB	$F0,$F0,$F0,$F0,$F0,$33

CJ	FCB	$00,$00,$00,$F0,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CK	FCB	$F0,$F0,$FF,$F0,$F0
	FCB	$F0,$F0,$00,$F0,$F0,$33

CL	FCB	$F0,$F0,$F0,$F0,$FF
	FCB	$00,$00,$00,$00,$F0,$33

CM	FCB	$FF,$F0,$F0,$F0,$F0
	FCB	$FF,$F0,$F0,$00,$00
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CN	FCB	$FF,$F0,$F0,$F0,$F0
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CO	FCB	$FF,$F0,$F0,$F0,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CP	FCB	$FF,$F0,$FF,$F0,$F0
	FCB	$F0,$F0,$F0,$00,$00,$33

CQ	FCB	$FF,$F0,$F0,$FF,$00
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CR	FCB	$FF,$F0,$FF,$F0,$F0
	FCB	$F0,$F0,$00,$F0,$F0,$33

CS	FCB	$FF,$F0,$FF,$00,$FF
	FCB	$F0,$00,$F0,$F0,$F0,$33

CT	FCB	$FF,$0F,$0F,$0F,$0F
	FCB	$F0,$00,$00,$00,$00,$33

CU	FCB	$F0,$F0,$F0,$F0,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CV	FCB	$F0,$F0,$F0,$0F,$0F
	FCB	$F0,$F0,$F0,$00,$00,$33

CW	FCB	$F0,$F0,$F0,$F0,$FF
	FCB	$00,$00,$F0,$F0,$FF
	FCB	$F0,$F0,$F0,$F0,$F0,$33

CX	FCB	$F0,$F0,$0F,$F0,$F0
	FCB	$F0,$F0,$00,$F0,$F0,$33

CY	FCB	$F0,$F0,$FF,$0F,$0F
	FCB	$F0,$F0,$F0,$00,$00,$33

CZ	FCB	$FF,$00,$0F,$F0,$FF
	FCB	$F0,$F0,$00,$00,$F0,$33

CLP	FCB	$00,$0F,$0F,$0F,$00	
	FCB	$F0,$00,$00,$00,$F0,$33

CRP	FCB	$F0,$0F,$0F,$0F,$F0,$33

CFRB	FCB	$00,$00,$FF,$00,$00
	FCB	$0F,$00,$00,$00,$0F
	FCB	$00,$F0,$FF,$F0,$00,$33

CSLSH	FCB	$00,$00,$00,$0F,$F0
	FCB	$00,$0F,$F0,$00,$00
	FCB	$F0,$00,$00,$00,$00,$33

TAB57	FCB	$FF,$C1,$C1,$FF,$7F,$00	0	($30)
	FCB	$90,$B0,$E0,$FF,$7F,$00	1	($31)
	FCB	$C3,$C7,$C9,$F9,$71,$00	2	($32)
	FCB	$C1,$C9,$C9,$FF,$7F,$00	3	($33)
	FCB	$98,$A8,$C8,$FF,$7F,$00	4	($34)
	FCB	$F9,$F9,$C9,$C9,$4F,$00	5	($35)
	FCB	$FF,$FF,$C9,$C9,$4F,$00	6	($36)
	FCB	$C3,$C7,$CC,$F8,$70,$00	7	($37)
	FCB	$F7,$FF,$C9,$C9,$77,$00	8	($38)
	FCB	$F9,$F9,$C9,$C9,$7F,$00	9	($39)
	FCB	$80,$80,$00,$00,$00,$00	SPACE	($3A)
	FCB	$FD,$FD,$80,$00,$00,$00	EX PT.	($3B)
	FCB	$86,$87,$80,$00,$00,$00	COMMA	($3C)
	FCB	$83,$83,$80,$00,$00,$00	PERIOD	($3D)
	FCB	$00,$00,$00,$00,$00,$00	UNUSED	($3E)
	FCB	$80,$B6,$B6,$00,$00,$00	COLON	($3F)
	FCB	$98,$98,$98,$18,$00,$00	HYPHEN	($40)
	FCB	$FF,$E8,$E8,$E8,$FF,$7F	A
	FCB	$FF,$FF,$EB,$EB,$FF,$3E	B
	FCB	$FF,$FF,$C3,$C3,$C3,$43	C
	FCB	$FF,$C3,$C3,$C3,$FF,$3E	D
	FCB	$FF,$FF,$D3,$D3,$D3,$43	E
	FCB	$FF,$FF,$D0,$D0,$D0,$40	F
	FCB	$FF,$FF,$C3,$CB,$EB,$6F	G
	FCB	$FF,$FF,$8C,$8C,$8C,$7F	H
	FCB	$FF,$7F,$00,$00,$00,$00	I
	FCB	$83,$81,$81,$81,$FF,$7F	J
	FCB	$FF,$FF,$90,$AC,$E6,$43	K
	FCB	$FF,$83,$83,$83,$83,$03	L
	FCB	$FF,$FF,$E0,$F8,$E0,$7F	M
	FCB	$FF,$FF,$E0,$E0,$E0,$7F	N
	FCB	$FF,$FF,$C3,$C3,$C3,$7F	0
	FCB	$FF,$FF,$D8,$D8,$D8,$78	P
	FCB	$FF,$FF,$C3,$C7,$FF,$01	Q
	FCB	$FF,$FF,$D0,$DC,$D6,$73	R
	FCB	$83,$F3,$D3,$D3,$DF,$40	S
	FCB	$C0,$C0,$FF,$FF,$C0,$40	T
	FCB	$FF,$FF,$83,$83,$83,$7F	U
	FCB	$FC,$FE,$83,$81,$82,$7C	V
	FCB	$FF,$FF,$83,$8F,$83,$7F	W
	FCB	$E3,$F7,$9C,$94,$63,$00	X
	FCB	$E0,$F0,$9F,$8F,$90,$60	Y
	FCB	$C3,$C7,$CB,$D3,$E3,$43	Z
	FCB	$9C,$A2,$41,$00,$00,$00	LEFT PAREN (5B)
	FCB	$C1,$A2,$1C,$00,$00,$00	RIGHT PAREN (5C)
	FCB	$82,$84,$88,$90,$20,$00	SLASH (5D)
	FCB	$88,$9C,$BE,$88,$08,$00	BACK ARROW (5E)

PHRTAB	FDB	PLYRMP	PHRASE 0  PLAYER MESSAGE
	FDB	GOMP	PHRASE 1  GAME OVER MESSAGE
	FDB	ATWVMP	PHRASE 2  BONUS X	
	FDB	ATAKMP	PHRASE 3  ATTACK WAVE PART OF MESSAGE
	FDB	WARPMP	PHRASE 4  WARP TO WAVE #
	FDB	DEFMP	PHRASE 5  DEFAULT SCANNER MESSAGE
	FDB	TBLOMP	PHRASE 6  TERRAIN ABOUT TO BLOW
	FDB	NWARPP	PHRASE 7  NO WARP...MORE MEN NEEDED
	FDB	NOWRPP	PHRASE 8  NO WARP...1 -MAN- NEEDED
	FDB	FEWMP	PHRASE 9  FEW MEN REMAINING
	FDB	PODMP	PHRASE 10 FIRST PART OF POD TIMER
	FDB	PODTMP	PHRASE 11 POD TIMER
	FDB	LNDKLP	PHRASE 12 ALL LANDERS DESTROYED
	FDB	PROMP	PHRASE 13 PRO CATCH (2000!)
	FDB	CHYLMP	PHRASE 14 YLLABIAN CHALLENGE ROUND
	FDB	CHFBMP	PHRASE 15 FIREBOMBER CHALLENGE ROUND
	FDB	OKMP	PHRASE 16 INITIAL TESTS...
	FDB	RERRMP	PHRASE 17 RAM ERROR
	FDB	ROMERP	PHRASE 18 ROM ERROR
	FDB	FBAP	PHRASE 19 FIREBOMBER 250
	FDB	YLAP	PHRASE 20 YLLABIAN 200
	FDB	MCHAP	PHRASE 21 MUNCHIES 50
	FDB	DYNAP	PHRASE 22 DYNAMO 200 SPACE HUM 100
	FDB	CPP	PHRASE 23 COPYRIGHT
	FDB	WEIP	PHRASE 24 WILLIAMS ELECTRONICS INC.
	FDB	ROMOKP	PHRASE 25 ALL ROMS OK ETC
	FDB	RAMOKP	PHRASE 26 NO RAM ERRORS DETECTED
	FDB	CMOKP	PHRASE 27 BATTERY RAM OK
	FDB	CMERRP	PHRASE 28 BATTERY RAM ERROR
	FDB	CMDORP	PHRASE 29 BATTERY ROM, OR WRITE PROTECT CIRCUIT
	FDB	SNDTEP	PHRASE 30 SOUND LINE X
	FDB	FDROMP	PHRASE 31 FRONT DOOR ROM ERROR
	FDB	FDRAMP	PHRASE 32 FRONT DOOR RAM ERROR
	FDB	SWTSTP	PHRASE 33 SWITCH TEST
	FDB	S$AUP	AUTO UP	34
	FDB	S$ADV	ADV	35
	FDB	S$RC	RIGHT COIN	36
	FDB	S$HSR	HIGH SCORE RESET37
	FDB	S$LC	LEFT COIN	38
	FDB	S$CC	CENTER COIN	39
	FDB	S$SS	SLAM SWITCH	40
	FDB	S$FIRE	FIRE MESSAGE	41
	FDB	S$THR	THRUST	42
	FDB	S$SB	SMART BOMB	43
	FDB	S$HYP	HYPER SPACE	44
	FDB	S$S2	START 2	45
	FDB	S$S1	START 1	46
	FDB	S$REV	REVERSE	47
	FDB	S$DOWN	DOWN	48
	FDB	S$UP	UP	49
	FDB	S$INV	INVISO	50
	FDB	CRTSTP	COLOR MATRIX TEST	51
	FDB	BOOKMP	BOOKKEEPING	52
	FDB	A$S1	LEFT SLOT	53
	FDB	A$S2	CENTER SLOT	54
	FDB	A$S3	RIGHT SLOT	55
	FDB	A$PDC	PAIT CREDITS	56
	FDB	A$FS	FREE SHIPS	57
	FDB	A$TIM	TOTAL PLAY TIME	58
	FDB	A$SHIP	TOTAL SHIP COUNT59
	FDB	A$PLAY	TOTAL PLAYS	60
	FDB	A$WARP	WARPS	61
	FDB	AUD1P	HIGH OF 6 DIG	62
	FDB	AUD2P	LOW OF 6 DIG	63
	FDB	AUD3P	4 DIG	64
	FDB	FROBMP	FROB		65
	FDB	CONTMP	CONTINUED ON NEXT PAGE	66
	FDB	J$REP	REPLAY ADJUSTMENT	67
	FDB	J$SHP	1 CRED SHIPS	68
	FDB	J$SHP2	2 CRED SHIPS	69
	FDB	J$CONS	COIN SELECT	70
	FDB	J$SL1	SLOT 1		71
	FDB	J$SL2	SLOT 2		72
	FDB	J$SL3	SLOT 3		73
	FDB	J$CUC	CREDIT UNIT	74
	FDB	J$CUB	BONUS UNIT	75
	FDB	J$MUN	MINIMUM UNITS NEEDED	76
	FDB	J$FREE	FREE PLAY	77
	FDB	J$MASA	MASTER ADJUST	78
	FDB	J$IND	INITIAL DIFFICULTY	79
	FDB	J$DCE	DIFFICULTY CEILING	80
	FDB	J$FBO	FIRST BONE WAVE	81
	FDB	J$LBO	LAST BONE WAVE	82
	FDB	J$BOS	BONE SIZE	83
	FDB	J$INV	INVISO TIME PER UNIT	84
	FDB	J$WRP	WARP MEN	85
	FDB	J$LWRP	LAST WARP WAVE	86
	FDB	J$GODL	GOD LETTERS	87
	FDB	J$FACS	FACTORY SETTINGS	88
	FDB	J$CAUD	CLEAR AUDIT	89
	FDB	J$CLHS	CLEAR HIGH SCORES	90
	FDB	J$AUTO	AUTO CYCLE	91
	FDB	J$OPM	OPERATOR MESSAGE	92
	FDB	J$GOD	LETTERS IN GOD	93
	FDB	REPMSP	REPLAY MESSAGE	94
	FDB	REPREP	RECOMMENDED REPLAY	95
	FDB	NOMESP	JUST THE NUMBER	96
	FDB	HIVOLP	HI VOLUME ARCADE	97
	FDB	RECOMP	RECOMMENDED SETTING	98
	FDB	POORPP	WEAK PLAYER SETTING	99
	FDB	CUSTMP	CUSTOM SETTINGS	100
	FDB	YESMP	YES ONLY	101
	FDB	NOMP	NO ONLY	102
	FDB	EXLIBP	EXTRA LIBERAL	103
	FDB	LIBP		104
	FDB	MODP		105
	FDB	CONSP		106
	FDB	EXCONP		107
	FDB	NO2CP		108
	FDB	CMIMEP		109
	FDB	RESMSP		110
	FDB	YES1MP		111
	FDB	COIN1P		112
	FDB	COIN2P		113
	FDB	COIN3P		114
	FDB	COIN4P		115
	FDB	COIN5P		116
	FDB	COIN6P		117
	FDB	COIN7P		118
	FDB	COIN8P		119
	FDB	NOREPP		120
	FDB	BOOKRP		121
	FDB	CRDP		122
	FDB	HSCRP		123
	FDB	SECP		124
	FDB	OPMESP		125
	FDB	HSRESP		126
	FDB	NPSP		127
	FDB	NCGP		128
	FDB	NSHP		129
	FDB	NSBP		130
	FDB	NIUP		131
	FDB	SEXP		132
	FDB	SECSP		133
	FDB	SWRPP		134
	FDB	GODMSP		135
	FDB	CONGP		136
	FDB	NOWMSP		137
	FDB	HSTBMP		138
	FDB	HSINDP		139
	FDB	HIGH1P		140
	FDB	HIGH3P		141
	FDB	BMBAP		142
	FDB	BTAP		143
	FDB	PODAP		144
	FDB	LNDAP		145
	FDB	MUTAP		146
	FDB	SGAP		147
	FDB	ONLY5P		148
	FDB	CENTRP		149
	FDB	CP2P		150
	FDB	YLBONP		151
	FDB	FBBONP		152

YLBONP	FCB	COLOR,$EE,CURSAB,$1C,$70,0
	FCC	'YLLABIAN STARFLEET TERMINATED'
	FCB	PERIOD,PERIOD,PERIOD,COLOR,$FF,CURSAB,$3C,$90,0
	FCC	'2500'
	FCB	SPACE,SPACE,COLOR,$DD
	FCC	'B'
	FCB	COLOR,$EE
	FCC	'O'
	FCB	COLOR,$FF
	FCC	'N'
	FCB	COLOR,$DD
	FCC	'U'
	FCB	COLOR,$EE
	FCC	'S'
	FCB	0

FBBONP	FCB	COLOR,$FF,CURSAB,$28,$70,0
	FCC	'FIREBOMBER STRIKEFORCE ANNIHILATED'
	FCB	SPACE,EXPT,COLOR,$AA,CURSAB,$38,$90,0
	FCC	'5000  BONUS'
	FCB	0

CENTRP	FCB	COLOR,$99,CURSAB,$13,$70,0
	FCC	'CENTER THE LINE USING THRUST AND FIRE'
	FCB	CURSAB,$1E,$7E,0
	FCC	'PRESS ADVANCE WHEN CENTERED'
	FCB	COLOR,$BB,SFONT,CURSAB,$4B,$50,0,ARROW
	FCB	0

OPMESP	FCB	COLOR,$BB,CURSAB,$2C,$16,0
	FCC	'ATTRACT MODE MESSAGE'
	FCB	SUB	
	FDB	TELSUB
	FCB	0

ONLY5P	FCB	COLOR,$BB,CURSAB,$20,$70,0
	FCC	'5 ENTRIES MAXIMUM PER PLAYER'
	FCB	CURSAB,$28,$90,1
	FCC	'LOWEST ENTRY REPLACED'
	FCB	0

TELSUB	FCB	SFONT,COLOR,$33,CURSAB,$2C,$C0,0
	FCB	SUB
	FDB	UPDNSB
	FCC	'LETTER'
	FCB	CURSAB,$37,$CC,0
	FCC	'USE FIRE TO ENTER LETTER'
	FCB	COLOR,$44
	FDB	0

PRPLYR	FCB	CURSAB,$40,$10,$0
	FCC	'PLAYER '
	FCB	0

GODMSP	FCB	COLOR,$BB,SUB	
	FDB	PRPLYR	
	FCB	ZBLANK,NUMY,CURSAB,$20,$28,0
	FCC	'YOU HAVE JUST COMPLETED THE'	
	FCB	CURSAB,$25,$38,0
	FCC	'GREATEST STARGATE MISSION'
	FCB	CURSAB,$36,$58,0
	FCC	'ENTER YOUR NAME'
	FCB	CURSAB,$34,$68,0,LPAREN
	FCC	'UP TO '
	FCB	ZBLANK,NUMB
	FCC	' LETTERS'
	FCB	RPAREN,SUB
	FDB	TELSUB
	FCB	0

CONGP	FCB	COLOR,$44,SUB	
	FDB	PRPLYR
	FCB	ZBLANK,NUMB
	FCB	CURSAB,$21,$2C,0
	FCC	'YOU HAVE ENTERED THE REALM'
	FCB	CURSAB,$23,$38,1
	FCC	'OF THE STARGATE IMMORTALS'
NOWSUB	FCB	CURSAB,$2D,$58,0
	FCC	'ENTER YOUR INITIALS'
	FCB	COLON,SUB
	FDB	TELSUB
	FCB	0

NOWMSP	FCB	COLOR,$99,SUB
	FDB	PRPLYR
	FCB	ZBLANK,NUMB
	FCB	CURSAB,$45,$64,0
	FCC	'ALSO'
	FCB	SUB
	FDB	NOWSUB
	FCB	0

HSTBMP	FCB	COLOR,$11,CURSAB,$3D,$3A,0
	FCC	'IMMORTALS'
	FCB	CURSAB,$40,$C0,0
	FCC	'MORTALS'
	FCB	0

HSINDP	FCB	SBLANK,NUMB,RPAREN,SPACE,SPACE,0

HIGH1P	FCB	SBLANK,NUMY,0

HIGH3P	FCB	CURSOR,$8,0,0,SBLANK,NUMY,0

REPMSP	FCB	SBLANK,NUMB
	FCC	'000'
	FCB	0

REPREP	FCB	SBLANK,NUMB
	FCC	'000  '
	FCB	SUB	
	FDB	RECS1
	FCB	0

SPSUB	FCB	CURSOR,8,0,0	MOVE 8 FULL BYTES TO THE RIGHT
	FCB	0

RECS1	FCC	'RECOMMENDED'
	FCB	0

RECS2	FCB	SUB
	FDB	SPSUB
	FCB	SUB
	FDB	RECS1
	FCB	0

NOMESP	FCB	SBLANK,NUMB,0

HIVOLP	FCB	SUB
	FDB	SVSUB
	FCC	'HIGH VOLUME ARCADES'
	FCB	0

RECOMP	FCB	SBLANK,NUMB,SUB
	FDB	RECS2
	FCB	0

POORPP	FCB	SUB
	FDB	SVSUB
	FCC	'FOR WEAKER PLAYERS'
	FCB	0

CUSTMP	FCB	SUB
	FDB	SVSUB
	FCC	'CUSTOM '
	FCB	HYPHEN,SPACE,LPAREN
	FCC	'ADJUST BELOW'
	FCB	RPAREN,0

YES1MP	FCC	'YES'
	FCB	SUB
	FDB	SPSUB
	FCB	CURSOR,-2,0,0 ALLOW FOR EXTRA LETTER 'S'
	FCC	'ADVANCE TO ACTIVATE'
	FCB	0

SVSUB	FCB	SBLANK,NUMB,SUB
	FDB	SPSUB
	FCB	0

YESMP	FCC	'YES'
	FCB	0

NOMP	FCC	'NO'
	FCB	0


EXLIBP	FCB	SUB	
	FDB	EXSBB
	FCB	SUB
	FDB	LIBS
	FCB	0

EXSBB	FCB	SUB
	FDB	SVSUB
	FCC	'EXTRA '
	FCB	0

LIBS	FCC	'LIBERAL'
	FCB	0

LIBP	FCB	SUB
	FDB	SVSUB
	FCB	SUB
	FDB	LIBS
	FCB	0

MODP	FCB	SUB

	FDB	SVSUB
	FCC	'MODERATE'
	FCB	0

CONSP	FCB	SUB
	FDB	SVSUB
	FCB	SUB
	FDB	CONSUB
	FCB	0

CONSUB	FCC	'CONSERVATIVE'
	FCB	0

EXCONP	FCB	SUB
	FDB	EXSBB
	FCB	SUB
	FDB	CONSUB
	FCB	0

NOREPP	FCB	SUB
	FDB	SVSUB
	FCC	'NO EXTRA SHIPS'
	FCB	0	

COIN1P	FCB	SUB	
	FDB	SVSUB
	FCC	'1'	
	FCB	SLASH	
	FCC	'FIFTY  3'
	FCB	SLASH
	FCC	'DOLLAR'
	FCB	0

COIN2P	FCB	SUB
	FDB	SVSUB
	FCC	'1'	
	FCB	SLASH
	FCC	'1 DM  6'
	FCB	SLASH 
	FCC	'5 DM'
	FCB	0

COIN3P	FCB	SUB
	FDB	SVSUB
	FCC	'1'	
	FCB	SLASH 
	FCC	'QUARTER  4'
	FCB	SLASH 
	FCC	'DOLLAR'
	FCB	0

COIN4P	FCB	SUB
	FDB	SVSUB
	FCC	'1'	
	FCB	SLASH
	FCC	'2 F  3'
	FCB	SLASH
	FCC	'5 F'
	FCB	0

COIN5P	FCB	SUB
	FDB	SVSUB
	FCC	'1'
	FCB	SLASH
	FCC	'FIFTY  2'
	FCB	SLASH
	FCC	'DOLLAR'
	FCB	0

COIN6P	FCB	SUB
	FDB	SVSUB
	FCC	'1'	
	FCB	SLASH
	FCC	'25  4'
	FCB	SLASH
	FCC	'1 G'
	FCB	0

COIN7P	FCB	SUB
	FDB	SVSUB
	FCC	'1'	
	FCB	SLASH
	FCC	'5 F  2'
	FCB	SLASH
	FCC	'10 F'
	FCB	0

COIN8P	FCB	SUB
	FDB	SVSUB
	FCC	'1'
	FCB	SLASH
	FCC	'10 F'
	FCB	0

NO2CP	FCB	SUB
	FDB	SVSUB
	FCC	'NO 2'	
	FCB	SUB
	FDB	SHIP2
	FCB	0

CMIMEP	FCB	COLOR,$22,CURSAB,$30,$80,0
	FCC	'ADJUSTMENT FAILURE'
	FCB	CURSAB,$20,$A0,0	
	FCC	'RESTORE'
	FCB	SUB
	FDB	FACSUB
	FCC	'BY'
	FCB	CURSAB,$20,$B0,0,SUB
	FDB	CMISUB
	FCB	COKSUB
	FDB	CMICOK
	FCB	CURSAB,$20,$C0,0
	FCC	'AND TURNING GAME OFF AND ON'	
	FCB	0

FACSUB	FCC	' FACTORY SETTINGS '
	FCB	0

RESMSP	FCB	COLOR,$99,CURSAB,$21,$80,0,SUB
	FDB	FACSUB
	FCC	' RESTORED'
	FCB	0

CMISUB	FCC	'OPENING FRONT DOOR'
	FCB	0

CMICOK	FCB	CURSAB,$20,$B0,0,COLOR,$00,SUB
	FDB	CMISUB
	FCB	CURSAB,$20,$B0,0,COLOR,$22
	FCC	'RAISING TABLE TOP'
	FCB	0

FROBMP	FCB	COLOR,$99,FROB,COLOR,$88,0	DISPLAY FROB

CONTMP	FCB	LFONT,COLOR,$99,CURSAB,$37,$10,0
	FCC	'GAME ADJUSTMENT'
	FCB	SFONT,CURSAB,$2D,$C4,0,COLOR,$BB,SUB
	FDB	UPDNSB
	FCC	'ADJUSTMENT'
	FCB	CURSAB,$2D,$CE,0
	FCC	'USE THRUST AND FIRE TO CHANGE THE VALUE'
	FCB	CURSAB,$11,$B3,0,COLOR,$88
	FCC	'MORE ADJUSTMENTS'	
	FCB	CURSAB,$3D,$DE,0,COLOR,$33
	FCC	'PRESS ADVANCE TO EXIT'
	FCB	0

UPDNSB	FCC	'USE UP'
	FCB	HYPHEN
	FCC	'DOWN LEVER TO SELECT '
	FCB	0

J$REP	FCC	'EXTRA SHIP EVERY'
	FCB	0

J$SHP	FCB	SUB	
	FDB	SHIP1
	FCC	'1'
	FCB	SUB
	FDB	SHIP2
	FCB	0

SHIP1	FCC	'SHIPS FOR '
	FCB	0

SHIP2	FCC	' CREDIT GAME'
	FCB	0

J$SHP2	FCB	SUB
	FDB	SHIP1
	FCC	'2'
	FCB	SUB
	FDB	SHIP2
	FCB	0

J$CONS	FCC	'PRICING SELECTION'
	FCB	0

J$SL1	FCC	'LEFT'
	FCB	SUB
	FDB	SLSUB
	FCB	0

J$SL2	FCC	'CENTER'
	FCB	SUB
	FDB	SLSUB
	FCB	0

J$SL3	FCC	'RIGHT'
	FCB	SUB
	FDB	SLSUB
	FCB	0	

SLSUB	FCC	' SLOT UNITS'
	FCB	0

J$CUC	FCB	SUB	
	FDB	UNFOR
	FCC	'CREDIT'
	FCB	0

J$CUB	FCB	SUB
	FDB	UNFOR
	FCC	'BONUS CREDIT'
	FCB	0

J$MUN	FCC	'MINIMUM UNITS FOR ANY CREDIT'
	FCB	0

UNFOR	FCC	'UNITS REQUIRED FOR '
	FCB	0

J$FREE	FCC	'FREE PLAY'
	FCB	0

J$MASA	FCC	'MASTER'
	FCB	SUB
	FDB	DIFC
	FCC	'CONTROL'
	FCB	0

DIFC	FCC	' DIFFICULTY '
	FCB	0

J$IND	FCC	'INITIAL'
	FCB	SUB
	FDB	DIFC
	FCB	0

J$DCE	FCC	'MAXIMUM'
	FCB	SUB
	FDB	DIFC
	FCB	0

J$FBO	FCC	'1ST'	
	FCB	SUB
	FDB	EXHRD
	FCB	0

J$LBO	FCC	'LAST'
	FCB	SUB
	FDB	EXHRD
	FCB	0

EXHRD	FCC	' WAVE OF ACCELERATED DIFFICULTY'
	FCB	0

J$BOS	FCC	'DIFFICULTY ACCELERATION RATE'
	FCB	0

J$INV	FCC	'INVISO TIME PER SHIP'
	FCB	0

J$WRP	FCC	'MEN NEEDED TO WARP'	
	FCB	0

J$LWRP	FCC	'LAST WAVE WARP ALLOWED'
	FCB	0

J$GODL	FCC	'LETTERS FOR'	
	FCB	SUB
	FDB	HISCR
	FCB	0

HISCR	FCC	' HIGHEST SCORE NAME'
	FCB	0

J$FACS	FCC	'RESTORE'	
	FCB	SUB
	FDB	FACSUB
	FCB	0

J$CAUD	FCC	'CLEAR'		
	FCB	SUB
	FDB	BKTSUB
	FCB	0

J$CLHS	FCC	'RESET HIGH SCORE TABLE'
	FCB	0

J$AUTO	FCC	'AUTO CYCLE'
	FCB	0

J$OPM	FCC	'SET ATTRACT MODE MESSAGE'
	FCB	0

J$GOD	FCC	'SET'
	FCB	SUB
	FDB	HISCR
	FCB	0

AUD1P	FCB	SBLANK,NUMB,0	SETUP SKIP-BLANKING

AUD2P	FCB	NBLANK,NUMY,0	NO BLANKING ON THIS PART

AUD3P	FCB	CURSOR,6,0,0	MOVE 2 CHARACTERS RIGHT
	FCB	SBLANK,NUMY,0	THEN PRINT NUMBER WITH SPACING

BOOKMP	FCB	CURSAB,$2F,$10,0
	FCB	COLOR,$99,SUB
	FDB	BKTSUB
	FCB	COLOR,$88,0

BOOKRP	FCB	CURSAB,$20,$60,0
	FCB	COLOR,$33,SUB
	FDB	BKTSUB
	FCC	'CLEARED'
	FCB	0

HSRESP	FCB	CURSAB,$2A,$40,0
	FCB	COLOR,$88
	FCC	'HIGH SCORE TABLE RESET'
	FCB	0

BKTSUB	FCC	' BOOKKEEPING TOTALS '
	FCB	0

AUDX1	EQU	$1C
AUDX2	EQU	$6C

A$PDC	FCB	CURSAB,AUDX1,$60,0
	FCC	'PAID CREDITS'
	FCB	CURSAB,AUDX2,$60,0,0

A$FS	FCB	CURSAB,AUDX1,$70,0
	FCC	'EXTRA SHIPS'	
	FCB	CURSAB,AUDX2,$70,0,0

A$TIM	FCB	CURSAB,AUDX1,$80,0
	FCC	'PLAY TIME IN MINUTES'
	FCB	CURSAB,AUDX2,$80,0,0

A$SHIP	FCB	CURSAB,AUDX1,$90,0
	FCC	'SHIPS PLAYED'
	FCB	CURSAB,AUDX2,$90,0,0

A$PLAY	FCB	CURSAB,AUDX1,$A0,0
	FCC	'TOTAL PLAYS'
	FCB	CURSAB,AUDX2,$A0,0,0

A$WARP	FCB	CURSAB,AUDX1,$B0,0
	FCC	'WARPS'
	FCB	CURSAB,AUDX2,$B0,0,0

A$S1	FCB	CURSAB,AUDX1,$30,0
	FCC	'LEFT'
	FCB	SUB
	FDB	COINSB
	FCB	CURSAB,AUDX2,$30,0,0

A$S2	FCB	CURSAB,AUDX1,$40,0
	FCC	'CENTER'
	FCB	SUB
	FDB	COINSB
	FCB	CURSAB,AUDX2,$40,0,0

A$S3	FCB	CURSAB,AUDX1,$50,0
	FCC	'RIGHT'
	FCB	SUB
	FDB	COINSB
	FCB	CURSAB,AUDX2,$50,0,0

COINSB	FCC	' SLOT COINS'
	FCB	0

CRTSTP	FCB	COLOR,$33,CURSAB,$3A,$80,0
	FCC	'COLOR RAM TEST'
	FCB	CURSAB,$24,$B0,0
	FCC	'VERTICAL BARS INDICATE ERROR'
	FCB	0

S$AUP	FCC	'AUTO UP'
	FCB	0

S$ADV	FCC	'ADVANCE'
	FCB	0

S$RC	FCC	'RIGHT COIN'
	FCB	0

S$HSR	FCC	'HIGH SCORE RESET'
	FCB	0

S$LC	FCC	'LEFT COIN'
	FCB	0

S$CC	FCC	'CENTER COIN'
	FCB	0

S$SS	FCC	'SLAM SWITCH'
	FCB	0

S$FIRE	FCC	'FIRE'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

S$THR	FCC	'THRUST'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

S$SB	FCC	'SMART BOMB'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

S$HYP	FCC	'HYPERSPACE'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

S$S2	FCC	'2'

	FCB	SUB
	FDB	PSTSUB
	FCB	0

S$S1	FCC	'1'
	FCB	SUB	
	FDB	PSTSUB
	FCB	0

PSTSUB	FCC	' PLAYER START'
	FCB	0

S$REV	FCC	'REVERSE'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

S$DOWN	FCC	'DOWN'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

S$UP	FCC	'UP'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

S$INV	FCC	'INVISO'
	FCB	COKSUB
	FDB	PLYSUB
	FCB	0

PLYSUB	FCC	'  '
	FCB	LPAREN
	FCC	'PLAYER '
	FCB	ZBLANK,NUMB,RPAREN,0

SWTSTP	FCB	COLOR,$88,CURSAB,$3A,26,0
	FCC	'SWITCH TEST'
	FCB	0

ROMOKP	FCB	COLOR,$99,CURSAB,$3E,$50,$0
	FCC	'ALL ROMS OK'
	FCB	CURSAB,$36,$90,0,COLOR,$33
	FCC	'RAM TEST FOLLOWS'
	FCB	CURSAB,$2E,$A0,0
	FCC	'PRESS ADVANCE TO EXIT'
	FCB	0

RAMSUB	FCC	' RAM ERRORS DETECTED'
	FCB	0

RAMOKP	FCB	COLOR,$99,CURSAB,$2A,$80,0
	FCC	'NO'
	FCB	SUB
	FDB	RAMSUB
	FCB	0

CMOKP	FCB	COLOR,$99,CURSAB,$21,$80,0
	FCC	'NO CMOS'
	FCB	SUB
	FDB	RAMSUB
	FCB	0

CMSUB	FCC	'CMOS RAM'
	FCB	SUB
	FDB	ERRSUB
	FCB	0

CMERRP	FCB	COLOR,$22,CURSAB,$36,$80,0
	FCB	SUB	
	FDB	CMSUB
	FCB	0

CMDORP	FCB	SUB		
	FDB	CMERRP
	FCB	CURSAB,$28,$90,0
	FCC	'OR WRITE PROTECT FAILURE'
	FCB	COLOR,$99,SUB	
	FDB	FDSUBB
	FCB	COKSUB
	FDB	FDCOCK
	FCB	0

FDCOCK	FCB	COLOR,$00,SUB
	FDB	FDSUBB
	FCB	COLOR,$99,CURSAB,$17,$B0,0,HYPHEN
	FCC	'TABLE TOP MUST BE RAISED FOR TEST'
	FCB	HYPHEN,0

FDSUBB	FCB	CURSAB,$18,$B0,0,HYPHEN
	FCC	'FRONT DOOR MUST BE OPEN FOR TEST'
	FCB	HYPHEN,0

SNDTEP	FCB	COLOR,$99,CURSAB,$3A,$80,0
	FCC	'SOUND LINE '
	FCB	ZBLANK,NUMB,0
FBAP	FCB	COLOR,$DD
	FCC	'FIREBOMBER'
	FCB	HYPHEN
	FCC	'250   FIREBALL'
	FCB	HYPHEN
	FCC	'100'
	FCB	0
YLAP	FCB	COLOR,$FF
	FCC	'YLLABIAN SPACE GUPPY'
	FCB	HYPHEN
	FCC	'200'
	FCB	0
MCHAP	FCB	COLOR,$88
	FCC	'PHRED'
	FCB	HYPHEN
	FCC	'200'
	FCB	COLOR,$FF
	FCC	' BIG RED'
	FCB	HYPHEN
	FCC	'200'
	FCB	COLOR,$33
	FCC	' MUNCHIES'
	FCB	HYPHEN
	FCC	'50'
	FCB	0
DYNAP	FCB	COLOR,$99
	FCC	'DYNAMO'
	FCB	HYPHEN
	FCC	'200   SPACE HUM'
	FCB	HYPHEN
	FCC	'100'
	FCB	0
BMBAP	FCB	COLOR,$DD
	FCC	'BOMBER'
	FCB	HYPHEN
	FCC	'250'
	FCB	0
BTAP	FCB	COLOR,$33
	FCC	'BAITER'
	FCB	HYPHEN
	FCC	'200'
	FCB	0
PODAP	FCB	COLOR,$22
	FCC	'POD'
	FCB	HYPHEN
	FCC	'1000   SWARMER'
	FCB	HYPHEN
	FCC	'150'
	FCB	0
LNDAP	FCB	COLOR,$44
	FCC	'LANDER'
	FCB	HYPHEN
	FCC	'150'
	FCB	0
MUTAP	FCB	COLOR,$CC
	FCC	'MUTANT'
	FCB	HYPHEN
	FCC	'150'
	FCB	0
SGAP	FCB	COLOR,$AA,LFONT
	FCC	'STARGATE   ENEMIES'
	FCB	0
CPP	FCB	COLOR,$AA
	FCC	'COPYRIGHT 1981'
	FCB	0
WEIP	FCC	'WILLIAMS ELECTRONICS INC'
	FCB	PERIOD,0
CRDP	FCB	COLOR,$11
	FCC	'CREDITS'
	FCB	COLON,SPACE,ZBLANK,NUMB,0
HSCRP	FCB	COLOR,$88
	FCC	'HIGH SCORE DISPLAY'
	FCB	COLON
	FCC	'PRESS HYPERSPACE'
	FCB	0
SECP	FCC	'GAME SECRETS'
	FCB	COLON
	FCC	'PRESS REVERSE'
	FCB	0
NPSP	FCB	COLOR,$AA,ZBLANK,NUMB
	FCC	' PLAYER START'
	FCB	0
NCGP	FCB	COLOR,$FF,ZBLANK,NUMB
	FCC	' CREDIT GAME'
	FCB	0
NSHP	FCB	COLOR,$11,ZBLANK,NUMB
	FCC	' SHIPS'
	FCB	0
NSBP	FCB	NUMB
	FCC	' SMART BOMBS'
	FCB	0
NIUP	FCB	NUMB
	FCC	' INVISO UNITS'
	FCB	0
SEXP	FCB	COLOR,$AA,ZBLANK
	FCC	'EXTRA SHIP'
	FCB	COMMA
	FCC	' SMART BOMB'
	FCB	COMMA
	FCC	' INVISO TIME AWARDED EVERY '
	FCB	NUMB
	FCC	'000 POINTS'
	FCB	0
SECSP	FCB	COLOR,$88
	FCC	'S'
	FCB	COLOR,$77
	FCC	'E'
	FCB	COLOR,$66
	FCC	'C'
	FCB	COLOR,$55
	FCC	'R'
	FCB	COLOR,$44
	FCC	'E'
	FCB	COLOR,$33
	FCC	'T'
	FCB	COLOR,$22
	FCC	'S'
	FCB	0
SWRPP	FCB	COLOR,$88,CURSAB,$10,$55,0
	FCC	'WARP'
	FCB	COLON,SFONT
	FCC	' RESCUE '
	FCB	ZBLANK,NUMY
	FCC	' HUMANOIDS AND CARRY THEM INTO STARGATE'
	FCB	CURSAB,$22,$5D,0
	FCC	'WARP NOT ALLOWED AFTER WAVE '
	FCB	NUMB,CURSAB,$22,$63,0
	FCC	'FLYING BACKWARDS INTO STARGATE INHIBITS WARP'
	FCB	COLOR,$77,LFONT,CURSAB,$10,$6F,0
	FCC	'INVISO'
	FCB	COLON,SFONT
	FCC	'ACTIVATE INVISO ANTIMATTER CLOAKING DEVICE TO DISAPPEAR'
	FCB	CURSAB,$24,$77,0
	FCC	'AND DESTROY EVERYTHING IN YOUR PATH'
	FCB	CURSAB,$23,$7D,0
	FCC	' RED LINE BELOW SMART BOMB DISPLAY SHOWS INVISO TIME LEFT'
	FCB	LFONT,CURSAB,$10,$89,0,COLOR,$66
	FCC	'STARGATE'
	FCB	COLON,CURSAB,$3F,$89,0,SFONT
	FCC	'TRANSPORTS PLAYER TO HUMANOIDS BEING'
	FCB	CURSAB,$3F,$91,0
	FCC	'ABDUCTED'
	FCB	PERIOD,SPACE,SPACE
	FCC	'OTHERWISE TRANSPORTS PLAYER'
	FCB	CURSAB,$3F,$97,0
	FCC	'TO OPPOSITE SIDE OF PLANET'
	FCB	LFONT,CURSAB,$10,$B7,0,COLOR,$44
	FCC	'SMART BOMB'
	FCB	COLON,SFONT
	FCC	' DESTROYS ALL ENEMIES ON SCREEN'
	FCB	LFONT,CURSAB,$10,$A3,0,COLOR,$55
	FCC	'HUMANOIDS'
	FCB	COLON,CURSAB,$32,$A3,0,SFONT
	FCC	'PLANET EXPLODES WHEN ALL HUMANOIDS LOST'
	FCB	CURSAB,$32,$AB,0
	FCC	'HUMANOIDS RESTORED AFTER EVERY 5TH WAVE'
	FCB	0
CP2P	FCB	LPAREN
	FCC	'C'
	FCB	RPAREN
	FCC	' 1981 WILLIAMS ELECTRONICS INC'
	FCB	PERIOD,0
INSUB	FCB	CURSAB,$30,$70,0
	FCC	'INITIAL TESTS INDICATE'
	FCB	COLON,0

OKMP	FCB	COLOR,$99,SUB
	FDB	INSUB	
	FCB	CURSAB,$3A,$90,0
	FCC	'ALL SYSTEMS GO'
	FCB	0

INERSB	FCB	COLOR,$22,SUB	IN RED,
	FDB	INSUB	INITIAL TESTS INDICATE,
	FCB	CURSAB,$40,$90,0,0 AND SET CURSOR FOR ERROR

FDSUB	FCB	CURSAB,$40,$80,0
	FCB	COLOR,$22
	FCB	0

FDROMP	FCB	SUB
	FDB	FDSUB
	FCC	'ROM'
	FCB	SUB
	FDB	ERRSUB
	FCB	NUMB,0

FDRAMP	FCB	SUB
	FDB	FDSUB
	FCC	'RAM'
	FCB	SUB
	FDB	ERRSUB
	FCB	NUMB,0

ROMERP	FCB	SUB
	FDB	INERSB
	FCC	'ROM'
	FCB	SUB
	FDB	ERRSUB
	FCB	NUMB,0

RERRMP	FCB	SUB	
	FDB	INERSB
	FCC	'RAM'
	FCB	SUB
	FDB	ERRSUB
	FCB	NUMB,0

ERRSUB	FCC	' ERROR '
	FCB	ZBLANK,0

CHSUB	FCB	CURSAB,$3F,$70,0,COLOR,$DD
	FCB	ZBLANK
	FCC	' WAVE '
	FCB	NUMB,COLON
	FCB	0

CHYLMP	FCB	SUB
	FDB	CHSUB
	FCB	COLOR,$AA
	FCB	CURSAB,$32,$88,0
	FCC	'YLLABIAN DOGFIGHT'
	FCB	0

CHFBMP	FCB	SUB
	FDB	CHSUB
	FCB	COLOR,$CC
	FCB	CURSAB,$2E,$88,0
	FCC	'FIREBOMBER SHOWDOWN'
	FCB	0

PROMP	FCB	COLOR,$55
	FCC	'2000'
	FCB	EXPT,0

FEWMP	FCB	COLOR,$FF,CURSAB	
	FDB	MESSUL+(11*256)
	FCB	0
	FCC	' HUMANOIDS REMAINING'
	FCB	COLON,SPACE,ZBLANK,NUMB,0

PODMP	FCB	COLOR,$88,CURSAB
	FDB	MESSUL+(12*256)
	FCB	0
	FCC	'POD INTERSECTION '
	FCB	HYPHEN	
	FCC	' 0'
	FCB	COLON,0


PODTMP	FCB	COLOR,$88,CURSAB
	FDB	MESSUL+(49*256)
	FCB	0,NBLANK,NUMB,0

DEFMP	FCB	COLIND		
	FDB	SCNCOL
	FCB	CURSAB
	FDB	MESSUL+(20*256)
	FCB	0,ARROW
	FCC	'  SCANNER  '
	FCB	ARROW,0

TBLOMP	FCB	COLOR,$55,CURSAB
	FDB	MESSUL+$A00
	FCB	0
	FCC	'PLANET SURFACE UNSTABLE'	
	FCB	EXPT,0

LNDKLP	FCB	COLOR,$55,CURSAB
	FDB	MESSUL+$C00
	FCB	0
	FCC	'ALL LANDERS DESTROYED'
	FCB	EXPT,0

NWARPP	FCB	COLOR,$77,CURSAB
	FDB	MESSUL+$400
	FCB	0,ZBLANK,SUB
	FDB	WRSUB
	FCB	NUMB,SUB
	FDB	ADSUB
	FCC	'EN'
	FCB	0

WRSUB	FCC	'WARP REQUIRES '
	FCB	0

ADSUB	FCC	' ADDITIONAL M'
	FCB	0

NOWRPP	FCB	COLOR,$77,CURSAB
	FDB	MESSUL+$400
	FCB	0,SUB
	FDB	WRSUB	WARP REQUIRES
	FCC	'1'	1
	FCB	SUB
	FDB	ADSUB	ADDITIONAL M
	FCC	'AN'	AN
	FCB	0

PLYRMP	FCB	COLOR,$11
	FCC	'PLAYER '
	FCB	ZBLANK,NUMB,0

GOMP	FCB	COLOR,$AA
	FCC	'GAME OVER'
	FCB	0

WARPMP	FCB	COLOR,$99
	FCB	CURSAB,$34,$50,0
	FCB	PERIOD,PERIOD,PERIOD
	FCC	'WARP TO WAVE '
	FCB	ZBLANK,NUMB
	FCB	0

ATAKMP	FCB	COLOR,$11
	FCB	CURSAB,$36,$50,0 (MATCH THE PROMS!!!)
	FCC	'ATTACK WAVE '
	FCB	ZBLANK,NUMB
	FCB	CURSAB,$3C,$60,0
	FCC	'COMPLETED'
	FCB	0

ATWVMP	FCB	COLOR,$FF
	FCB	CURSAB,$38,$90,0
	FCC	'BONUS  X  '
	FCB	NUMY,0

	FCC	'$/ THIS IS STARGATE '
	FCC	'$$/ DESIGNED EXCLUSIVELY FOR '
	FCC	'$/ WILLIAMS ELECTRONICS INC '
	FCC	'$/ BY EUGENE P. JARVIS AND LAWRENCE E. DEMAR '
	FCC	'$$/ COPYRIGHT 1981 WILLIAMS ELECTRONICS INC. '
	FCC	'$/ ALL RIGHTS RESERVED'

	END
